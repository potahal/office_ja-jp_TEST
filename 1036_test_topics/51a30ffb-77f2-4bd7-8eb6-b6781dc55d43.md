
# Mettre en surbrillance la cellule, la ligne ou la colonne active

Les exemples de code suivants montrent comment mettre en surbrillance la cellule active ou les lignes et les colonnes qui contiennent la cellule active. Ces exemples utilisent l'événement  **[SelectionChange](183e2ca7-06b2-f689-1f77-182dbfbf1e1d.md)** de l'objet **[Worksheet](182b705e-854a-81cc-a4b0-59b942de55ae.md)**.

 **Exemple de code fourni par :** Tom Urtis,[Atlas Programming Management](http://www.atlaspm.com/)

## Mise en surbrillance de la cellule active

L'exemple de code suivant efface la couleur de toutes les cellules de la feuille de calcul en définissant la propriété  **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** sur 0, puis met en surbrillance la cellule active en définissant la propriété **ColorIndex** sur 8 (turquoise).


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    Application.ScreenUpdating = False
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    ' Highlight the active cell
    Target.Interior.ColorIndex = 8
    Application.ScreenUpdating = True
End Sub
```


## Mise en surbrillance de la ligne et de la colonne entières contenant la cellule active

L'exemple de code suivant efface la couleur de toutes les cellules de la feuille de calcul en définissant la propriété  **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** sur 0, puis met en surbrillance la ligne et la colonne entières contenant la cellule active à l'aide des propriétés **[EntireRow](9e66da51-6cef-4109-ea4e-2acaad42aa1f.md)** et **[EntireColumn](7be55670-75fd-fb02-dc1a-9d70e3a9d80d.md)**.


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    If Target.Cells.Count > 1 Then Exit Sub
    Application.ScreenUpdating = False
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    With Target
        ' Highlight the entire row and column that contain the active cell
        .EntireRow.Interior.ColorIndex = 8
        .EntireColumn.Interior.ColorIndex = 8
    End With
    Application.ScreenUpdating = True
End Sub
```


## Mise en surbrillance de la ligne et de la colonne contenant la cellule active, dans la zone active

L'exemple de code suivant efface la couleur de toutes les cellules de la feuille de calcul en définissant la propriété  **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** sur 0, puis met en surbrillance la ligne et la colonne contenant la cellule active, dans la zone active, à l'aide de la propriété **[CurrentRegion](39277cc5-07ff-8453-7330-b272b365f9dc.md)** de l'objet **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)**.


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    If IsEmpty(Target) Or Target.Cells.Count > 1 Then Exit Sub
    Application.ScreenUpdating = False
    With ActiveCell
        ' Highlight the row and column that contain the active cell, within the current region
        Range(Cells(.Row, .CurrentRegion.Column), Cells(.Row, .CurrentRegion.Columns.Count + .CurrentRegion.Column - 1)).Interior.ColorIndex = 8
        Range(Cells(.CurrentRegion.Row, .Column), Cells(.CurrentRegion.Rows.Count + .CurrentRegion.Row - 1, .Column)).Interior.ColorIndex = 8
    End With
    Application.ScreenUpdating = True
End Sub
```


## À propos du collaborateur
<a name="AboutContributor"> </a>

Élu MVP, Tom Urtis est le fondateur d'Atlas Programming Management, une société proposant une gamme complète de solutions commerciales Microsoft Office et Excel et située dans la Silicon Valley. Tom possède plus de 25 ans d'expérience dans la gestion d'entreprise et le développement d'applications Microsoft Office. Il est également le co-auteur de « Holy Macro! It's 2,500 Excel VBA Examples ».

