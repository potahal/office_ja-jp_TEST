
# ComboBox.NotInList, événement (Access)

L'événement  **NotInList** se produit lorsque l'utilisateur entre une valeur dans la zone de texte d'une zone de liste modifiable et que cette valeur ne figure pas dans la liste correspondante.
 


## Syntaxe

*expression* . **NotInList**( ***NewData***, ***Response*** )
 

 
*expression* Variable qui représente un objet **ComboBox**.
 

 

### Paramètres



|**Nom**|**Obligatoire/Facultatif**|**Type de données**|**Description**|
|:-----|:-----|:-----|:-----|
| _NewData_|Obligatoire|**Chaîne**|Chaîne que Microsoft Access utilise pour transmettre à la procédure événementielle le texte que l'utilisateur a entré dans la zone de texte de la zone de liste modifiable.|
| _Response_|Obligatoire|**Entier**|La valeur indique comment l'événement  **NotInList** a été géré. L'argument _Response_ peut être l'une des constantes intrinsèques suivantes : **acDataErrDisplay** (par défaut) Affiche le message par défaut à l'utilisateur. Vous pouvez utiliser cette constante quand vous ne souhaitez pas autoriser l'utilisateur à ajouter une nouvelle valeur dans la liste de zone modifiable. **acDataErrContinue** N'affiche pas le message par défaut à l'utilisateur. Vous pouvez utiliser cette constante quand vous souhaitez afficher un message personnalisé à l'intention de l'utilisateur. Par exemple, la procédure événementielle peut afficher une boîte de dialogue personnalisée pour demander à l'utilisateur s'il veut enregistrer la nouvelle entrée. Si la réponse est Oui, la procédure événementielle ajoute cette nouvelle entrée dans la liste et affecte la valeur **acDataErrAdded** à l'argument _Response_. Si la réponse est Non, la procédure événementielle affecte la valeur  **acDataErrContinue** à l'argument _Response_. **acDataErrAdded** N'affiche pas de message à l'intention de l'utilisateur mais vous permet d'ajouter l'entrée à la zone de liste modifiable dans la procédure événementielle **NotInList**. Une fois l'entrée ajoutée, Microsoft Access réactualise la liste. Microsoft Access vérifie ensuite que la chaîne figure bien dans la zone de liste modifiable, puis enregistre la valeur de l'argument _NewData_ dans le champ auquel est liée la zone de liste modifiable. Si la chaîne n'est pas dans la liste, Microsoft Access affiche un message d'erreur.|

## Remarques

Pour exécuter une macro ou une procédure événementielle lorsque cet événement se produit, affectez le nom de la macro ou [Event Procedure] à la propriété  **[OnNotInList](307e9f0c-6db7-b995-166b-060c697b9f6e.md)**.
 

 
Cet événement permet à l'utilisateur d'ajouter une nouvelle valeur à la liste de la zone de liste modifiable.
 

 
La propriété  **[LimitToList](885ed814-6e04-b9f1-0acb-3ded28e00f93.md)** doit avoir la valeur Oui pour que l'événement **NotInList** se produise.
 

 
L'événement  **NotInList** n'entraîne pas l'événement **Error**.
 

 
L'événement  **NotInList** a lieu pour les zones de liste modifiable dont la propriété **LimitToList** a pour valeur Oui, lorsque vous tapez une valeur qui ne figure pas dans la liste et que vous essayez de vous placer sur un autre contrôle ou de sauvegarder l'enregistrement. Il intervient après tous les événements **Change** pour la zone de liste modifiable.
 

 
Quand la valeur Oui est affectée à la propriété  **[AutoExpand](0b3fabf8-4004-0868-3ddc-aef297514324.md)**, Microsoft Access choisit des valeurs correspondantes dans la liste à mesure que l'utilisateur tape des caractères dans la zone de texte de la zone de liste modifiable. Si les caractères que l'utilisateur tape correspondent aux premiers caractères d'une valeur de la liste (par exemple, l'utilisateur tape « Dubon » et la liste contient « Dubonnet »), l'événement **NotInList** n'a pas lieu quand l'utilisateur passe à un autre contrôle ou sauvegarde l'enregistrement. Cependant, les caractères que Microsoft Access ajoute à ceux tapés par l'utilisateur (dans l'exemple, « net ») sont sélectionnés dans la zone de texte de la zone de liste modifiable. Si l'utilisateur souhaite que l'événement **NotInList** ait lieu en pareil cas (par exemple, l'utilisateur souhaite ajouter le nouveau nom « Dubon » à la zone de liste modifiable), il doit taper un caractère **ESPACE**,  **RETOUR-ARRIÈRE** ou **SUPPR** derrière le dernier caractère de la nouvelle valeur.
 

 
Quand la propriété  **LimitToList** a la valeur Oui et que la zone de liste modifiable est déroulée, Microsoft Access choisit dans la liste les valeurs pouvant correspondre aux caractères tapés par l'utilisateur dans la zone de texte de la zone de liste modifiable, même si la propriété **AutoExpand** a la valeur Non. Si l'utilisateur appuie sur **ENTRÉE** ou passe à un autre contrôle ou enregistrement, la valeur choisie apparaît dans la zone de liste modifiable. Dans ce cas, l'événement **NotInList** n'a pas lieu. Pour déclencher l'événement **NotInList**, l'utilisateur ne doit pas dérouler la liste de la zone de liste modifiable.
 

 

## Exemple

L'exemple suivant utilise l'événement  **NotInList** pour ajouter un élément à une zone de liste déroulante.
 

 
Pour essayer cet exemple, créez une zone de liste déroulante appelée Colors (Couleurs) dans un formulaire. Affectez la valeur Oui à la propriété  **LimitToList** de cette zone de liste déroulante. Pour remplir la zone de liste déroulante, définissez sa propriété **RowSourceType** en tant que liste de valeurs et fournissez une liste de valeurs séparées par des points-virgules comme paramètre de la propriété **RowSource**. Fournissez, par exemple, les valeurs suivantes pour cette propriété : Rouge; Vert; Bleu.
 

 
Ensuite, ajoutez la procédure événementielle suivante au formulaire. Passez en mode Formulaire et entrez une nouvelle valeur dans l'espace réservé au texte de la zone de liste modifiable. 
 

 

 **Remarque**  L'exemple précédent ajoute un élément à une zone de liste modifiable indépendante. Dans ce cas, vous ajoutez une valeur à un champ de la source de données sous-jacente. La plupart du temps, vous ne pouvez pas ajouter simplement un champ dans un nouvel enregistrement : selon la structure des données de la table, vous devrez probablement ajouter un ou plusieurs champs pour satisfaire aux conditions requises par les données. Par exemple, un nouvel enregistrement doit inclure des valeurs pour les champs comportant la clé primaire. Si vous avez besoin d'ajouter en temps réel des éléments à une zone de liste modifiable, vous devez demander à l'utilisateur d'entrer des données pour tous les champs requis, d'enregistrer le nouvel enregistrement, puis d'actualiser la zone de liste modifiable pour afficher la nouvelle valeur.
 




```
Private Sub Colors_NotInList(NewData As String, _ 
        Response As Integer) 
    Dim ctl As Control 
     
    ' Return Control object that points to combo box. 
    Set ctl = Me!Colors 
    ' Prompt user to verify they wish to add new value. 
    If MsgBox("Value is not in list. Add it?", _ 
         vbOKCancel) = vbOK Then 
        ' Set Response argument to indicate that data 
        ' is being added. 
        Response = acDataErrAdded 
        ' Add string in NewData argument to row source. 
        ctl.RowSource = ctl.RowSource &amp; ";" &amp; NewData 
    Else 
    ' If user chooses Cancel, suppress error message 
    ' and undo changes. 
        Response = acDataErrContinue 
        ctl.Undo 
    End If 
End Sub
```


 

 
L'exemple suivant montre comment utiliser l'événement  **NotInList** pour ajouter un élément à une zone de liste modifiable liée.
 

 
 **Exemple de code fourni par :** Bill Jelen,[MrExcel.com](http://www.mrexcel.com/)
 

 



```
Private Sub cboDept_NotInList(NewData As String, Response As Integer)
    Dim oRS As DAO.Recordset, i As Integer, sMsg As String
    Dim oRSClone As DAO.Recordset

    Response = acDataErrContinue

    If MsgBox("Add dept?", vbYesNo) = vbYes Then
        Set oRS = CurrentDb.OpenRecordset("tblDepartments", dbOpenDynaset)
        oRS.AddNew
        oRS.Fields(1) = NewData
        For i = 2 To oRS.Fields.Count - 1
            sMsg = "What do you want for " &amp; oRS(i).Name
            oRS(i).Value = InputBox(sMsg, , oRS(i).DefaultValue)
        Next i
        oRS.Update
        cboDept = Null
        cboDept.Requery
        DoCmd.OpenTable "tblDepartments", acViewNormal, acReadOnly
        DoCmd.GoToRecord acDataTable, "tblDepartments", acLast
    End If
End Sub
```

L'exemple suivant montre comment ajouter un élément à une zone de liste modifiable liée.
 

 
 **Exemple de code fourni par :**[Référence du programmeur Microsoft Access 2010](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl)
 

 



```
Private Sub cboMainCategory_NotInList(NewData As String, Response As Integer)

    On Error GoTo Error_Handler
    Dim intAnswer As Integer
    intAnswer = MsgBox("""" &amp; NewData &amp; """ is not an approved category. " &amp; vbcrlf _
        &amp; "Do you want to add it now?" _ vbYesNo + vbQuestion, "Invalid Category")

    Select Case intAnswer
        Case vbYes
            DoCmd.SetWarnings False
            DoCmd.RunSQL "INSERT INTO tlkpCategoryNotInList (Category) "
                &amp; _ "Select """ &amp; NewData &amp; """;"
            DoCmd.SetWarnings True
            Response = acDataErrAdded
        Case vbNo
            MsgBox "Please select an item from the list.", _
                vbExclamation + vbOKOnly, "Invalid Entry"
            Response = acDataErrContinue

    End Select

    Exit_Procedure:
        DoCmd.SetWarnings True
        Exit Sub

    Error_Handler:
        MsgBox Err.Number &amp; ", " &amp; Error Description
        Resume Exit_Procedure
        Resume

End Sub
```


## À propos des collaborateurs
<a name="AboutContributors"> </a>

Holy Macro! Books publie des livres divertissants pour les personnes qui utilisent Microsoft Office. Vous pouvez consulter le catalogue complet sur MrExcel.com.
 

 
Wrox Press est guidé par la philosophie de programmeur à programmeur. Les livres Wrox sont écrits par des programmeurs pour des programmeurs et la marque Wrox est synonyme de solutions de référence aux problèmes de programmation concrets.
 

 

## Voir aussi
<a name="AboutContributors"> </a>


#### Concepts


 
[Objet ComboBox](1cf508d5-023e-eb38-3991-71e82b2a4e7e.md)
#### Autres ressources


 
[Membres de l'objet ComboBox](d0d83ca3-3698-295e-5335-7d0816557d6b.md)