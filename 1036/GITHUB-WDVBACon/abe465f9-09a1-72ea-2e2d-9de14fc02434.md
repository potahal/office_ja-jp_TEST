
# Affichage des boîtes de dialogue prédéfinies de Word

Cette rubrique contient les informations et exemples suivants :
 


- [Affichage d'une boîte de dialogue prédéfinie](#item1)
    
 
- [Renvoi et modification des paramètres des boîtes de dialogue](#item2)
    
 
- [Vérification de l'action effectuée pour fermer une boîte de dialogue](#item4)
    
 

## Affichage d'une boîte de dialogue prédéfinie

Vous pouvez utiliser Visual Basic pour Applications (VBA) pour afficher une boîte de dialogue prédéfinie afin de récupérer les saisies effectuées par l'utilisateur ou contrôler Word. La méthode  **[Show](6b236493-342d-934b-f360-00b7846789e8.md)** de l'objet **[Dialog](f90f6e6d-aaa0-c127-ab37-ca074144eff1.md)** affiche et exécute l'action effectuée dans une boîte de dialogue Word prédéfinie. Pour accéder à une boîte de dialogue Word prédéfinie donnée, affectez l'une des constantes **[WdWordDialog](af867214-8cd6-0477-6c88-2efe06589730.md)** à la propriété **[Dialogs](17acdfab-32d2-ddb8-04aa-692f9ffb20b8.md)**. L'instruction de macro ci-dessous, par exemple, permet d'afficher la boîte de dialogue **Ouvrir** ( **wdDialogFileOpen** ).
 

 

```
Sub ShowOpenDialog() 
 Dialogs(wdDialogFileOpen).Show 
End Sub
```

Si lorsqu'un fichier est sélectionné l'utilisateur clique sur  **OK**, le fichier est ouvert (l'action est exécutée). Cet exemple montre comment afficher la boîte de dialogue **Imprimer** ( **wdDialogFilePrint** ).
 

 



```
Sub ShowPrintDialog() 
 Dialogs(wdDialogFilePrint).Show 
End Sub
```

Définissez la propriété  **[DefaultTab](22de708e-fb23-b27a-00f0-dc43787c7eaf.md)** pour accéder à un onglet spécifique dans une boîte de dialogue Word. L'exemple suivant affiche l'onglet **Bordure de page** dans la boîte de dialogue **Bordure et trame**.
 

 



```
Sub ShowBorderDialog() 
 With Dialogs(wdDialogFormatBordersAndShading) 
 .DefaultTab = wdDialogFormatBordersAndShadingTabPageBorder 
 .Show 
 End With 
End Sub
```


 **Remarque**  Vous pouvez également utiliser les propriétés VBA de Word pour afficher les informations utilisateur sans afficher de boîte de dialogue. L'exemple ci-dessous montre comment utiliser la propriété  **[UserName](96f5ffb6-a20d-96f0-e3a4-0ad2dd47bf99.md)** de l'objet **[Application](d1cf6f8f-4e88-bf01-93b4-90a83f79cb44.md)** pour afficher le nom de l'utilisateur de l'application sans afficher la boîte de dialogue **Informations utilisateur**.
 




```
Sub DisplayUserInfo() 
 MsgBox Application.UserName 
End Sub
```

Si le nom de l'utilisateur est modifié dans l'exemple précédent, la modification n'est pas répercutée dans la boîte de dialogue. Pour mettre en vigueur les paramètres définis dans une boîte de dialogue sans afficher de boîte de dialogue, vous devez utiliser la méthode  **[Execute](7f7dce3a-40ef-988c-f5ea-06a25c0ccc4b.md)**. L'exemple suivant montre comment afficher la boîte de dialogue **Utilisateur**, et, si le nom n'est pas une chaîne vide, comment répercuter les paramètres dans la boîte de dialogue à l'aide de la méthode **Execute**.
 

 



```
Sub ShowAndSetUserInfoDialogBox() 
 With Dialogs(wdDialogToolsOptionsUserInfo) 
 .Display 
 If .Name <> "" Then .Execute 
 End With 
End Sub
```


 **Remarque**  Utilisez les propriétés et les méthodes VBA dans Word afin de définir les informations utilisateur sans afficher de boîte de dialogue. L'exemple de code suivant montre comment modifier le nom de l'utilisateur au moyen de la propriété  **UserName** de l'objet **Application**, puis comment afficher la boîte de dialogue **Utilisateur** afin de montrer que la modification a bien été apportée. Notez que l'affichage de la boîte de dialogue n'est pas nécessaire pour modifier une valeur dans une boîte de dialogue.
 




```
Sub SetUserName() 
 Application.UserName = "Jeff Smith" 
 Dialogs(wdDialogToolsOptionsUserInfo).Display 
End Sub
```


## Renvoi et modification des paramètres des boîtes de dialogue

L'utilisation de l'objet  **Dialog** pour modifier ou renvoyer la valeur d'une boîte de dialogue n'est pas très efficace lorsque vous pouvez effectuer ces opérations à l'aide d'une propriété ou d'une méthode . En outre, dans la très grande majorité des cas, il est plus simple et plus rapide d'utiliser du code VBA que d'avoir recours à l'objet **Dialog**. De ce fait, les exemples ci-dessous montrent également comment utiliser les propriétés VBA pour exécuter des tâches identiques.
 

 
Avant de renvoyer ou de modifier un paramètre de boîte de dialogue à l'aide de l'objet  **[Dialog](f90f6e6d-aaa0-c127-ab37-ca074144eff1.md)**, vous devez identifier la boîte de dialogue concernée. Pour ce faire, utilisez la propriété **[Dialogs](1c734758-d00a-31d7-d4f7-bf8c406ab6b5.md)** avec une constante **[WdWordDialog](af867214-8cd6-0477-6c88-2efe06589730.md)**. Après avoir instancié un objet **Dialog**, vous pouvez renvoyer ou définir des options dans la boîte de dialogue. L'exemple ci-dessous montre comment afficher le retrait droit défini dans la boîte de dialogue **Paragraphe**.
 

 



```
Sub ShowRightIndent() 
 Dim dlgParagraph As Dialog 
 Set dlgParagraph = Dialogs(wdDialogFormatParagraph) 
 MsgBox "Right indent = " &amp; dlgParagraph.RightIndent 
End Sub
```


 **Remarque**  Vous pouvez utiliser les méthodes et les propriétés VBA de Word pour afficher le paramètre relatif au retrait droit du paragraphe. L'exemple suivant montre comment utiliser la propriété  **[RightIndent](de69209e-d88d-d367-9d84-94faa07a30bd.md)** de l'objet **[ParagraphFormat](712d754a-dc92-f1a3-531d-dfae74a42c23.md)** afin d'afficher le retrait droit du paragraphe au niveau du point d'insertion.
 




```
Sub ShowRightIndexForSelectedParagraph() 
 MsgBox Selection.ParagraphFormat.RightIndent 
End Sub
```

De la même façon que vous pouvez renvoyer les paramètres d'une boîte de dialogue, vous pouvez également définir ces paramètres. L'exemple suivant montre comment activer la case à cocher  **Paragraphes solidaires** de la boîte de dialogue **Paragraphe**.
 

 



```
Sub SetKeepWithNext() 
 With Dialogs(wdDialogFormatParagraph) 
 .KeepWithNext = 1 
 .Execute 
 End With 
End Sub
```


 **Remarque**  Vous pouvez également utiliser les méthodes et les propriétés VBA de Word pour modifier le retrait droit du paragraphe. L'exemple ci-dessous montre comment utiliser la propriété  **[KeepWithNext](5fc8ad97-d839-7837-04c7-dac2efe1d1c2.md)** de l'objet **ParagraphFormat** afin que le paragraphe sélectionné demeure avec le paragraphe suivant.
 




```
Sub SetKeepWithNextForSelectedParagraph() 
 Selection.ParagraphFormat.KeepWithNext = True 
End Sub
```


 **Remarque**  Faites appel à la méthode  **[Update](7adf7403-77eb-85da-8a5a-092d1c8c548f.md)** pour vous assurer que les valeurs d'une boîte de dialogue correspondent à celles utilisées actuellement. La méthode **Update** peut vous être utile si vous définissez une variable de boîte de dialogue au début d'une macro et que vous voulez revenir en arrière ou modifier les paramètres en cours.
 


## Vérification de l'action effectuée pour fermer une boîte de dialogue

La valeur renvoyée par les méthodes  **Show** et **Display** indique sur quel bouton l'utilisateur a cliqué pour fermer la boîte de dialogue. Cet exemple montre comment afficher la boîte de dialogue **Saut**, et, si l'utilisateur clique sur **OK**, afficher un message dans la barre d'état.
 

 

```
Sub DialogBoxButtons() 
 If Dialogs(wdDialogInsertBreak).Show = -1 Then 
 StatusBar = "Break inserted" 
 End If 
End Sub
```

Le tableau ci-dessous répertorie les valeurs renvoyées qui sont associées à des boutons au sein des boîtes de dialogue.
 

 


|**Valeur renvoyée**|**Description**|
|:-----|:-----|
|-2|Bouton  **Fermer**.|
|-1|Bouton  **OK**.|
|0 (zéro)|Bouton  **Annuler**.|
|> 0 (zéro)|Bouton de commande, 1 correspondant au premier bouton, 2 au deuxième, etc.|
