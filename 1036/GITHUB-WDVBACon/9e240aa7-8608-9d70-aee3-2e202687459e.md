
# Utilisation des objets Range

Dans Visual Basic, une tâche courante consiste à spécifier une zone dans un document, puis à effectuer une opération sur cette zone, comme insérer du texte ou appliquer une mise en forme. Admettons, par exemple, que vous vouliez écrire une macro destinée à rechercher un mot ou une expression dans une partie d'un document. Cette partie du document peut être représentée par un objet  **[Range](15a7a1c4-5f3f-5b6e-60e9-29688de3f274.md)**. Une fois l'objet **Range** identifié, les méthodes et propriétés de l'objet **Range** peuvent être appliquées pour modifier le contenu de la plage.
 

Un objet  **Range** fait référence à une zone contiguë d'un document. Chaque objet **Range** est défini par la position de son caractère de début et celle de son caractère de fin. De la même manière que les signets sont utilisés dans un document, les objets **Range** sont utilisés dans les procédures Visual Basic pour identifier des parties données d'un document. Un objet **Range** peut être aussi petit que le point d'insertion ou aussi grand que le document tout entier. Toutefois, contrairement à un signet, un objet **Range** n'existe que pendant la durée de l'exécution de la procédure qui l'a défini.
 

Les propriétés  **[Start](aadedbb7-1ee2-9e5a-296d-0ebe25b6d8f4.md)**, **[End](fe90f321-c7b5-bea2-fa60-e6b750b33cf7.md)** et **[StoryType](bf11ba94-de45-ae76-09fa-9463cd2c4723.md)** servent à identifier un objet **Range** de manière exclusive. Les propriétés **Start** et **End** renvoient ou définissent les positions des caractères de début et de fin de l'objet **Range**. La position de caractère a la valeur zéro au début du document, un après le premier caractère, et ainsi de suite. La propriété **StoryType** contient les constantes **WdStoryType**, qui correspondent à 11 types d'articles différents.
 


 **Remarque**  Les objets  **Range** sont indépendants de la sélection. Ainsi, vous pouvez définir et modifier une plage sans modifier la sélection en cours. Vous pouvez aussi définir plusieurs plages dans un document, mais il ne peut y avoir qu'une sélection par volet de document.
 


## Utilisation de la méthode Range

Utilisez la méthode  **[Range](e28026bf-aa5e-8cf4-e765-7350afd57741.md)** de l'objet **[Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)** pour créer un objet **Range** situé dans l'article principal et comportant un point de départ et un point d'arrivée donnés. L'exemple ci-dessous montre comment créer un objet **Range** qui commence au début du premier caractère et s'étend jusqu'au dixième caractère.
 

 

```
Sub SetNewRange() 
 Dim rngDoc As Range 
 Set rngDoc = ActiveDocument.Range(Start:=0, End:=10) 
End Sub
```

Vous pouvez voir que l'objet  **Range** est créé lorsque vous appliquez une propriété ou une méthode à l'objet **Range**. L'exemple ci-dessous montre comment mettre en gras les 10 premiers caractères du document actif.
 

 



```
Sub SetBoldRange() 
 Dim rngDoc As Range 
 Set rngDoc = ActiveDocument.Range(Start:=0, End:=10) 
 rngDoc.Bold = True 
End Sub
```

Lorsque vous devez référencer un objet  **Range** plusieurs fois, vous pouvez utiliser l'instruction **Set** pour définir une variable qui est égale à l'objet **Range**. Cependant, si vous ne devez effectuer qu'une seule opération sur un objet **Range**, il n'est pas nécessaire de stocker ce dernier dans une variable. Vous pouvez obtenir le même résultat en utilisant une seule instruction qui identifie la plage et modifie la propriété **[Bold](04723b36-43bb-4721-90a5-33447a9b742e.md)**.
 

 



```
Sub BoldRange() 
 ActiveDocument.Range(Start:=0, End:=10).Bold = True 
End Sub
```

Tout comme un signet, une plage peut correspondre à un groupe de caractères ou juste marquer un emplacement dans un document. Dans l'exemple ci-dessous, l'objet  **Range** dispose des mêmes points de début et de fin. La plage ne comprend pas de texte. Cet exemple montre comment insérer du texte au début du document actif.
 

 



```
Sub InsertTextBeforeRange() 
 Dim rngDoc As Range 
 Set rngDoc = ActiveDocument.Range(Start:=0, End:=0) 
 rngDoc.InsertBefore "Hello " 
End Sub
```

Vous pouvez définir les points de début et de fin d'une plage en utilisant les nombres correspondant aux positions de caractères comme illustré ci-dessus, ou utiliser les propriétés  **Start** et **End** avec des objets tels que **[Selection](7b574a91-c33e-ecfd-6783-6b7528b2ed8f.md)**, **[Bookmark](be6b0c7b-60ca-97e7-ef19-6de335da3197.md)** ou **Range**. L'exemple qui suit montre comment créer un objet **Range** commençant au début du deuxième paragraphe et finissant après le troisième paragraphe.
 

 



```
Sub NewRange() 
 Dim doc As Document 
 Dim rngDoc As Range 
 
 Set doc = ActiveDocument 
 Set rngDoc = doc.Range(Start:=doc.Paragraphs(2).Range.Start, _ 
 End:=doc.Paragraphs(3).Range.End) 
End Sub
```

Pour plus d'exemples et d'informations, voir la méthode  **[Range](e28026bf-aa5e-8cf4-e765-7350afd57741.md)**.
 

 

## Utilisation de la propriété Range

La propriété  **Range** est disponible pour plusieurs objets, tels que **[Paragraph](0a704079-a082-4ab1-841b-fc0d49dd26d4.md)**, **[Bookmark](be6b0c7b-60ca-97e7-ef19-6de335da3197.md)** et **[Cell](cbe6ae71-b2da-63a9-1446-0a2f81ab8b14.md)**. Elle est utilisée pour renvoyer un objet **Range**. L'exemple ci-dessous montre comment renvoyer un objet **Range** faisant référence au premier paragraphe du document actif.
 

 

```
Sub SetParagraphRange() 
 Dim rngParagraph As Range 
 Set rngParagraph = ActiveDocument.Paragraphs(1).Range 
End Sub
```

Une fois que vous disposez d'un objet  **Range**, vous pouvez utiliser ses propriétés et ses méthodes pour le modifier. L'exemple suivant montre comment sélectionner le deuxième paragraphe du document actif pour, ensuite, centrer la sélection.
 

 



```
Sub FormatRange() 
 ActiveDocument.Paragraphs(2).Range.Select 
 Selection.ParagraphFormat.Alignment = wdAlignParagraphCenter 
End Sub
```

Si vous devez appliquer de nombreuses propriétés ou méthodes au même objet  **Range**, vous pouvez utiliser la structure **With…End With**. L'exemple ci-dessous montre comment mettre en forme le texte du premier paragraphe du document actif.
 

 



```
Sub FormatFirstParagraph() 
 Dim rngParagraph As Range 
 Set rngParagraph = ActiveDocument.Paragraphs(1).Range 
 With rngParagraph 
 .Bold = True 
 .ParagraphFormat.Alignment = wdAlignParagraphCenter 
 With .Font 
 .Name = "Stencil" 
 .Size = 15 
 End With 
 End With 
End Sub
```

Pour plus d'exemples et d'informations, voir la propriété  **[Range](983f7bd3-10b4-882f-5b4d-01e44127676f.md)**.
 

 

## Redéfinition d'un objet Range

Utilisez la méthode  **[SetRange](91097079-406c-98f4-d37c-cca8dab7aef0.md)** pour redéfinir un objet **Range** existant. L'exemple suivant montre comment définir en tant que plage la sélection en cours. La méthode **SetRange** permet ensuite de redéfinir la plage de telle sorte qu'elle désigne la sélection en cours augmentée des dix caractères qui la suivent.
 

 

```
Sub ExpandRange() 
 Dim rngParagraph As Range 
 Set rngParagraph = Selection.Range 
 rngParagraph.SetRange Start:=rngParagraph.Start, _ 
 End:=rngParagraph.End + 10 
End Sub
```

Pour plus d'exemples et d'informations, voir la méthode  **[Range](e28026bf-aa5e-8cf4-e765-7350afd57741.md)** pour **[Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)**.
 

 

 **Remarque**  Lorsque vous déboguez vos macros, vous pouvez utiliser la méthode  **Select** pour garantir qu'un objet **Range** se rapporte à la plage de texte voulue. Par exemple, les instructions ci-dessous sélectionnent un objet **Range** qui fait référence aux deuxième et troisième paragraphes du document actif, puis mettent en forme la police de la sélection.
 




```
Sub SelectRange() 
 Dim rngParagraph As Range 
 
 Set rngParagraph = ActiveDocument.Paragraphs(2).Range 
 
 rngParagraph.SetRange Start:=rngParagraph.Start, _ 
 End:=ActiveDocument.Paragraphs(3).Range.End 
 rngParagraph.Select 
 
 Selection.Font.Italic = True 
End Sub
```

