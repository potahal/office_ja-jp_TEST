
# Événement Form.BeforeUpdate (Access)

L'événement  **BeforeUpdate** se produit avant que les données modifiées d'un contrôle ou d'un enregistrement soient mises à jour.
 


## Syntaxe

*expression* . **BeforeUpdate**( ***Cancel*** )
 

 
*expression* Variable qui représente un objet **Form**.
 

 

### Paramètres



|**Nom**|**Obligatoire/Facultatif**|**Type de données**|**Description**|
|:-----|:-----|:-----|:-----|
| _Cancel_|Obligatoire|**Entier**|Le paramètre détermine si l'événement  **BeforeUpdate** se produit. Si vous définissez l'argument _Cancel_ sur **True** (?1), l'événement **BeforeUpdate** est annulé.|

## Remarques

La modification des données d'un contrôle à l'aide de Visual Basic ou d'une macro contenant l'action  **SetValue** ne déclenche pas ces événements pour le contrôle. Toutefois, si vous passez ensuite à un autre enregistrement ou que vous sauvegardez l'enregistrement, l'événement **BeforeUpdate** du formulaire survient.
 

 
Pour exécuter une macro ou une procédure d'événement lorsque cet événement survient, définissez la propriété  **BeforeUpdate** sur le nom de la macro ou sur [Procédure d'événement].
 

 
Cet événement ne s'applique pas aux boutons d'options, cases à cocher, boutons bascule d'un groupe d'options. Il s'applique uniquement au groupe d'options lui-même.
 

 
L'événement  **BeforeUpdate** est déclenché lors de la mise à jour d'un contrôle ou d'un enregistrement. Au sein d'un enregistrement, les données modifiées de chaque contrôle sont mises à jour lorsque ce dernier perd le focus ou lorsque l'utilisateur appuie sur la touche **Entrée** ou **Tabulation**. Lorsque l'enregistrement n'a plus le focus ou si l'utilisateur clique sur  **Sauvegarder un enregistrement** dans le menu **Enregistrements**, l'enregistrement est mis à jour dans son intégralité et les données sont enregistrées dans la base de données.
 

 
Lorsque vous entrez des données inédites ou modifiées dans le contrôle d'un formulaire et que vous passez à un autre enregistrement ou que vous sauvegardez l'enregistrement en cliquant sur  **Sauvegarder un enregistrement** dans le menu **Enregistrements**, l'événement  **AfterUpdate** du formulaire survient immédiatement après l'événement **AfterUpdate** du contrôle. Lorsque vous passez à un autre enregistrement, les événements **Exit** et **LostFocus** du contrôle surviennent, suivis de l'événement **Current** de l'enregistrement auquel vous êtes passé et des événements **Enter** et **GotFocus** du premier contrôle de cet enregistrement. Pour exécuter la macro ou procédure d'événement **AfterUpdate** sans exécuter les macros ou procédures d'événement **Exit** et **LostFocus**, sauvegardez l'enregistrement à l'aide de la commande **Sauvegarder un enregistrement** du menu **Enregistrements**.
 

 
Les macros et procédures d'événement  **BeforeUpdate** ne sont exécutées que si vous modifiez les données d'un contrôle. Cet événement ne survient pas lorsqu'une valeur change dans le contrôle calculé. Les macros et procédures d'événement **BeforeUpdate** d'un formulaire sont exécutées uniquement si vous modifiez les données d'un ou plusieurs contrôles de l'enregistrement.
 

 
Pour les formulaires, vous pouvez utiliser l'événement  **BeforeUpdate** pour annuler la mise à jour d'un enregistrement avant de passer à un autre enregistrement.
 

 
Si l'utilisateur entre une nouvelle valeur dans le contrôle, le paramètre de propriété  **OldValue** n'est pas modifié tant que les données n'ont pas été enregistrées (et que l'enregistrement n'a pas été mis à jour). Si vous annulez une mise à jour, la valeur de la propriété **OldValue** remplace la valeur existante du contrôle.
 

 
Vous utilisez souvent l'événement  **BeforeUpdate** pour valider les données, notamment lorsque vous effectuez des validations complexes, par exemple :
 

 

- Celles qui sont liées à des conditions qui dépendent de plusieurs valeurs dans le formulaire.
    
 
- Celles qui affichent des messages d'erreur différents en fonction des données entrées.
    
 
- Celles que l'utilisateur peut supplanter.
    
 
- Celles qui contiennent des références à des contrôles d'autres formulaires ou des fonctions définies par l'utilisateur.
    
 

 **Remarque**  Pour effectuer des validations simples ou des validations plus complexes telles que demander une valeur dans un champ ou valider plusieurs contrôles d'un formulaire, vous pouvez utiliser la propriété  **ValidationRule** pour les contrôles et les propriétés **ValidationRule** et **Required** pour les champs et les enregistrements des tables.
 

Une erreur d'exécution se produit si vous tentez de modifier des données contenues dans le contrôle qui a déclenché l'événement  **BeforeUpdate** dans la procédure de l'événement.
 

 

## Exemple

L'exemple suivant montre comment utiliser une procédure d'événement  **BeforeUpdate** pour vérifier si un nom de produit a déjà été entré dans la base de données. Une fois que l'utilisateur a tapé un nom de produit dans la zone **ProductName**, la valeur est comparée au champ  **ProductName** de la table **Products**. Si une valeur correspondante existe dans la table  **Products**, un message informe l'utilisateur que le produit a déjà été entré.
 

 
Pour tester l'exemple, ajoutez la procédure d'événement suivante à un formulaire nommé  **Products** contenant une zone de texte appelée **ProductName**.
 

 



```
Private Sub ProductName_BeforeUpdate(Cancel As Integer) 
    If(Not IsNull(DLookup("[ProductName]", _ 
        "Products", "[ProductName] ='" _ 
        &amp; Me!ProductName &amp; "'"))) Then 
        MsgBox "Product has already been entered in the database." 
        Cancel = True 
        Me!ProductName.Undo 
    End If 
End Sub
```


 

 
L'exemple suivant permet d'afficher un message quand un contrôle est vide dans un formulaire continu lorsqu'un utilisateur passe à un autre enregistrement.
 

 
 **Exemple de code fourni par :** Bill Jelen,[MrExcel.com](http://www.mrexcel.com/)
 

 



```
Private Sub Form_BeforeUpdate(Cancel As Integer)
    Dim oContr As Control
    For Each oContr In Me.Detail.Controls
        If IsNull(oContr) = True Then
            If MsgBox(oContr.Name &amp; " is empty", vbOKCancel) = vbCancel Then
                Cancel = True: oContr.SetFocus: Exit Sub
            End If
        End If
    Next oContr
End Sub
```

L'exemple suivant montre comment utiliser l'événement  **BeforeUpdate** d'un formulaire pour demander à ce qu'une valeur soit entrée dans un des contrôles lorsqu'un autre contrôle inclut également des données.
 

 
 **Exemple de code fourni par :**[Référence du programmeur Microsoft Access 2010](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl)
 

 



```
Private Sub Form_BeforeUpdate(Cancel As Integer)
If (IsNull(Me.FieldOne)) Or (Me.FieldOne.Value =  "") Then
    ' No action required
Else
    If (IsNull(Me.FieldTwo)) or (Me.FieldTwo.Value = "") Then
        MsgBox "You must provide data for field 'FieldTwo', " &amp; _
            "if a value is entered in FieldOne", _
            vbOKOnly, "Required Field"
        Me.FieldTwo.SetFocus
        Cancel = True
        Exit Sub
    End If
End If

End Sub

```


## À propos des collaborateurs
<a name="AboutContributors"> </a>

Holy Macro! Books publie des livres divertissants pour les personnes qui utilisent Microsoft Office. Vous pouvez consulter le catalogue complet sur MrExcel.com.
 

 
Wrox Press est guidé par la philosophie de programmeur à programmeur. Les livres Wrox sont écrits par des programmeurs pour des programmeurs et la marque Wrox est synonyme de solutions de référence aux problèmes de programmation concrets.
 

 

## Voir aussi
<a name="AboutContributors"> </a>


#### Concepts


 
[Objet Form](72ef9219-142b-b690-b696-3eba9a5d4522.md)
#### Autres ressources


 
[Membres de l'objet Form](e1976b58-28ca-8f76-cdf3-6732cb06ce6c.md)