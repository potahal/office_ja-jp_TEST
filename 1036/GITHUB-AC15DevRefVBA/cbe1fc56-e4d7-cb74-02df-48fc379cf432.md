
# Application.DLookup, méthode (Access)

La fonction  **DLookup** recherche la valeur d'un champ particulier qui fait partie d'un jeu d'enregistrements défini (un domaine).
 


## Syntaxe

*expression* . **DLookup**( ***Expr***, ***Domain***, ***Criteria*** )
 

 
*expression* Variable qui représente un objet **Application**.
 

 

### Paramètres



|**Nom**|**Requis/Facultatif**|**Type de données**|**Description**|
|:-----|:-----|:-----|:-----|
| _Expr_|Obligatoire|**Chaîne**|Expression qui identifie le champ dont vous souhaitez renvoyer la valeur. Il peut s'agir d'une expression chaîne identifiant un champ dans une table ou une requête, ou d'une expression qui effectue un [calcul sur les données de ce champ](http://msdn.microsoft.com/library/73c27d1c-0a3c-03e4-c17c-337133d7b316%28Office.15%29.aspx). Dans  _expr_, vous pouvez inclure le nom d'un champ de table, un contrôle de formulaire, une constante ou une fonction. Si  _expr_ inclut une fonction, elle peut être intrinsèque ou définie par l'utilisateur, mais ne peut pas être une autre fonction de regroupement Domaine ou SQL.|
| _Domain_|Obligatoire|**Chaîne**|Expression chaîne identifiant les enregistrements qui constituent le domaine. Il peut s'agir d'un nom de table ou d'un nom de requête qui ne requiert aucun paramètre.|
| _Criteria_|Facultatif|**Variante**|Expression chaîne facultative servant à limiter la plage des données sur laquelle porte la fonction  **DLookup**. Par exemple, _criteria_ est souvent équivalent à la clause WHERE d'une expression SQL sans le mot WHERE. En cas d'omission de _criteria_, la fonction  **DLookup** évalue _expr_ par rapport à l'intégralité du domaine. Chaque champ compris dans _criteria_ doit aussi l'être dans _domain_, sans quoi la fonction  **DLookup** renvoie la valeur **Null**.|

### Valeur renvoyée

Variante
 

 

## Remarques

La fonction  **DLookup** vous permet d'afficher la valeur d'un champ qui n'appartient pas à la source d'enregistrements sur laquelle le formulaire ou l'état est fondé. Par exemple, supposez que vous disposez d'un formulaire fondé sur une table Détails Commandes. Il affiche les champs OrderID, ProductID, UnitPrice, Quantity et Discount. Cependant, le champ ProductName fait partie d'une autre table, la table Produits. Utilisez la fonction **DLookup** dans un contrôle calculé pour afficher le nom du produit (ProductName) dans le même formulaire.
 

 
La fonction  **DLookup** renvoie une seule valeur de champ à partir des informations spécifiées dans _criteria_. L'argument  _criteria_ est facultatif, mais si vous n'associez pas de valeur à _criteria_, la fonction  **DLookup** renvoie simplement une valeur aléatoire dans le domaine.
 

 
Si aucun enregistrement ne satisfait à  _criteria_ ou si _domain_ ne contient pas d'enregistrement, la fonction **DLookup** renvoie la valeur **Null**.
 

 
Si plusieurs enregistrements correspondent au  _criteria_, la fonction  **DLookup** renvoie la valeur du champ de la première occurrence. Il est recommandé de choisir un critère qui garantira l'unicité de la valeur de champ renvoyée par la fonction **DLookup**. Vous pouvez utiliser une valeur de clé primaire pour ce critère, comme `[EmployeeID]` dans l'exemple suivant pour garantir que la fonction **DLookup** renvoie une valeur unique :
 

 



```
Dim varX As Variant 
varX = DLookup("[LastName]", "Employees", "[EmployeeID] = 1")
```

Que vous utilisiez la fonction  **DLookup** dans une macro, un module, une expression de requête ou un contrôle calculé, construisez l'argument _criteria_ avec soin pour en garantir l'évaluation correcte.
 

 
Vous pouvez utiliser la fonction  **DLookup** pour spécifier des critères dans la ligne Critères d'une requête, au sein d'une expression de champ calculé dans une requête, ou dans la ligne Mise à jour d'une requête de mise à jour.
 

 
Vous pouvez aussi utiliser la fonction  **DLookup** dans une expression de contrôle calculé d'un formulaire ou d'un état, si le champ à afficher ne fait pas partie de la source d'enregistrements sur laquelle le formulaire ou l'état est fondé. Par exemple, imaginons que vous disposez d'un formulaire Order Details (Détails commandes) fondé sur une table Order Details et dont la zone de texte ProductID affiche le champ ProductID. Pour rechercher ProductName dans une table Products à partir de la valeur de la zone de texte, vous pouvez créer une autre zone de texte et affecter l'expression suivante à sa propriété **ControlSource**:
 

 



```
=DLookup("[ProductName]", "Products", "[ProductID] =" _ 
     &amp; Forms![Order Details]!ProductID)
```

 **Conseils**
 

 

- Bien que la fonction  **DLookup** vous permette d'afficher la valeur d'un champ dans une table externe, il peut s'avérer plus efficace de créer une requête contenant les champs nécessaires et de baser votre formulaire ou votre état sur cette requête.
    
 
- Vous pouvez aussi utiliser l'Assistant Recherche pour trouver des valeurs dans une table externe.
    
 
 **Liens fournis par :**
 
![Icône de membre de la communauté](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) Communauté[UtterAccess](http://www.utteraccess.com)
 

 

- [Formulaires : Remplir les contrôles/zones des texte en fonction de la sélection de zones de liste modifiable](http://www.utteraccess.com/wiki/index.php/Forms:_Populate_Controls/Text_Boxes_Based_on_Combobox_Selection)
    
 
- [Rechercher tous les tableaux/champs dans une base de données pour une valeur](http://www.utteraccess.com/wiki/index.php/Search:_All_Tables_All_Fields)
    
 

## Exemple

L'exemple suivant retourne le nom contenu dans le champ CompanyName (Nom de société) de l'enregistrement qui satisfait à  _critère_. Le domaine consiste en la table Shippers (Messagers). L'argument  _critère_ limite le jeu d'enregistrements à ceux pour lesquels N° messager est égal à 1.
 

 

```
Dim varX As Variant 
varX = DLookup("[CompanyName]", "Shippers", "[ShipperID] = 1")
```

Dans l'exemple qui suit, extrait de la table Shippers, le contrôle ShipperID du formulaire fournit l'argument critère pour la fonction  **DLookup**. Remarquez que la référence au contrôle n'est pas entourée des guillemets caractérisant les chaînes. Ceci garantit qu'à chaque appel de la fonction **DLookup**, Microsoft Access extraira la valeur en cours de ce contrôle.
 

 



```
Dim varX As Variant 
varX = DLookup("[CompanyName]", "Shippers", "[ShipperID] = " _ 
    &amp; Forms!Shippers!ShipperID)
```

L'exemple qui suit utilise la variable  `intSearch` pour obtenir la valeur.
 

 



```
Dim intSearch As Integer 
Dim varX As Variant 
 
intSearch = 1 
varX = DLookup("[CompanyName]", "Shippers", _ 
    "[ShipperID] = " &amp; intSearch)
```


 

 
Les exemples suivants montrent comment utiliser différents types de critères avec la fonction  **DLookup**.
 

 
 **Exemple de code fourni par :**
 
![Icône de membre de la communauté](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) Communauté[UtterAccess](http://www.utteraccess.com)
 

 



```
    ' ***************************
    ' Typical Use
    ' Numerical values. Replace "number" with the number to use.
    variable = DLookup("[FieldName]", "TableName", "[Criteria] = number")

    ' Strings.
    ' Numerical values. Replace "string" with the string to use.
    variable = DLookup("[FieldName]", "TableName", "[Criteria]= 'string'")

    ' Dates. Replace "date" with the string to use.
    variable = DLookup("[FieldName]", "TableName", "[Criteria]= #date#")
    ' ***************************

    ' ***************************
    ' Referring to a control on a form
    ' Numerical values
    variable = DLookup("[FieldName]", "TableName", "[Criteria] = " &amp; Forms!FormName!ControlName)

    ' Strings
    variable = DLookup("[FieldName]", "TableName", "[Criteria] = '" &amp; Forms!FormName!ControlName &amp; "'")

    ' Dates
    variable = DLookup("[FieldName]", "TableName", "[Criteria] = #" &amp; Forms!FormName!ControlName &amp; "#")
    ' ***************************

    ' ***************************
    ' Combinations
    ' Multiple types of criteria
    variable = DLookup("[FieldName]", "TableName", "[Criteria1] = " &amp; Forms![FormName]![Control1] _
             &amp; " AND [Criteria2] = '" &amp; Forms![FormName]![Control2] &amp; "'" _
            &amp; " AND [Criteria3] =#" &amp; Forms![FormName]![Control3] &amp; "#")
    
    ' Use two fields from a single record.
    variable = DLookup("[LastName] &amp; ', ' &amp; [FirstName]", "tblPeople", "[PrimaryKey] = 7")
            
    ' Expressions
    variable = DLookup("[Field1] + [Field2]", "tableName", "[PrimaryKey] = 7")
    
    ' Control Structures
    variable = DLookup("IIf([LastName] Like 'Smith', 'True', 'False')", "tableName", "[PrimaryKey] = 7")
    ' ***************************
```


## À propos des collaborateurs
<a name="AboutContributors"> </a>

UtterAccess est le premier forum d'aide et wiki de Microsoft Access. Cliquez ici pour le rejoindre.
 

 

## Voir aussi
<a name="AboutContributors"> </a>


#### Concepts


 
[Objet Application](aefb0713-97e6-e2c7-e530-8fd2e1316a55.md)
#### Autres ressources


 
[Membres de l'objet Application](3ab5276c-d52a-72a9-244c-ec92ead48811.md)