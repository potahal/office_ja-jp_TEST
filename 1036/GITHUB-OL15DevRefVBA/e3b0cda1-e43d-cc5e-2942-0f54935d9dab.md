
# Restrict, méthode

Applique un filtre à la collection  **[Items](3a99730b-e62a-5ca6-f6ec-911c95173242.md)** et renvoie une nouvelle collection contenant l'ensemble des éléments de la collection d'origine qui correspondent aux critères du filtre.
 


## Syntaxe

*expression* . **Restrict**( ***Filter*** )
 

 
*expression* Expression qui renvoie un objet **Items**.
 

 

### Paramètres


|
|

### Valeur renvoyée

Une collection  **Items** qui représente les éléments de la collection **Items** d'origine qui correspond au filtre.
 

 

## Remarques

Cette méthode peut être utilisée à la place des méthodes  **[Find](e7a791d8-b80b-df07-84a3-a85acabfcf80.md)** et **[FindNext](2530f640-e024-3567-f539-6bdbf645401d.md)** pour itérer sur des éléments spécifiques au sein d'une collection. Les méthodes **Find** et **FindNext** sont plus rapides que le filtrage si le nombre d'élément est peu élevé. La méthode **Restrict** est beaucoup plus rapide si la collection comporte de nombreux éléments et particulièrement, si seuls quelques éléments doivent être renvoyés.
 

 

 **Remarque**  Si vous utilisez des champs définis par l'utilisateur à l'intérieur d'une clause  **Find** ou **Restrict**, ces champs doivent exister dans le dossier. Si ce n'est pas le cas, le code générera un message d'erreur indiquant que ces champs sont inconnus. Pour ajouter un champ à un dossier, il suffit d'afficher le  **sélecteur de champs** et de cliquer sur **Nouveau**.
 

Cette méthode ne peut pas être utilisée et génère une erreur avec les propriétés suivantes :
 

 

|
|

### Création de filtres pour les méthodes Find et Restrict

La syntaxe du filtre varie selon le type de champ que vous filtrez.
 

 

### String (pour les champs de texte)

Lors du filtrage de champs de type Texte, vous pouvez utiliser une paire de guillemets simples (') ou de guillemets double ("") pour délimiter les valeurs faisant partie du filtre. Par exemple, l'ensemble des lignes suivantes fonctionne correctement lorsque le champ est de type  **String**:
 

 
 `sFilter = "[NomEntreprise] = 'Microsoft'"`
 

 
 `sFilter = "[NomEntreprise] = ""Microsoft"""`
 

 
 `sFilter = "[NomEntreprise] = " &amp; Chr(34) &amp; "Microsoft" &amp; Chr(34)`
 

 
Lorsqu'il s'agit de spécifier un filtre dans une requête Jet ou DASL, si vous utilisez une paire de guillemets simples pour délimiter une chaîne qui fait partie de ce filtre et que le chaîne contient un autre guillemet simple ou une apostrophe, ajoutez un guillemet simple en guise de caractère d'échappement avant le guillemet simple ou l'apostrophe. Adoptez la même approche si vous utilisez une paire de guillemets doubles pour délimier une chaîne : si la chaîne contient un guillemet double, ajoutez un guillemet double en guise de caractère d'échappement avant le guillemet double.
 

 
Par exemple, dans la chaîne de filtre DASL appliquée à la propriété  **Subject** et qui porte sur les mots `can't`, toute la chaîne de filtre est délimitée par une paire de guillemets doubles, et la chaîne imbriquée  `can't` est délimitée par une paire de guillemets simples. Dans cette chaîne de filtre, trois caractères doivent être associés à des caractères d'échappement : le guillemet double initial et le guillemet double final pour la référence de propriété de `http://schemas.microsoft.com/mapi/proptag/0x0037001f` et l'apostrophe située dans la condition de valeur du mot `can't`. En appliquant les caractères d'échappement appropriés, vous pouvez exprimer la chaîne de filtre comme suit :
 

 
 `filter = "@SQL=""http://schemas.microsoft.com/mapi/proptag/0x0037001f"" = 'can''t'"`
 

 
Vous pouvez également utiliser la fonction  `chr(34)` pour représenter le guillemet double (dont la valeur de caractère ASCII est 34) utilisé comme caractère d'échappement. En utilisant `chr(34)` à la place d'un caractère d'échappement de type guillemet double, vous pouvez exprimer le dernier exemple comme suit :
 

 
 `filter = "@SQL= " &amp; Chr(34) &amp; "http://schemas.microsoft.com/mapi/proptag/0x0037001f" _`
 

 
 `&amp; Chr(34) &amp; " = " &amp; "'can''t'"`
 

 
Il est également nécessaire d'utiliser des caractères d'échappement pour les guillemets simples et doubles figurant dans les requêtes DASL à l'aide des opérateurs  **ci_startswith** ou **ci_phrasematch**. Par exemple, la requête suivante exécute une requête de correspondance d'expression pour `can't` dans l'objet du message :
 

 
 `filter = "@SQL=" &amp; Chr(34) &amp; "http://schemas.microsoft.com/mapi/proptag/0x0037001E" _`
 

 
 `&amp; Chr(34) &amp; " ci_phrasematch " &amp; "'can''t'"`
 

 
Autre exemple : celui d'une chaîne de filtre DASL appliquée à la propriété  **Subject** et qui porte sur les mots `the right stuff`, où le mot  `stuff` est entre guillemets. Dans ce cas, vous devez ajouter des caractères d'échappement pour les guillemets doubles comme suit :
 

 
 `filter = "@SQL=""http://schemas.microsoft.com/mapi/proptag/0x0037001f"" = 'the right ""stuff""'"`
 

 
Un ensemble différent de règles d'échappement s'appliquent aux références des propriétés nommées qui contiennent des espaces, des guillemets simples ou des guillemets doubles. Si une référence de propriété contient un espace, un guillemet simple ou un guillemets double, vous devez utiliser l'échappement d'URL (Universal Resource Locator) dans la référence de propriété comme suit :
 

 

|
|
Par exemple, pour rechercher une propriété personnalisée appelée  **Mom's "Gift"** contenant le mot `pearls`, voici le fitre que vous devriez utiliser :
 

 
 `filter = "@SQL=" &amp; Chr(34) &amp; _`
 

 
 `"http://schemas.microsoft.com/mapi/string/{00020329-0000-0000-C000-000000000046}/" _`
 

 
 `&amp; "Mom%27s%20%22Gift%22" &amp; Chr(34) &amp; " like '%pearls%'"`
 

 

### Date

Bien que les dates et les heures soient généralement stockées avec un format Date, les méthodes  **Find** et **Restrict** nécessitent que la date et l'heure soient converties en une représentation de chaîne. Pour vérifier que la date est mise en forme comme prévu dans Microsoft Outlook, utilisez la fonction **Format**. L'exemple de code suivant montre comment créer un filtre pour rechercher tous les contacts modifiés après le 15 janvier 1999 à 15:30.
 

 
 `sFilter = "[HeureDernièreModification] > '" &amp; Format("1/15/99 3:30pm", "ddddd h:nn AMPM") &amp; "'"`
 

 

### Opérateurs booléens

Les opérateurs Boolean, TRUE/FALSE, YES/NO, ON/OFF, etc, ne doivent pas être convertis en chaîne. Par exemple, pour déterminer si la journalisation est activée pour les contacts, utilisez ce filtre :
 

 
 `sFilter = "[Journal] = True"`
 

 

 **Remarque**  Si vous utilisez des guillemets avec des champs de type  **Boolean**, une chaîne vide trouvera les éléments dont les champs comportent la valeur  **False**, les chaînes non vides trouveront, quant à elle, les éléments dont les champs comportent la valeur  **True**.
 


### Mots clés (ou catégories)

Le champ  **Catégories** est de type mots clés et a été conçu pour contenir plusieurs valeurs. Lorsque vous accédez au champ **Catégories** via un programme, celui-ci se comporte comme un champ de texte, et la chaîne doit correspondre exactement à la valeur recherchée. Les valeurs dans la chaîne de texte sont séparées par une virgule et un espace. Cela signifie généralement que vous ne pouvez pas utiliser les méthodes **Find** et **Restrict** sur un champ de mots clés s'il contient plusieurs valeurs. Par exemple, si un contact appartient à la catégorie Business et si un contact appartient aux catégories Business et Social, il n'est pas facile d'utiliser les méthodes **Find** et **Restrict** pour extraire tous les éléments qui se trouvent dans la catégorie Business. Il est préférable, dans ce cas, d'effectuer une recherche dans les contacts du dossier et d'utiliser la fonction **Instr** pour vérifier si la chaîne « Business » est contenue dans le champ des mots clés.
 

 

 **Remarque**  Il existe une exception à cette règle, si vous limitez le champ Catégories à deux valeurs au maximum, vous pouvez alors utiliser les méthodes  **Find** et **Restrict** avec l'opérateur logique OR pour extraire tous les contacts Business. Par exemple (en pseudo code) : "Business" OR "Business, Personal" OR "Personal, Business." Les chaînes Category ne respectent pas la casse.
 


### Entier

Vous pouvez rechercher les champs de type  **Integer** avec ou sans guillemets comme délimiteurs. Les filtres suivants retrouveront les contacts qui ont été créés avec Outlook 2000 :
 

 
 `sFilter = "[VersionInterneOutlook] = 92711"`
 

 
 `sFilter = "[VersionInterneOutlook] = '92711'"`
 

 

### Utilisation de variables dans le filtre

Comme l'illustre l'exemple de la méthode  **Restrict**, vous pouvez utiliser des valeurs issues de variables dans le filtre. L'exemple de code Microsoft Visual Basic Scripting Edition (VBScript) suivant illustre la syntaxe qui utilise des variables dans le filtre.
 

 
 `sFullName = "Dan Wilson"`
 

 
 `' Cette approche utilise Chr(34) pour délimiter la valeur.`
 

 
 `sFilter = "[NomComplet] = " &amp; Chr(34) &amp; "sNomComplet" &amp; Chr(34)`
 

 
 `' Cette approche utilise des guillemets doubles pour délimiter la valeur.`
 

 
 `sFilter = "[NomComplet] = """ &amp; sNomComplet &amp; """"`
 

 

### Utilisation d'opérateurs logiques dans le filtre

Les opérateurs logiques autorisés sont AND, OR et NOT. Ci-dessous figurent des variantes de la clause pour la méthode  **Restrict**, afin que vous puissiez définir plusieurs critères.
 

 
OR : le code suivant renvoie tous les contacts dont la catégorie est Business ou Personal.
 

 
 `sFilter = "[Catégories] = 'Personal' Or [Catégories] = 'Business'"`
 

 
AND : Le code suivant extrait tous les contacts personnels travaillant à Microsoft.
 

 
 `sFilter = "[Catégories] = 'Personal' And [NomEntreprise] = 'Microsoft'"`
 

 
NOT : Le code suivant extrait tous les contacts personnels qui ne travaillent pas à Microsoft.
 

 
 `sFilter = "[Catégories] = 'Personal' And Not([NomEntreprise] = 'Microsoft')"`
 

 

### Remarques supplémentaires

Si vous essayez d'utiliser les méthodes  **Find** ou **Restrict** avec des champs définis par l'utilisateur, ces champs doivent être définis dans le dossier, sinon, une erreur se produit. Il n'existe aucun moyen d'effectuer une opération « contains » (contient). Par exemple, vous ne pouvez pas utiliser **Find** ou **Restrict** pour rechercher des éléments dont le champ **Objet** contient un mot particulier. Il est préférable, dans ce cas, d'utiliser la méthode **[AdvancedSearch](7b433d8b-08b9-dff1-b854-287d76b47a90.md)** ou de parcourir l'ensemble des éléments dans le dossier et d'utiliser la fonction **InStr** pour effectuer une recherche dans un champ. Vous pouvez utiliser la méthode **Restrict** pour rechercher des éléments commençant par un caractère particulier. Par exemple, pour rechercher tous les contacts dont le nom de famille commence par la lettre M, utilisez le filtre suivant :
 

 
 `sFilter = "[Nom] > 'LZZZ' And [Nom] < 'N'"`
 

 

## Exemple

Cet exemple de code Visual Basic pour Applications (VBA) montre comment utiliser la méthode  **Restrict** pour obtenir tous les éléments de la boîte de réception de la catégorie **Business** et les déplacer dans le dossier **Business**. Pour exécuter ce code, créez un sous-dossier Business ou assurez-vous qu'un tel dossier existe sous Boîte de réception
 

 

```
Sub MoveItems()
    Dim myNamespace As Outlook.NameSpace
    Dim myFolder As Outlook.Folder
    Dim myItems As Outlook.Items
    Dim myRestrictItems As Outlook.Items
    Dim myItem As Outlook.MailItem

    Set myNamespace = Application.GetNamespace("MAPI")
    Set myFolder = _
        myNamespace.GetDefaultFolder(olFolderInbox)
    Set myItems = myFolder.Items
    Set myRestrictItems = myItems.Restrict("[Categories] = 'Business'")
    For i =  myRestrictItems.Count To 1 Step -1
        myRestrictItems(i).Move myFolder.Folders("Business")
    Next
End Sub
```


## Exemple

Cet exemple de code, en Visual Basic pour Applications (VBA), montre comment utiliser la méthode  **Restrict** pour appliquer un filtre aux contacts en fonction de la propriété **[LastModificationTime](14962429-bbf6-a814-647a-70be1dad339d.md)**.
 

 

```
Public Sub ContactDateCheck()
    Dim myNamespace As Outlook.NameSpace
    Dim myContacts As Outlook.Items
    Dim myItems As Outlook.Items
    Dim myItem As Object
    
    Set myNamespace = Application.GetNamespace("MAPI")
    Set myContacts = myNamespace.GetDefaultFolder(olFolderContacts).Items
    Set myItems = myContacts.Restrict("[LastModificationTime] > '01/1/2003'")
    For Each myItem In myItems
        If (myItem.Class = olContact) Then
            MsgBox myItem.FullName &amp; ": " &amp; myItem.LastModificationTime
        End If
    Next
End Sub
```


## Exemple

L'exemple de code Visual Basic pour Applications suivant est identique au précédent, à la seule différence qu'il montre comment utiliser une variable dans le filtre.
 

 

```
Public Sub ContactDateCheck2()
    Dim myNamespace As Outlook.NameSpace
    Dim myContacts As Outlook.Items
    Dim myItem As Object
    Dim DateStart As Date
    Dim DateToCheck As String
    Dim myRestrictItems As Outlook.Items
	
    Set myNameSpace = Application.GetNamespace("MAPI")
    Set myContacts = myNameSpace.GetDefaultFolder(olFolderContacts).Items
    DateStart = #01/1/2003#
    DateToCheck = "[LastModificationTime] >= """ &amp; DateStart &amp; """"
    Set myRestrictItems = myContacts.Restrict(DateToCheck)
    For Each myItem In myRestrictItems
        If (myItem.Class = olContact) Then
            MsgBox myItem.FullName &amp; ": " &amp; myItem.LastModificationTime
        End If
    Next
End Sub
```

