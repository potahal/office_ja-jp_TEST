
# Application.AdvancedSearch, méthode (Outlook)

Effectue une recherche basée sur une chaîne de recherche DASL (DAV Searching and Locating) spécifiée.
 


## Syntaxe

*expression* . **AdvancedSearch**( ***Scope***, ***Filter***, ***SearchSubFolders***, ***Tag*** )
 

 
*expression* Variable qui représente un objet **[Application](797003e7-ecd1-eccb-eaaf-32d6ddde8348.md)**.
 

 

### Paramètres



|**Nom**|**Obligatoire/Facultatif**|**Type de données**|**Description**|
|:-----|:-----|:-----|:-----|
| _Scope_|Obligatoire|**Chaîne**|Portée de la recherche. Par exemple, le chemin d'accès à un dossier. Il est recommandé d'écrire le chemin entre des guillemets simples. Sinon, la recherche risque de ne pas renvoyer de résultats corrects si le chemin contient des caractères spéciaux, y compris des caractères Unicode. Pour spécifier plusieurs chemins, encadrez chacun d'eux par des guillemets simples et séparez-les par une virgule.|
| _Filter_|Facultatif|**Variante**|Filtre de recherche DASL qui définit les paramètres de la recherche.|
| _SearchSubFolders_|Facultatif|**Variante**|Détermine si la recherche inclut certains ou tous les sous-dossiers du dossier.|
| _Tag_|Facultatif|**Variante**|Nom donné en tant qu'identificateur pour la recherche.|

### Valeur renvoyée

Objet  **[Search](226a5d49-3caf-90dd-725c-265404d1939f.md)** qui représente les résultats de la recherche.
 

 

## Remarques

Vous pouvez effectuer plusieurs recherches simultanées en appelant la méthode  **AdvancedSearch** sur plusieurs lignes de code. Toutefois, sachez que lorsque vous créez par programme un grand nombre de dossiers de recherche, l'activité générée par les recherches simultanées peut avoir des répercussions négatives sur les performances d'Outlook, surtout si Outlook effectue les recherches en mode Exchange en ligne.
 

 
La méthode  **AdvancedSearch** et les fonctions associées dans le modèle objet Outlook ne crée pas de dossier de recherche qui apparaît dans l'interface utilisateur Outlook. Vous pouvez cependant utiliser la méthode **[Save](a6dbec81-67fd-e337-b640-4f94ab36218f.md)** de l'objet **Search** qui est renvoyée pour créer un dossier de recherche présent dans la liste de dossiers de recherche de l'interface utilisateur Outlook.
 

 
Le paramètre  _Scope_ permet de spécifier un ou plusieurs dossiers au sein du même magasin, mais vous ne pouvez pas spécifier des dossiers situés dans plusieurs magasins. Pour spécifier plusieurs dossiers compris dans le même magasin pour le paramètre _Scope_, encadrez chaque chemin d'accès de dossier par des guillemets simples et insérez des virgules pour séparer ces chemins. Pour les dossiers par défaut tels que Boîte de réception ou Éléments envoyés, vous pouvez utiliser simplement le nom du dossier au lieu de saisir l'intégralité du chemin d'accès. Par exemple, les deux lignes de code suivantes représentent des paramètres  _Scope_ valides :
 

 



```
Scope = "'Inbox', 'Sent Items'"
```




```
Scope = "'" &amp; Application.Session.GetDefaultFolder(olFolderInbox).FolderPath _  
    &amp; "','" &amp; Application.Session.GetDefaultFolder(olFolderSentMail).FolderPath &amp; "'"
```

Le paramètre  _Filter_ peut être une requête DASL valide. Pour plus d'informations sur les requêtes DASL, voir[Filtrage des éléments](http://msdn.microsoft.com/library/4038e042-1b07-5d18-18b0-c2b58c9c42da%28Office.15%29.aspx) et[Référencer les propriétés par un espace de noms](http://msdn.microsoft.com/library/c1c7bfa9-64d7-81d2-84e7-f0a4c57780b3%28Office.15%29.aspx). Notez que vous ne pouvez pas utiliser une requête JET pour le paramètre de recherche avancée  _Filter_. Si la recherche instantanée est activée sur une banque qui contient un dossier spécifié dans le paramètre  _Scope_, vous pouvez utiliser les mots clés de recherche instantanée pour obtenir de meilleurs résultats. Si vous utilisez les mots clés de recherche instantanée et si la recherche instantanée n'est pas activée, Outlook renvoie une erreur et la recherche échoue.
 

 

## Exemple

L'exemple Visual Basic pour Applications (VBA) suivant recherche dans la  **Boîte de réception** les éléments dont le sujet est _Test_ et affiche les noms des expéditeurs des éléments de messagerie renvoyés par la recherche. La procédure événementielle **[AdvancedSearchComplete](4f33ad44-20a3-62cd-aa1b-db74581ebb3c.md)** attribue la valeur **True** à la variable booléenne `blnSearchComp` à la fin de la recherche. Cette variable booléenne est utilisée par la procédure `TestAdvancedSearchComplete()` pour déterminer à quel moment se termine la recherche. L'exemple de code doit être placé dans un module de classe tel que `ThisOutlookSession` et la procédure `TestAdvancedSearchComplete()` doit être appelée avant que la procédure événementielle soit appelée par Outlook.
 

 

```
Public blnSearchComp As Boolean  
  
Private Sub Application_AdvancedSearchComplete(ByVal SearchObject As Search)  
    Debug.Print "The AdvancedSearchComplete Event fired"  
    If SearchObject.Tag = "Test" Then  
        m_SearchComplete = True  
    End If  
  
End Sub  
  
Sub TestAdvancedSearchComplete()  
    Dim sch As Outlook.Search  
    Dim rsts As Outlook.Results  
    Dim i As Integer  
    blnSearchComp = False  
    Const strF As String = "urn:schemas:mailheader:subject = 'Test'"  
    Const strS As String = "Inbox"     
    Set sch = Application.AdvancedSearch(strS, strF, ?Test?)   
    While blnSearchComp = False  
        DoEvents  
    Wend   
    Set rsts = sch.Results  
    For i = 1 To rsts.Count  
        Debug.Print rsts.Item(i).SenderName  
    Next  
End Sub  

```

L'exemple Microsoft Visual Basic pour Applications suivant indique comment utiliser la méthode  **AdvancedSearch** pour créer une recherche. Les paramètres de la recherche, tels que spécifiés par l'argument*Filter* de la méthode **AdvancedSearch**, renvoient tous les éléments des dossiers Boîte de réception et Éléments envoyés qui comportent le mot « Office » dans leur objet. Les dossiers Boîte de réception et Éléments envoyés de l'utilisateur sont spécifiés comme étendue de la recherche et la propriété **[SearchSubFolders](26dd1970-ba59-9f6a-8cf6-3dba0f9668b2.md)** prend la valeur **True**. Une fois la recherche terminée, la méthode **[GetTable](3aba6b77-73a3-9620-9c18-b2e03c7b63bc.md)** est appelée sur l'objet **[Search](226a5d49-3caf-90dd-725c-265404d1939f.md)** pour une énumération plus performante des résultats.
 

 



```
Public m_SearchComplete As Boolean  
  
Private Sub Application_AdvancedSearchComplete(ByVal SearchObject As Search)  
    If SearchObject.Tag = "MySearch" Then  
        m_SearchComplete = True  
    End If  
End Sub  
  
Sub TestSearchForMultipleFolders()  
    Dim Scope As String  
    Dim Filter As String  
    Dim MySearch As Outlook.Search  
    Dim MyTable As Outlook.Table  
    Dim nextRow As Outlook.Row  
    m_SearchComplete = False  
    'Establish scope for multiple folders  
    Scope = "'" &amp; Application.Session.GetDefaultFolder( _  
    olFolderInbox).FolderPath _  
    &amp; "','" &amp; Application.Session.GetDefaultFolder( _  
    olFolderSentMail).FolderPath &amp; "'"  
    'Establish filter  
    If Application.Session.DefaultStore.IsInstantSearchEnabled Then  
        Filter = Chr(34) &amp; "urn:schemas:httpmail:subject" _  
        &amp; Chr(34) &amp; " ci_phrasematch 'Office'"  
    Else  
        Filter = Chr(34) &amp; "urn:schemas:httpmail:subject" _  
        &amp; Chr(34) &amp; " like '%Office%'"  
    End If  
    Set MySearch = Application.AdvancedSearch( _  
    Scope, Filter, True, "MySearch")  
    While m_SearchComplete <> True  
        DoEvents  
    Wend  
    Set MyTable = MySearch.GetTable  
    Do Until MyTable.EndOfTable  
        Set nextRow = MyTable.GetNextRow()  
        Debug.Print nextRow("Subject")  
    Loop  
End Sub  

```


## Voir aussi


#### Concepts


 
[Objet Application](797003e7-ecd1-eccb-eaaf-32d6ddde8348.md)
#### Autres ressources


 
[Membres de l'objet Application](3519c89c-2353-85ee-7ddc-62e5dd85a8e7.md)