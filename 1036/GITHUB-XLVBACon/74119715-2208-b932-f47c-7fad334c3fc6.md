
# Faire référence à des plages nommées

Il est plus facile d'identifier des plages par leurs noms que par un style de référence A1. Pour nommer une plage sélectionnée, cliquez sur la zone nom à l'extrémité gauche de la barre de formule, tapez un nom puis appuyez sur Entrée.
 


## Référence à une plage nommée

L'exemple suivant montre comment faire référence à la plage nommée « MyRange » dans le classeur nommé « MyBook.xls ».
 

 

```
vba
Sub FormatRange() 
    Range("MyBook.xls!MyRange").Font.Italic = True 
End Sub
```

L'exemple suivant montre comment faire référence à la plage « Sheet1!Sales » du classeur « Report.xls ».
 

 



```
vba
Sub FormatSales() 
    Range("[Report.xls]Sheet1!Sales").BorderAround Weight:=xlthin 
End Sub
```

Pour sélectionner une plage nommée, utilisez la méthode  **GoTo** qui active le classeur et la feuille de calcul puis sélectionne la plage.
 

 



```
vba
Sub ClearRange() 
    Application.Goto Reference:="MyBook.xls!MyRange" 
    Selection.ClearContents 
End Sub
```

L'exemple suivant montre comment la même procédure doit être écrite pour le classeur actif.
 

 



```
vba
Sub ClearRange() 
    Application.Goto Reference:="MyRange" 
    Selection.ClearContents 
End Sub
```

 **Exemple de code fourni par :** Dennis Wallentin,[VSTO &amp; .NET &amp; Excel](http://xldennis.wordpress.com/)
 

 
Cet exemple utilise une plage nommée en tant que formule de validation des données. Pour cet exemple, les données de validation doivent apparaître sur la feuille 2 dans la plage A2:A100. Ces données de validation sont utilisées pour valider les données saisies dans la feuille 1 dans la plage D2:D10.
 

 



```
vba
Sub Add_Data_Validation_From_Other_Worksheet()
'The current Excel workbook and worksheet, a range to define the data to be validated, and the target range
'to place the data in.
Dim wbBook As Workbook
Dim wsTarget As Worksheet
Dim wsSource As Worksheet
Dim rnTarget As Range
Dim rnSource As Range

'Initialize the Excel objects and delete any artifacts from the last time the macro was run.
Set wbBook = ThisWorkbook
With wbBook
    Set wsSource = .Worksheets("Sheet2")
    Set wsTarget = .Worksheets("Sheet1")
    On Error Resume Next
    .Names("Source").Delete
    On Error GoTo 0
End With

'On the source worksheet, create a range in column A of up to 98 cells long, and name it "Source".
With wsSource
    .Range(.Range("A2"), .Range("A100").End(xlUp)).Name = "Source"
End With

'On the target worksheet, create a range 8 cells long in column D.
Set rnTarget = wsTarget.Range("D2:D10")

'Clear out any artifacts from previous macro runs, then set up the target range with the validation data.
With rnTarget
    .ClearContents
    With .Validation
        .Delete
        .Add Type:=xlValidateList, _
             AlertStyle:=xlValidAlertStop, _
             Formula1:="=Source"
        
'Set up the Error dialog with the appropriate title and message
        .ErrorTitle = "Value Error"
        .ErrorMessage = "You can only choose from the list."
    End With
End With

End Sub
```


## Boucle sur des cellules d'une plage nommée

L'exemple suivant exécute une boucle sur chacune des cellules d'une plage nommée à l'aide de la boucle  **For Each...Next**. Si la valeur d'une de ces cellules dépasse la valeur de la variable `Limit`, la couleur de la cellule devient jaune.
 

 

```
vba
Sub ApplyColor() 
    Const Limit As Integer = 25 
    For Each c In Range("MyRange") 
        If c.Value > Limit Then 
            c.Interior.ColorIndex = 27 
        End If 
    Next c 
End Sub
```


## À propos du collaborateur
<a name="AboutContributor"> </a>

Dennis Wallentin est l'auteur de VSTO &amp; .NET &amp; Excel, un blog consacré aux solutions .NET Framework pour Excel et Excel Services. Dennis développe des solutions Excel depuis plus de 20 ans et a également co-écrit « Professional Excel Development: The Definitive Guide to Developing Applications Using Microsoft Excel, VBA and .NET (2nd Edition) ».
 

 
