
# Range.Find, méthode (Excel)

Cette méthode recherche des informations spécifiques dans une plage.
 


## Syntaxe

*expression* . **Find**( ***What***, ***After***, ***LookIn***, ***LookAt***, ***SearchOrder***, ***SearchDirection***, ***MatchCase***, ***MatchByte***, ***SearchFormat*** )
 

 
*expression* Variable qui représente un objet **Range**.
 

 

### Paramètres



|**Nom**|**Requis/Facultatif**|**Type de données**|**Description**|
|:-----|:-----|:-----|:-----|
| _What_|Obligatoire|**Variante**|Données à rechercher. Il peut s'agir d'une chaîne ou de tout autre type de données Microsoft Excel.|
| _After_|Facultatif|**Variante**|Cellule après laquelle vous souhaitez commencer la recherche. Cela correspond à la position de la cellule active lorsqu'une recherche est effectuée à partir de l'interface utilisateur. Notez que  _After_ doit correspondre à une seule cellule de la plage. Rappelez-vous que la recherche commence après cette cellule ; la cellule spécifiée n'est pas recherchée tant que la méthode n'est pas revenue à cette cellule. Si vous ne spécifiez pas cet argument, la recherche commence après la cellule située dans le coin supérieur gauche de la plage.|
| _LookIn_|Facultatif|**Variante**|Type d'informations.|
| _LookAt_|Facultatif|**Variante**|Il peut s'agir de l'une des constantes  **XlLookAt** suivantes : **xlWhole** ou **xlPart**.|
| _SearchOrder_|Facultatif|**Variante**|Il peut s'agir de l'une des constantes  **XlSearchOrder** suivantes : **xlByRows** ou **xlByColumns**.|
| _SearchDirection_|Facultatif|[XlSearchDirection](fc53fda7-d01c-c407-ccdb-d12004a4dcee.md)|Sens de la recherche.|
| _MatchCase_|Facultatif|**Variante**|**True** pour que la recherche respecte la casse. La valeur par défaut est **False**.|
| _MatchByte_|Facultatif|**Variante**|S'utilise uniquement si vous avez sélectionné ou installé la prise en charge des langues codées sur deux octets.  **True** pour que les caractères codés sur deux octets ne correspondent qu'aux caractères codés sur deux octets. **False** pour que les caractères codés sur deux octets correspondent à leurs équivalents codés sur un octet.|
| _SearchFormat_|Facultatif|**Variante**|Format de la recherche.|

### Valeur renvoyée

Objet [Range](b8207778-0dcc-4570-1234-f130532cc8cd.md) qui représente la première cellule où ces informations sont trouvées.
 

 

## Remarques

Cette méthode renvoie  **Nothing** si aucune correspondance n'est trouvée. La méthode **Find** n'affecte pas la sélection ni la cellule active.
 

 
Les paramètres des arguments  _LookIn_,  _LookAt_,  _SearchOrder_ et _MatchByte_ sont enregistrés chaque fois que vous utilisez cette méthode. Si vous ne spécifiez aucune valeur pour ces arguments lors du prochain appel à la méthode, les valeurs enregistrées sont utilisées. Le fait de définir ces arguments modifie les paramètres de la boîte de dialogue **Rechercher**, ce qui a pour effet de changer les paramètres enregistrés qui sont utilisés lorsque vous ne spécifiez pas les arguments. Pour éviter toute ambiguïté, définissez ces arguments explicitement chaque fois que vous utilisez cette méthode.
 

 
Les méthodes [FindNext](308c6241-2398-13e6-ba68-17ec713376f6.md) et[FindPrevious](c03f2e17-d28c-8b0d-b8c8-024863523c99.md) permettent de répéter la recherche.
 

 
Lorsque la recherche atteint la fin de la plage de recherche spécifiée, elle revient au début de cette plage. Pour arrêter une recherche lorsqu'elle revient au point de départ, enregistrez l'adresse de la première cellule trouvée, puis comparez l'adresse de chaque cellule ultérieurement trouvée avec l'adresse enregistrée.
 

 
Pour effectuer des recherches plus complexes, utilisez une instruction  `For Each...Next` avec l'opérateur **Like**. Par exemple, le code suivant effectue une recherche dans toutes les cellules de la plage A1:C5 qui utilise une police dont le nom commence par les lettres Cour. Lorsque Microsoft Excel trouve une cellule correspondante, il lui affecte la police Times New Roman.
 

 
 `For Each c In [A1:C5] If c.Font.Name Like "Cour*" Then c.Font.Name = "Times New Roman" End If Next`
 

 

 

 

## Exemple

Cet exemple montre comment rechercher toutes les cellules de la plage A1:A500 dans la première feuille de calcul contenant la valeur 2 et comment la remplacer par la valeur 5.
 

 

```
With Worksheets(1).Range("a1:a500") 
    Set c = .Find(2, lookin:=xlValues) 
    If Not c Is Nothing Then 
        firstAddress = c.Address 
        Do 
            c.Value = 5 
            Set c = .FindNext(c) 
        Loop While Not c Is Nothing And c.Address <> firstAddress 
    End If 
End With
```

 **Exemple de code fourni par :** Holy Macro! Books,[Holy Macro! It's 2,500 Excel VBA Examples](http://www.mrexcel.com/store/index.php?l=product_detail&amp;p=1)
 

 
Cet exemple prend un chemin et un nom de classeur, ainsi qu'un élément à rechercher, puis recherche ce dernier dans le classeur spécifié. S'il trouve l'élément recherché, l'adresse du résultat est stockée dans la cellule D10 du classeur actuel.
 

 



```
Option Explicit

Sub FindAddress()
    'Defining the variables.
    Dim GCell As Range
    Dim Page$, Txt$, MyPath$, MyWB$, MySheet$
    
    
    'The text for which to search.
    Txt = "Hello"
    'The path to the workbook in which to search.
    MyPath = "C:\Your\File\Path\"
    'The name of the workbook in which to search.
    MyWB = "YourFileName.xls"
    
    'Use the current sheet as the place to store the data for which to search.
    MySheet = ActiveSheet.Name
    
    'If an error occurs, use the error handling routine at the end of this file.
    On Error GoTo ErrorHandler
    
    'Turn off screen updating, and then open the target workbook.
    Application.ScreenUpdating = False
    Workbooks.Open FileName:=MyPath &amp; MyWB
    
    'Search for the specified text
    Set GCell = ActiveSheet.Cells.Find(Txt)
    
    'Record the address of the data, along with the date, in the current workbook.
    With ThisWorkbook.ActiveSheet.Range("D10")
        .Value = "Address of " &amp; Txt &amp; ":"
        .Offset(0, 1).Value = "Date:"
        .Offset(1, 0).Value = GCell.Address
        .Offset(1, 1).Value = Date
        .Columns.AutoFit
        .Offset(1, 1).Columns.AutoFit
    End With
    
    'Close the data workbook, without saving any changes, and turn screen updating back on.
    ActiveWorkbook.Close savechanges:=False
    Application.ScreenUpdating = True
Exit Sub

'Error Handling section.
ErrorHandler:
Select Case Err.Number
        'Common error #1: file path or workbook name is wrong.
        Case 1004
            Range("D10:E11").ClearContents
            Application.ScreenUpdating = True
            MsgBox "The workbook " &amp; MyWB &amp; " could not be found in the path" &amp; vbCrLf &amp; MyPath &amp; "."
        Exit Sub
        
        'Common error #2: the specified text wasn't in the target workbook.
        Case 9, 91
            ThisWorkbook.Sheets(MySheet).Range("D10:E11").ClearContents
            Workbooks(MyWB).Close False
            Application.ScreenUpdating = True
            MsgBox "The value " &amp; Txt &amp; " was not found."
        Exit Sub
        
        'General case: turn screenupdating back on, and exit.
        Case Else
            Application.ScreenUpdating = True
        Exit Sub
End Select

End Sub
```


## À propos du collaborateur
<a name="AboutContributor"> </a>

Holy Macro! Books publie des livres divertissants pour les personnes qui utilisent Microsoft Office. Vous pouvez consulter le catalogue complet sur MrExcel.com.
 

 

## Voir aussi
<a name="AboutContributor"> </a>


#### Concepts


 
[Objet Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)
#### Autres ressources


 
[Membres de la plage](4336bf81-1e63-7e44-1792-baf366a027a7.md)