
# Extraire les données d'un enregistrement dans un jeu d'enregistrements DAO

Après avoir localisé un ou des enregistrements, vous pouvez souhaiter extraire les données afin de les utiliser dans votre application au lieu de les modifier dans la table source sous-jacente.
 


## Copie d'un seul champ

Vous pouvez copier un seul champ d'un enregistrement vers une variable du type de données approprié. L'exemple suivant extrait trois champs du premier enregistrement dans un objet  **[Recordset](http://msdn.microsoft.com/library/9774232C-E6DA-175B-FC7F-ED2AB7908FA0%28Office.15%29.aspx)**.
 

 

```
Dim dbsNorthwind As DAO.Database 
Dim rstEmployees As DAO.Recordset 
Dim strFirstName As String 
Dim strLastName As String 
Dim strTitle As String 
 
   Set dbsNorthwind = CurrentDb 
   Set rstEmployees = dbsNorthwind.OpenRecordset("Employees") 
 
   rstEmployees.MoveFirst 
   strFirstName = rstEmployees!FirstName 
   strLastName = rstEmployees!LastName 
   strTitle = rstEmployees!Title 

```


## Copie de tous les enregistrements dans un tableau

Pour copier un ou plusieurs enregistrements, vous pouvez créer un tableau bidimensionnel et copier un enregistrement à la fois. Vous incrémentez le premier indice de chaque champ et le second indice de chaque enregistrement.
 

 
Pour cela, utilisez la méthode  **[GetRows](http://msdn.microsoft.com/library/59F6E4F0-E7B1-DB60-31C7-3338B66D3345%28Office.15%29.aspx)**, qui renvoie une tableau bidimensionnel. Le premier indice identifie le champ, tandis que le second identifie le numéro de ligne, comme suit.
 

 



```
varRecords(intField, intRecord) 

```

L'exemple de code suivant utilise une instruction SQL pour extraire 3 champs d'une table intitulée Employés dans un objet  **Recordset**. Ensuite, il utilise la méthode **GetRows** pour extraire les 3 premiers enregistrements du **jeu d'enregistrement** et stocke les enregistrements sélectionnés dans un tableau à 2 dimensions. Puis, il imprime chaque enregistrement, champ par champ, en utilisant les 2 index de tableau pour sélectionner les champs et enregistrements.
 

 
Pour illustrer l'utilisation des index de tableau, l'exemple suivant utilise une instruction distincte pour identifier et imprimer chaque champ de chaque enregistrement. Dans la pratique, il est plus fiable d'utiliser deux boucles, l'une imbriquée dans l'autre, et de fournir des variables de type entier pour les index qui concernent les deux dimensions du tableau.
 

 



```
Sub GetRowsTest() 
 
Dim dbsNorthwind As DAO.Database 
Dim rstEmployees As DAO.Recordset 
Dim varRecords As Variant 
Dim intNumReturned As Integer 
Dim intNumColumns As Integer 
Dim intColumn As Integer 
Dim intRow As Integer 
Dim strSQL As String 
 
On Error GoTo ErrorHandler 
 
   Set dbsNorthwind = CurrentDb 
   strSQL = "SELECT FirstName, LastName, Title FROM Employees" 
   Set rstEmployees = dbsNorthwind.OpenRecordset(SQL, dbOpenSnapshot) 
 
   varRecords = rstEmployees.GetRows(3) 
   intNumReturned = UBound(varRecords, 2) + 1 
   intNumColumns = UBound(varRecords, 1) + 1 
 
   For intRow = 0 To intNumReturned - 1 
      For intColumn = 0 To intNumColumns - 1 
         Debug.Print varRecords(intColumn, intRow) 
      Next intColumn 
   Next intRow 
 
   rstEmployees.Close 
   dbsNorthwind.Close 
 
   Set rstEmployees = Nothing 
   Set dbsNorthwind = Nothing 
 
Exit Sub 
 
ErrorHandler: 
   MsgBox "Error #: " &amp; Err.Number &amp; vbCrLf &amp; vbCrLf &amp; Err.Description 
End Sub 

```

Vous pouvez utiliser des appels consécutifs de la méthode  **GetRows** si plusieurs enregistrements sont disponibles. Comme le tableau est complété dès que vous appelez la méthode **GetRows**, vous voyez tout de suite pourquoi cette approche est beaucoup plus rapide que de copier un champ à la fois.
 

 
Notez également que vous ne devez pas déclarer le Variant en tant que tableau, car cette déclaration se fait automatiquement lorsque la méthode  **GetRows** renvoie des enregistrements. Vous pouvez ainsi utiliser des dimensions de tableau de longueur fixe sans savoir combien d'enregistrements ou de champs seront renvoyés, au lieu d'utiliser des dimensions de longueur variable qui occupent de la mémoire.
 

 
Si vous tentez d'extraire toutes les lignes à l'aide de plusieurs appels  **GetRows**, utilisez la propriété **[EOF](http://msdn.microsoft.com/library/AA82C6F9-89DA-1061-437C-8FFB000744B6%28Office.15%29.aspx)** pour être certain d'être à la fin du **jeu d'enregistrements**. La méthode **GetRows** peut renvoyer moins de lignes que demandé. Si vous demandez plus de lignes que le nombre de lignes restantes dans un **jeu d'enregistrements**, par exemple, la méthode **GetRows** ne renvoie que les lignes restantes. De même, s'il est impossible d'extraire une ligne dans la plage demandée, cette ligne ne sera pas renvoyée. Par exemple, s'il est impossible d'extraire le cinquième enregistrement d'un groupe de 10 enregistrements que vous tentez d'extraire, la méthode **GetRows** renverra quatre enregistrements, laissera la position actuelle sur l'enregistrement qui a posé un problème et ne générera pas d'erreur d'exécution. Cette situation peut se produire si un enregistrement d'une feuille de réponse dynamique a été supprimé par un autre utilisateur. Si la méthode renvoie moins d'enregistrements que le nombre demandé et que vous n'êtes pas à la fin du fichier, vous devez lire chaque champ de l'enregistrement actif pour déterminer l'erreur rencontrée par la méthode **GetRows**.
 

 
Comme la méthode  **GetRows** renvoie toujours tous les champs de l'objet **Recordset**, vous pouvez souhaiter créer une requête qui renvoie uniquement les champs requis. Ceci est particulièrement important pour les champs de type Objet OLE et Mémo.
 

 
