
# Faire référence à des objets contrôle Onglet dans VBA

Vous pouvez utiliser un contrôle Onglet pour présenter plusieurs pages d'informations sur un seul formulaire. Le contrôle Onglet est utile lorsque votre formulaire contient des informations qui peuvent être stockées dans au moins deux catégories.
 

Dans la plupart des cas, le contrôle Onglet fonctionne comme les autres contrôles sur un formulaire et il est possible d'y faire référence en tant que membre de la collection  **[Controls](26771888-86E8-28C3-6668-F793474CBB5B.md)** d'un formulaire. Par exemple, pour faire référence à un contrôle Onglet nommé TabControl1 sur un formulaire nommé Form1, vous pouvez utiliser l'expression suivante :
 



```
Form1.Controls!TabControl1 

```

Toutefois, étant donné que la collection  **Controls** est la collection par défaut de l'objet **[Form](72EF9219-142B-B690-B696-3EBA9A5D4522.md)**, vous n'avez pas à faire explicitement référence à la collection **Controls**. Autrement dit, vous pouvez omettre la référence à la collection **Controls** dans l'expression, comme suit :
 



```
Form1!TabControl1 

```


## Faire référence à la collection Pages

Un contrôle Onglet contient une ou plusieurs pages. Chaque page d'un contrôle Onglet est référencée en tant que membre de la collection  **[Pages](DC628CFA-9550-36E6-0AA1-06CF5E80FA25.md)** du contrôle Onglet. Il est possible de faire référence à chaque page de la collection **Pages** par sa propriété **[PageIndex](22B71F19-2734-F735-8A64-D02901C598C0.md)** (qui indique la position de la page dans la collection, en partant de 0), ou par son paramètre de propriété **[Name](FCF25A8E-3DC8-77E8-1969-3B672B8B94EC.md)**. Il n'existe aucune collection par défaut pour l'objet **[TabControl](05F7DE7B-8665-DF6D-3FBB-47F8547D3BAF.md)**, par conséquent, lorsque vous faites référence à des éléments de la collection **Pages** par leur valeur d'index, ou à des propriétés de la collection **Pages**, vous devez explicitement faire référence à la collection **Pages**.
 

 
Par exemple, pour modifier la valeur de la propriété  **[Caption](7F1B5038-4543-C373-96E9-135102CDD6E6.md)** pour la première page d'un contrôle Onglet nommé TabControl1 en faisant référence à sa valeur d'index dans la collection **Pages**, vous pouvez utiliser l'instruction suivante :
 

 



```
TabControl1.Pages(0).Caption = "First Page" 

```

Étant donné que chaque page est membre de la collection  **Controls** du formulaire, vous pouvez faire référence à une page uniquement par sa propriété **Name** sans faire référence au nom du contrôle Onglet ou à sa collection **Pages**. Par exemple, pour modifier la valeur de la propriété **Caption** d'une page avec sa propriété **Name** définie sur Page1, utilisez l'instruction suivante :
 

 



```
Page1.Caption = "First Page" 

```


 **Remarque**  Si un utilisateur ou un code modifie la propriété  **PageIndex** d'une page, la référence à l'index et à la position de la page est modifiée dans l'ordre de la page. Dans ce cas, si vous voulez maintenir une référence absolue à une page, faites référence à la propriété **Name** de cette page.
 

La collection  **Pages** possède une propriété, **[Count](6BF4C0B5-5EE5-A02E-ADA8-4D9771287A82.md)**, qui renvoie le nombre de pages d'un contrôle Onglet. Notez que cette propriété n'est pas une propriété du contrôle Onglet lui-même, mais de sa collection **Pages**, vous devez donc explicitement y faire référence. Par exemple, pour déterminer le nombre de pages dans TabControl1, utilisez l'instruction suivante :
 

 



```
TabControl1.Pages.Count 

```


## Faire référence à la page actuelle et la modifier

La propriété par défaut d'un contrôle Onglet est  **[Value](85849D32-3EF9-B959-FE07-026DE226623E.md)**, qui renvoie un entier qui identifie la page actuelle : 0 pour la première page, 1 pour la deuxième page, etc. La propriété **Value** n'est disponible que dans le code VBA ou dans des expressions. En lisant la propriété **Value** lors de l'exécution, vous pouvez déterminer quelle page est actuellement la première. Par exemple, l'instruction suivante renvoie la valeur de la page actuelle de TabControl1 :
 

 

```
TabControl1.Value 

```


 **Remarque**  Étant donné que la propriété  **Value** est la propriété par défaut d'un contrôle Onglet, vous n'avez pas besoin d'y faire référence de manière explicite. Par conséquent, vous pouvez omettre `.Value` dans l'exemple précédent.
 

La définition de la propriété  **Value** d'un contrôle Onglet fait de la page spécifiée la page actuelle. Par exemple, l'instruction suivante fait de la troisième page du TabControl1 la page actuelle :
 

 



```
TabControl1 = 2 

```

Ceci est utile si vous définissez une propriété  **[Style](DE0859CD-27AF-294B-DA0C-EF2055180B21.md)** d'un contrôle Onglet sur Aucun (aucun onglet n'est affiché) et si vous voulez utiliser les boutons de commande du formulaire pour déterminer quelle page est mise en avant. Pour utiliser un bouton de commande pour afficher une page, ajouter une procédure d'événement sur l'événement **[OnClick](1034AA82-58CD-F639-D936-326049CCF38C.md)** du bouton qui définit la propriété **Value** du contrôle Onglet sur l'entier qui identifie la page appropriée.
 

 
À l'aide de la propriété  **Value** et de la collection **Pages**, vous pouvez définir les propriétés au moment de l'exécution pour la première page. Par exemple, vous pouvez masquer la page actuelle et tous ses contrôles en définissant la propriété **[Visible](D01A5C26-18EE-2533-38D7-98A7BB84A971.md)** de cette page sur **False**. L'instruction suivante masque la page actuelle de TabControl1 :
 

 



```
TabControl1.Pages(TabControl1).Visible = False 

```

Chaque page d'un contrôle Onglet comporte également une propriété  **PageIndex** qui spécifie la position d'une page dans la collection **Pages** à l'aide de la même séquence de numérotation que la propriété **Value** du contrôle Onglet : 0 pour la première page, 1 pour la deuxième page, etc. La définition de la valeur de la propriété **PageIndex** d'une page modifie l'ordre d'apparition des pages dans le contrôle Onglet. Par exemple, si vous voulez que la page nommée Page1 soit la deuxième page, utilisez l'instruction suivante :
 

 



```
Page1.PageIndex = 1 

```

La propriété  **PageIndex** est plus généralement définie au moment de la création de la feuille de propriété d'une page. Vous pouvez également définir l'ordre des pages en cliquant avec le bouton droit sur la bordure d'un contrôle Onglet, puis en cliquant sur **Ordre des pages** dans le menu contextuel.
 

 

## Faire référence à des contrôles sur une page de contrôle Onglet

Les contrôles que vous placez sur une page de contrôle Onglet font partie de la même collection que tous les contrôles du formulaire. Par conséquent, chaque contrôle d'une page de contrôle Onglet doit avoir un nom unique par rapport à tous les autres contrôles du même formulaire. Vous pouvez faire référence aux contrôles d'une page de contrôle Onglet à l'aide de la même syntaxe que pour les contrôles d'un formulaire sans contrôle Onglet.
 

 

```
Forms!Employees!HomePhone 

```

Étant donné que chaque contrôle d'un formulaire possède sa propre collection  **Controls**, vous pouvez également faire référence aux contrôles sur un contrôle Onglet en tant que membres de sa collection **Controls**. Par exemple, le code suivant énumère (répertorie) tous les contrôles du contrôle Onglet du formulaire Employees.
 

 



```
Sub ListTabControlControls() 
 
   Dim tabCtl As TabControl 
   Dim ctlCurrent As Control 
 
On Error GoTo ErrorHandler 
 
   ' Return reference to tab control on Employees form. 
   Set tabCtl = Forms!Employees!TabCtl0 
 
   ' List all controls on the tab control in the Debug window. 
   For Each ctlCurrent In tabCtl 
      Debug.Print ctlCurrent.Name 
   Next ctlCurrent 
 
   Set tabCtl = Nothing 
   Set ctlCurrent = Nothing 
 
   Exit Sub 
 
ErrorHandler: 
   MsgBox "Error #: " &amp; Err.Number &amp; vbCrLf &amp; vbCrLf &amp; Err.Description 
End Sub 

```

En outre, chaque page d'un contrôle Onglet possède sa propre collection  **Controls**. À l'aide de la collection **Controls** d'une page, vous pouvez faire référence aux contrôles de chaque page. Le code suivant énumère les contrôles de chaque page du contrôle Onglet du formulaire Employees.
 

 



```
Sub ListPageControls() 
 
   Dim tabCtl As TabControl 
   Dim pagCurrent As Page 
   Dim ctlCurrent As Control 
   Dim intPageNum As Integer 
 
On Error GoTo ErrorHandler 
 
   ' Return reference to tab control on Employees form. 
   Set tabCtl = Forms!Employees!TabCtl0 
 
   ' List all controls for each page on the tab control in the 
   ' Debug window. 
   For Each pagCurrent In tabCtl.Pages 
      intPageNum = intPageNum + 1 
      Debug.Print "Page " &amp; intPageNum &amp; " Controls:" 
      For Each ctlCurrent In pagCurrent.Controls 
         Debug.Print ctlCurrent.Name 
      Next ctlCurrent 
      Debug.Print 
   Next pagCurrent 
 
   Set tabCtl = Nothing 
   Set ctlCurrent = Nothing 
   Set pagCurrent = Nothing 
 
   Exit Sub 
 
ErrorHandler: 
   MsgBox "Error #: " &amp; Err.Number &amp; vbCrLf &amp; vbCrLf &amp; Err.Description 
End Sub 

```

