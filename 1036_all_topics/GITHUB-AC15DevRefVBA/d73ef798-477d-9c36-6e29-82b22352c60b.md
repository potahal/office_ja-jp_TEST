
# Form.RecordsetClone, propriété (Access)

La propriété  **RecordsetClone** vous permet de faire référence à l'objet **Recordset** d'un formulaire spécifié par la propriété **[RecordSource](a473695a-7645-744d-bf69-760e1f2b9fb1.md)** de ce formulaire. En lecture seule.


## Syntaxe

 _expression_. **RecordsetClone**

 _expression_ Variable représentant un objet **Form**.


## Remarques

Le paramétrage de la propriété  **RecordsetClone** est une copie de la requête ou table sous-jacente spécifiée par la propriété **RecordSource** du formulaire. Si un formulaire est basé sur une requête, par exemple, toute référence à la propriété **RecordsetClone** est équivalente au clonage de l'objet **Recordset** à l'aide de la même requête. Si vous appliquez alors un filtre au formulaire, l'objet **Recordset** reflète le filtrage.

Cette propriété n'est disponible qu'avec [Visual Basic](http://msdn.microsoft.com/library/3fa3677b-a779-3bc7-0f0f-827c252b3292%28Office.15%29.aspx) et elle est en lecture seule dans tous les modes.

Utilisez la propriété  **RecordsetClone** pour vous déplacer ou effectuer des opérations dans les enregistrements d'un formulaire indépendants du formulaire lui-même. Par exemple, vous pouvez utiliser la propriété **RecordsetClone** lorsque vous souhaitez vous servir d'une méthode, telle que la méthode DAO **Find**, qui ne peut pas être utilisée avec des formulaires.

Lorsqu'un nouvel objet  **Recordset** est ouvert, son premier enregistrement est l'enregistrement en cours. Si vous utilisez une des méthodes **Find** ou **Move** pour activer un enregistrement quelconque de l'objet **Recordset**, vous devez d'abord synchroniser l'enregistrement en cours dans l'objet **Recordset** avec l'enregistrement en cours du formulaire en affectant la valeur de la propriété DAO **Bookmark** à la propriété **[Bookmark](e214a924-9110-a3de-9812-b9ec5cbad8ed.md)** du formulaire.

 **Lien fourni par :**
![Icône de membre de la communauté](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) Communauté[UtterAccess](http://www.utteraccess.com)


- [Transférer des données d'Access vers Excel](http://www.utteraccess.com/forum/Transfer-data-Access-Ex-t1672619.mdl)
    

## Exemple

L'exemple suivant utilise la propriété  **RecordsetClone** pour créer un clone de l'objet **Recordset** à partir du formulaire Commandes, puis imprime les noms des champs dans la fenêtre Exécution.


```
Sub Print_Field_Names() 
    Dim rst As Recordset, intI As Integer 
    Dim fld As Field 
 
    Set rst = Me.RecordsetClone 
    For Each fld in rst.Fields 
        ' Print field names. 
        Debug.Print fld.Name 
    Next 
End Sub
```

L'exemple suivant utilise la propriété  **RecordsetClone** et l'objet **Recordset** pour synchroniser l'enregistrement d'un jeu d'enregistrements avec l'enregistrement en cours du formulaire. Lorsque vous sélectionnez le nom d'une société dans une zone de liste modifiable, la méthode **FindFirst** permet de localiser l'enregistrement de cette société et la propriété **Bookmark** des objets d'accès aux données de l'objet **Recordset** est affectée à la propriété **Bookmark** du formulaire, ce qui entraîne l'affichage de l'enregistrement trouvé sur le formulaire.




```
Sub SupplierID_AfterUpdate() 
    Dim rst As Recordset 
    Dim strSearchName As String 
 
    Set rst = Me.RecordsetClone 
    strSearchName = Str(Me!SupplierID) 
    rst.FindFirst "SupplierID = " &amp; strSearchName 
        If rst.NoMatch Then 
            MsgBox "Record not found" 
        Else 
            Me.Bookmark = rst.Bookmark 
        End If 
    rst.Close 
End Sub
```

Vous pouvez utiliser la propriété  **RecordCount** pour compter le nombre d'enregistrements dans un objet **Recordset**. L'exemple suivant vous indique comment combiner les propriétés **RecordCount** et **RecordsetClone** pour compter les enregistrements d'un formulaire :




```
Forms!Orders.RecordsetClone.MoveLast 
MsgBox "My form contains " _ 
    &amp; Forms!Orders.RecordsetClone.RecordCount _ 
    &amp; " records.", vbInformation, "Record Count"
```


## À propos des collaborateurs
<a name="AboutContributors"> </a>

UtterAccess est le premier forum d'aide et wiki de Microsoft Access. Cliquez ici pour le rejoindre.


## Voir aussi
<a name="AboutContributors"> </a>


#### Concepts


[Objet Form](72ef9219-142b-b690-b696-3eba9a5d4522.md)
#### Autres ressources


[Membres de l'objet Form](e1976b58-28ca-8f76-cdf3-6732cb06ce6c.md)