
# ListBox.Recordset, propriété (Access)

Renvoie ou définit l'objet  **Recordset** ADO ou **[Recordset](http://msdn.microsoft.com/library/9774232C-E6DA-175B-FC7F-ED2AB7908FA0%28Office.15%29.aspx)** DAO représentant la source d'enregistrement de l'objet spécifié. **Objet** en lecture/écriture.


## Syntaxe

 _expression_. **Recordset**

 _expression_ Variable qui représente un objet **ListBox**.


## Remarques

La propriété  **Recordset** renvoie l'objet recordset qui fournit les données parcourues dans un formulaire, un rapport, un contrôle de zone de liste ou un contrôle zone de liste déroulante. Si un formulaire est basé sur une requête, par exemple, faire référence à la propriété **Recordset** revient à cloner un objet **Recordset** à l'aide de la même requête. Toutefois, contrairement à l'utilisation de la propriété **RecordsetClone**, modifier l'enregistrement en cours dans le recordset renvoyé par la propriété **Recordset** du formulaire définit également l'enregistrement en cours du formulaire.

Le comportement en lecture/écriture de la propriété  **Recordset** est déterminé par le type de recordset (ADO ou DAO) et le type de données (Access ou SQL) contenu dans le recordset identifié par la propriété.



|**Type d'objet Recordset**|**Basé sur des données SQL**|**Basé sur les données stockées par le moteur de base de données Access**|
|:-----|:-----|:-----|
|**ADO**|Lecture/écriture|Lecture/écriture|
|**DAO**|S/O|Lecture/écriture|
L'exemple suivant montre comment ouvrir un formulaire et un recordset, puis comment les lier en définissant la propriété  **Recordset** du formulaire sur l'objet **Recordset** nouvellement créé.




```
Global rstSuppliers As ADODB.Recordset 
Sub MakeRW()      
    DoCmd.OpenForm "Suppliers" 
    Set rstSuppliers = New ADODB.Recordset 
    rstSuppliers.CursorLocation = adUseClient 
    rstSuppliers.Open "Select * From Suppliers", _ 
         CurrentProject.Connection, adOpenKeyset, adLockOptimistic      
    Set Forms("Suppliers").Recordset = rstSuppliers 
End Sub
```

Utilisez la propriété  **Recordset**:


- Pour utiliser des méthodes avec l'objet  **Recordset** qui ne sont pas directement prises en charge dans les formulaires. Par exemple, vous pouvez utiliser la propriété **Recordset** des méthodes **Rechercher** ADO ou **Rechercher** DAO dans une boîte de dialogue personnalisée pour rechercher un enregistrement.
    
- Envelopper une transaction (qui peut être révoquée) dans un ensemble de modifications affectant plusieurs formulaires.
    
Modifier la propriété  **Recordset** d'un formulaire peut également modifier les propriétés **RecordSource**, **RecordsetType** et **RecordLocks**. En outre, il se peut que certaines propriétés associées aux données soient remplacées, par exemple, les propriétés **Filter**, **FilterOn**, **OrderBy** et **OrderByOn**.

Appeler la méthode  **Actualiser** du recordset d'un formulaire (par exemple, `Forms(0).Recordset.Requery`) peut remettre en cause la liaison. Pour actualiser les données d'un formulaire lié à un recordset, définissez la propriété  **RecordSource** du formulaire sur lui-même ( `Forms(0).RecordSource = Forms(0).RecordSource`).


- Lier plusieurs formulaires à un jeu de données commun. Cela permet de synchroniser plusieurs formulaires, par exemple :
    



```
   Set Me.Recordset = Forms!Form1.Recordset
```

Une erreur se produit lorsqu'un formulaire est lié à un jeu d'enregistrements si vous utilisez la commande Filtrer par formulaire.


## Exemple

L'exemple suivant utilise la propriété  **Recordset** pour créer une copie de l'objet **Recordset** du formulaire actuel, puis imprime le nom des champs dans la fenêtre Débogage.


```
Sub Print_Field_Names() 
    Dim rst As DAO.Recordset, intI As Integer 
    Dim fld As Field 
 
    Set rst = Me.Recordset 
    For Each fld in rst.Fields 
        ' Print field names. 
        Debug.Print fld.Name 
    Next 
End Sub
```

L'exemple suivant utilise la propriété  **Recordset** et l'objet **Recordset** pour synchroniser un recordset avec l'enregistrement en cours du formulaire. Lorsqu'un nom de société est sélectionné dans la zone de liste modifiable, la méthode **FindFirst** permet de rechercher l'enregistrement correspondant à cette société, puis de l'afficher dans le formulaire.




```
Sub SupplierID_AfterUpdate() 
    Dim rst As DAO.Recordset 
    Dim strSearchName As String 
 
    Set rst = Me.Recordset 
    strSearchName = CStr(Me!SupplierID) 
    rst.FindFirst "SupplierID = " &amp; strSearchName 
    If rst.NoMatch Then 
        MsgBox "Record not found" 
    End If 
    rst.Close 
End Sub
```

Le code suivant permet de déterminer le type de recordset renvoyé par la propriété  **Recordset** sous différentes conditions.




```
Sub CheckRSType() 
    Dim rs as Object 
 
    Set rs=Forms(0).Recordset 
    If TypeOf rs Is DAO.Recordset Then 
        MsgBox "DAO Recordset" 
    ElseIf TypeOf rs is ADODB.Recordset Then 
        MsgBox "ADO Recordset" 
    End If 
End Sub
```


## Voir aussi


#### Concepts


[Objet ListBox](6bc00755-34e7-4fc2-8e72-40dae2010dd8.md)
#### Autres ressources


[Membres de l'objet ListBox](d87ad51b-9a46-21f3-f6d6-ef98ea8aaf6d.md)