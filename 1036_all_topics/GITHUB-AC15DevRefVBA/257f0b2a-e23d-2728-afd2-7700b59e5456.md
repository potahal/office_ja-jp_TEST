
# Application.DCount, méthode (Access)

La fonction  **DCount** détermine le nombre d'enregistrements dans le jeu d'enregistrements spécifié (ou domaine).


## Syntaxe

 _expression_. **DCount**( ** _Expr_**, ** _Domain_**, ** _Criteria_** )

 _expression_ Variable qui représente un objet **Application**.


### Paramètres



|**Nom**|**Requis/Facultatif**|**Type de données**|**Description**|
|:-----|:-----|:-----|:-----|
| _Expr_|Obligatoire|**Chaîne**|Expression qui identifie le champ dont vous voulez compter les enregistrements. Il peut s'agir d'une expression chaîne identifiant un champ dans une table ou une requête, ou d'une expression qui effectue un calcul sur les données de ce champ. Dans  _expr_, vous pouvez inclure le nom d'un champ de table, un contrôle de formulaire, une constante ou une fonction. Si  _expr_ inclut une fonction, elle peut être intrinsèque ou définie par l'utilisateur, mais ne peut pas être une autre fonction de regroupement Domaine ou SQL.|
| _Domain_|Obligatoire|**Chaîne**|Expression de chaîne identifiant les enregistrements qui constituent le domaine. Il peut s'agir d'un nom de table ou d'un nom de requête qui ne nécessite aucun paramètre.|
| _Criteria_|Facultatif|**Variante**|Expression de chaîne facultative servant à limiter la plage des données sur laquelle porte la fonction  **DCount**. Par exemple, _criteria_ est souvent équivalent à la clause WHERE d'une expression SQL sans le mot WHERE. En cas d'omission de _criteria_, la fonction  **DCount** évalue _expr_ par rapport à l'intégralité du domaine. Chaque champ compris dans _criteria_ doit l'être aussi dans _domain_, sans quoi la fonction  **DCount** renvoie la valeur **Null**.|

### Valeur renvoyée

Variante


## Remarques

Par exemple, vous pouvez utiliser la fonction  **DCount** dans un module pour renvoyer le nombre d'enregistrements dans une table Commandes qui correspondent aux commandes passées à une date particulière.

Utilisez la fonction  **DCount** pour compter le nombre d'enregistrements d'un domaine si vous ne vous intéressez pas à leurs valeurs spécifiques. L'argument _expr_ peut effectuer un calcul sur un champ, mais la fonction **DCount** indique simplement le nombre d'enregistrements, quel que soit le calcul effectué par _expr_.

Utilisez la fonction  **DCount** dans un contrôle calculé, lorsque vous devez déterminer des critères visant à limiter la plage des données sur laquelle porte la fonction. Par exemple, pour afficher le nombre de commandes qui doivent être livrées en Californie, affectez l'expression suivante à la propriété **ControlSource** d'une zone de texte :




```
=DCount("[OrderID]", "Orders", "[ShipRegion] = 'CA'")
```

Si vous souhaitez simplement compter tous les enregistrements de  _domain_ sans définir de restrictions, utilisez la fonction **Count**.

La fonction  **Count** a été optimisée de manière à effectuer un calcul rapide des enregistrements dans les requêtes. Utilisez la fonction **Count** dans une expression de requête au lieu de la fonction **DCount** et spécifiez les éventuels critères pour imposer des restrictions sur les résultats. Utilisez la fonction **DCount** pour compter les enregistrements d'un domaine à partir d'un module ou d'une macro de code, ou dans un contrôle calculé.

La fonction  **DCount** vous permet de compter le nombre d'enregistrements contenant un champ particulier qui ne fait pas partie de la source d'enregistrements sur laquelle le formulaire ou l'état est fondé. Vous pouvez, par exemple, afficher le nombre de commandes de la table Commandes dans un contrôle calculé d'un formulaire fondé sur la table Produits.

La fonction  **DCount** ne compte pas les enregistrements qui contiennent des valeurs **Null** dans le champ désigné par _expr_, sauf si  _expr_ est le caractère générique astérisque (*). Dans ce cas, la fonction **DCount** calcule le nombre total d'enregistrements, y compris ceux qui contiennent des champs **Null**. L'exemple suivant calcule le nombre d'enregistrements dans la table Commandes :




```
intX = DCount("*", "Orders")
```

Si  _domain_ est une table possédant une clé primaire, vous pouvez également compter le nombre total d'enregistrements en choisissant pour l'argument _expr_ le champ de clé primaire, étant donné que ce champ ne peut pas contenir de valeur **Null**.

Si  _expr_ contient plusieurs noms de champs, séparez-les à l'aide d'un opérateur de concaténation, à savoir le signe &amp; ou le signe (+). Si le signe &amp; sépare les champs, la fonction **DCount** renvoie le nombre d'enregistrements contenant des données dans n'importe quel champ répertorié. Si c'est le signe +, la fonction **DCount** ne compte que le nombre d'enregistrements contenant des données dans l'ensemble des champs répertoriés. L'exemple suivant illustre l'effet des deux opérateurs avec un champ contenant des données dans tous les enregistrements (Destinataire) et un champ ne contenant pas de données (Région livraison).




```
intW = DCount("[ShipName]", "Orders") 
intX = DCount("[ShipRegion]", "Orders") 
intY = DCount("[ShipName] + [ShipRegion]", "Orders") 
intZ = DCount("[ShipName] &amp; [ShipRegion]", "Orders")
```


 **Remarque**  &amp; est l'opérateur préféré pour la concaténation de chaînes. Il est préférable d'utiliser l'opérateur d'addition uniquement pour les opérations numériques, sauf si vous voulez propager des valeurs de type  **Nulls** dans une expression.


## Exemple

La fonction suivante renvoie le nombre de commandes expédiées vers un pays ou une région déterminé après une date d'expédition spécifiée. Le domaine consiste en la table Commandes.


```
Public Function OrdersCount(ByVal strCountry As String, _ 
                            ByVal dteShipDate As Date) As Integer 
 
    OrdersCount = DCount("[ShippedDate]", "Orders", _ 
                  "[ShipCountry] = '" &amp; strCountry &amp; _ 
                  "' AND [ShippedDate] > #" &amp; dteShipDate &amp; "#") 
End Function 

```



Les exemples suivants montrent comment utiliser différents types de critères avec la fonction  **DCount**.

 **Exemple de code fourni par :**
![Icône de membre de la communauté](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) Communauté[UtterAccess](http://www.utteraccess.com)




```
    ' ***************************
    ' Typical Use
    ' Numerical values. Replace "number" with the number to use.
    variable = DCount("[FieldName]", "TableName", "[Criteria] = number")

    ' Strings.
    ' Numerical values. Replace "string" with the string to use.
    variable = DCount("[FieldName]", "TableName", "[Criteria]= 'string'")

    ' Dates. Replace "date" with the string to use.
    variable = DCount("[FieldName]", "TableName", "[Criteria]= #date#")
    ' ***************************

    ' ***************************
    ' Referring to a control on a form
    ' Numerical values
    variable = DCount("[FieldName]", "TableName", "[Criteria] = " &amp; Forms!FormName!ControlName)

    ' Strings
    variable = DCount("[FieldName]", "TableName", "[Criteria] = '" &amp; Forms!FormName!ControlName &amp; "'")

    ' Dates
    variable = DCount("[FieldName]", "TableName", "[Criteria] = #" &amp; Forms!FormName!ControlName &amp; "#")
    ' ***************************

    ' ***************************
    ' Combinations
    ' Multiple types of criteria
    variable = DCount("[FieldName]", "TableName", "[Criteria1] = " &amp; Forms![FormName]![Control1] _
             &amp; " AND [Criteria2] = '" &amp; Forms![FormName]![Control2] &amp; "'" _
            &amp; " AND [Criteria3] =#" &amp; Forms![FormName]![Control3] &amp; "#")
    
    ' Use two fields from a single record.
    variable = DCount("[LastName] &amp; ', ' &amp; [FirstName]", "tblPeople", "[PrimaryKey] = 7")
            
    ' Expressions
    variable = DCount("[Field1] + [Field2]", "tableName", "[PrimaryKey] = 7")
    
    ' Control Structures
    variable = DCount("IIf([LastName] Like 'Smith', 'True', 'False')", "tableName", "[PrimaryKey] = 7")
    ' ***************************
```


## À propos des collaborateurs
<a name="AboutContributors"> </a>

UtterAccess est le premier forum d'aide et wiki de Microsoft Access. Cliquez ici pour le rejoindre.


## Voir aussi
<a name="AboutContributors"> </a>


#### Concepts


[Objet Application](aefb0713-97e6-e2c7-e530-8fd2e1316a55.md)
#### Autres ressources


[Membres de l'objet Application](3ab5276c-d52a-72a9-244c-ec92ead48811.md)