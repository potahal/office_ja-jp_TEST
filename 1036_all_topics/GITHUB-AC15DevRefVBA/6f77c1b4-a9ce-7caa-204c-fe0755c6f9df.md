
# Report, objet (Access)

Un objet  **Report** fait référence à un rapport Microsoft Access particulier.


## Remarques

Un objet  **Report** est un membre de la collection **Reports**, qui est une collection de tous les rapports actuellement ouverts. Dans la collection **Reports**, l'indexation des rapports individuels commence à zéro. Vous pouvez faire référence à un objet **Report** individuel dans la collection **Reports** par son nom ou par son index dans la collection. Si le nom du rapport inclut un espace, il doit être entouré de crochets ([ ]).



|**Syntaxe**|**Exemple**|
|:-----|:-----|
|**Rapports** ! _reportname_|Reports!ÉtatCommande|
|**Rapports** ! [ _report name_]|Reports![État Commande]|
|**Rapports** (« _reportname_»)|Reports("ÉtatCommande")|
|**Rapports** ( _index_)|Reports(0)|

 **Remarque**  Chaque objet  **Report** possède une collection **Controls**, qui contient tous les contrôles du rapport. Vous pouvez faire référence au contrôle d'un rapport en faisant référence de manière implicite ou explicite à la collection **Controls**. Votre code sera plus rapide si vous faites référence à la collection **Controls** de manière implicite. Les exemples suivants illustrent deux façons de faire référence à un contrôle intitulé **NewData** sur le rapport appelé **OrderReport**.


```
' Implicit reference. 
Reports!OrderReport!NewData
```


```
' Explicit reference. 
Reports!OrderReport.Controls!NewData
```


## Exemple

L'exemple suivant montre comment utiliser l'événement  **NoData** d'un rapport pour empêcher le formulaire de rapport de s'ouvrir lorsqu'il n'y a aucune donnée à afficher.

 **Exemple de code fourni par :**[Référence du programmeur Microsoft Access 2010](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl)




```
Private Sub Report_NoData(Cancel As Integer)

    'Add code here that will be executed if no data
    'was returned by the Report's RecordSource
    MsgBox "No customers ordered this product this month. " &amp; _
        "The report will now close."
    Cancel = True

End Sub
```

L'exemple suivant montre comment utiliser l'événement  **Page** pour ajouter un filigrane à un rapport avant de l'imprimer.




```
Private Sub Report_Page()
    Dim strWatermarkText As String
    Dim sizeHor As Single
    Dim sizeVer As Single

#If RUN_PAGE_EVENT = True Then
    With Me
        '// Print page border
        Me.Line (0, 0)-(.ScaleWidth - 1, .ScaleHeight - 1), vbBlack, B
    
        '// Print watermark
        strWatermarkText = "Confidential"
        
        .ScaleMode = 3
        .FontName = "Segoe UI"
        .FontSize = 48
        .ForeColor = RGB(255, 0, 0)

        '// Calculate text metrics
        sizeHor = .TextWidth(strWatermarkText)
        sizeVer = .TextHeight(strWatermarkText)
        
        '// Set the print location
        .CurrentX = (.ScaleWidth / 2) - (sizeHor / 2)
        .CurrentY = (.ScaleHeight / 2) - (sizeVer / 2)
    
        '// Print the watermark
        .Print strWatermarkText
    End With
#End If

End Sub
```

L'exemple suivant montre comment définir la propriété  **BackColor** d'un contrôle en fonction de sa valeur.




```
Private Sub SetControlFormatting()
    If (Me.AvgOfRating >= 8) Then
        Me.AvgOfRating.BackColor = vbGreen
    ElseIf (Me.AvgOfRating >= 5) Then
        Me.AvgOfRating.BackColor = vbYellow
    Else
        Me.AvgOfRating.BackColor = vbRed
    End If
End Sub

Private Sub Detail_Format(Cancel As Integer, FormatCount As Integer)
    ' size the width of the rectangle
    Dim lngOffset As Long
    lngOffset = (Me.boxInside.Left - Me.boxOutside.Left) * 2
    Me.boxInside.Width = (Me.boxOutside.Width * (Me.AvgOfRating / 10)) - lngOffset
    
    ' do conditional formatting for the control in print preview
    SetControlFormatting
End Sub

Private Sub Detail_Paint()
    ' do conditional formatting for the control in report view
    SetControlFormatting
End Sub
```

L'exemple suivant montre comment mettre en forme un rapport pour afficher les barres de progression. L'exemple utilise une paire de contrôles du rectangle,  **boxInside** et **boxOutside**, pour créer une barre de progression basée sur la valeur de  **AvgOfRating**. Les barres de progression sont visibles uniquement lorsque le rapport est ouvert dans le mode  **Aperçu avant impression** ou lorsqu'il est imprimé.




```
Private Sub Report_Load()
    If (Me.CurrentView = AcCurrentView.acCurViewPreview) Then
        Me.boxInside.Visible = True
        Me.boxOutside.Visible = True
    Else
        Me.boxInside.Visible = False
        Me.boxOutside.Visible = False
    End If
End Sub

Private Sub Detail_Format(Cancel As Integer, FormatCount As Integer)
    ' size the width of the rectangle
    Dim lngOffset As Long
    lngOffset = (Me.boxInside.Left - Me.boxOutside.Left) * 2
    Me.boxInside.Width = (Me.boxOutside.Width * (Me.AvgOfRating / 10)) - lngOffset
    
    ' do conditional formatting for the control in print preview
    SetControlFormatting
End Sub
```


## À propos des collaborateurs
<a name="AboutContributors"> </a>

Wrox Press est guidé par la philosophie de programmeur à programmeur. Les livres Wrox sont écrits par des programmeurs pour des programmeurs et la marque Wrox est synonyme de solutions de référence aux problèmes de programmation concrets.


## Voir aussi
<a name="AboutContributors"> </a>


#### Autres ressources


[Membres de l'objet Report](73370a33-1ca0-da4d-9e36-88011bc2b93e.md)
[Référence du modèle d'objet Access](http://msdn.microsoft.com/library/2de134a4-6c5c-d2a3-8377-f4dd973ba650%28Office.15%29.aspx)