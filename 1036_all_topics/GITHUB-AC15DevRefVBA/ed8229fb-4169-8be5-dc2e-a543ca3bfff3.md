
# Form.Error, événement (Access)

L'événement Error est déclenché lorsqu'une erreur d'exécution se produit dans Microsoft Access alors qu'un formulaire est activé.


## Syntaxe

 _expression_. **Error**( ** _DataErr_**, ** _Response_** )

 _expression_ Variable qui représente un objet **Form**.


### Paramètres



|**Nom**|**Obligatoire/Facultatif**|**Type de données**|**Description**|
|:-----|:-----|:-----|:-----|
| _DataErr_|Obligatoire|**Entier**|Code d'erreur renvoyé par l'objet Err lorsqu'une erreur se produit. Vous pouvez utiliser l'argument DataErr avec la fonction Error pour associer le numéro au message d'erreur approprié.|
| _Response_|Obligatoire|**Entier**|La valeur de cet argument détermine si un message d'erreur s'affiche ou non. L'argument Réponse peut être l'une des constantes intrinsèques suivantes. 
<ul xmlns:xlink="http://www.w3.org/1999/xlink" xmlns:mtps="http://msdn2.microsoft.com/mtps" xmlns:MSHelp="http://msdn.microsoft.com/mshelp" xmlns:mshelp="http://msdn.microsoft.com/mshelp" xmlns:ddue="http://ddue.schemas.microsoft.com/authoring/2003/5" xmlns:msxsl="urn:schemas-microsoft-com:xslt"><li><p><b>acDataErrContinue</b>  Ignore l'erreur et continue sans afficher le message d'erreur par défaut de Microsoft Access. Vous pouvez fournir votre propre message d'erreur à la place de ce message d'erreur par défaut.  
  </p></li><li><p><b>acDataErrDisplay</b>  (valeur par défaut) Affiche le message d'erreur par défaut de Microsoft Access.</p></li></ul>|

## Remarques

Cela inclut les erreurs du moteur de base de données Microsoft Access, mais pas les erreurs d'exécution de Visual Basic ou d'ADO.

Pour exécuter une macro ou une procédure événementielle lorsque cet événement se produit, affectez le nom de la macro ou [Procédure événementielle] à la propriété  **OnError**.

L'exécution d'une procédure événementielle ou d'une macro associée à un événement Error vous permet d'intercepter un message d'erreur de Microsoft Access et de le remplacer par un message personnalisé mieux adapté à votre application.

 **Lien fourni par :**
![Icône de membre de la communauté](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) Communauté[UtterAccess](http://www.utteraccess.com)


- Erreurs
    

## Exemple

L'exemple suivant montre comment remplacer un message d'erreur par défaut par un message d'erreur personnalisé. Lorsque Microsoft Access renvoie un message d'erreur indiquant qu'il a détecté une clé en double (code d'erreur 3022), cette procédure événementielle affiche un message qui donne des informations plus spécifiques à l'application.

Pour essayer cet exemple, ajoutez la procédure événementielle suivante à un formulaire qui est fondé sur une table dotée d'un numéro d'identification d'employé unique comme clé de chaque enregistrement.




```
Private Sub Form_Error(DataErr As Integer, Response As Integer) 
    Const conDuplicateKey = 3022 
    Dim strMsg As String 
 
    If DataErr = conDuplicateKey Then 
        Response = acDataErrContinue 
        strMsg = "Each employee record must have a unique " _ 
            &amp; "employee ID number. Please recheck your data." 
        MsgBox strMsg 
    End If 
End Sub
```



L'exemple suivant indique comment vous pouvez remplacer un message d'erreur par défaut par un message d'erreur personnalisé.

 **Exemple de code fourni par :**
![Collaborateur MVP](images/odc_OfficeTA_33px_MVPContrib.jpg) Bill Jelen,[MrExcel.com](http://www.mrexcel.com/)




```
Private Sub Form_Error(DataErr As Integer, Response As Integer)
    Select Case DataErr
        Case 2113
            MsgBox "Only numbers are acceptable in this box", vbCritical, "Call 1-800-123-4567"
            Response = acDataErrContinue
        Case 2237
            MsgBox "You can only choose from the dropdown box"
            Response = acDataErrContinue
        Case 3022
            MsgBox "You entered a value that exists already in another record"
            Response = acDataErrContinue
            SSN.Value = SSN.OldValue
        Case 3314
            MsgBox "The DOH is required, so you cannot leave this field empty"
            Response = acDataErrContinue
        Case Else
            Response = acDataErrDisplay
    End Select
    ActiveControl.Undo
End Sub
```


## À propos des collaborateurs
<a name="AboutContributors"> </a>

UtterAccess est le premier forum d'aide et wiki de Microsoft Access. Cliquez ici pour le rejoindre.

Holy Macro! Books publie des livres divertissants pour les personnes qui utilisent Microsoft Office. Vous pouvez consulter le catalogue complet sur MrExcel.com.


## Voir aussi
<a name="AboutContributors"> </a>


#### Concepts


[Objet Form](72ef9219-142b-b690-b696-3eba9a5d4522.md)
#### Autres ressources


[Membres de l'objet Form](e1976b58-28ca-8f76-cdf3-6732cb06ce6c.md)