
# Propriété Form.OpenArgs (Access)

Détermine l'expression de chaîne spécifiée par l'argument  _OpenArgs_ de la méthode **OpenForm** ayant ouvert un formulaire. Type de données **Variant** en lecture-écriture.


## Syntaxe

 _expression_. **OpenArgs**

 _expression_ Variable qui représente un objet **Form**.


## Remarques

Vous ne pouvez utiliser cette propriété que dans une macro ou dans Visual Basic, avec la méthode  **OpenForm** de l'objet **DoCmd**. Le paramètre de cette propriété n'est accessible qu'en lecture, quel que soit le mode.

Pour utiliser la propriété  **OpenArgs**, ouvrez un formulaire à l'aide de la méthode **OpenForm** de l'objet **DoCmd** et définissez l'argument _OpenArgs_ sur l'expression de chaîne souhaitée. Le paramètre de propriété **OpenArgs** peut ensuite être utilisé dans du code pour le formulaire, comme dans une procédure d'événement **Open**. Vous pouvez également faire référence au paramètre de propriété dans une macro, comme une macro **Open**, ou une expression, comme une expression qui définit la propriété  **ControlSource** pour un contrôle sur le formulaire.

Supposons, par exemple, que le formulaire que vous ouvrez soit une liste de clients affichée en continu. Si vous souhaitez que l'enregistrement d'un client particulier devienne actif à l'ouverture du formulaire, vous pouvez définir le nom du client comme valeur de la propriété  **OpenArgs** et utiliser l'action **FindRecord** dans une macro Ouverture pour activer l'enregistrement dont vous avez spécifié le nom de client.


## Exemple

L'exemple suivant utilise la propriété  **OpenArgs** pour ouvrir le formulaire « Employés » au niveau d'un enregistrement d'employé spécifique. L'exemple montre également comment la méthode **OpenForm** définit la propriété **OpenArgs**. Vous pouvez exécuter cette procédure selon vos besoins ; par exemple, lors de la survenue d'un événement **AfterUpdate** pour une boîte de dialogue personnalisée permettant de saisir de nouvelles informations sur un employé.


```
Sub OpenToCallahan() 
    DoCmd.OpenForm "Employees", acNormal, , , acReadOnly, _ 
     , "Callahan" 
End Sub 
 
Sub Form_Open(Cancel As Integer) 
    Dim strEmployeeName As String 
    ' If OpenArgs property contains employee name, find 
    ' corresponding employee record and display it on form. For 
    ' example,if the OpenArgs property contains "Callahan", 
    ' move to first "Callahan" record. 
    strEmployeeName = Forms!Employees.OpenArgs 
    If Len(strEmployeeName) > 0 Then 
        DoCmd.GoToControl "LastName" 
        DoCmd.FindRecord strEmployeeName, , True, , True, , True 
    End If 
End Sub
```

L'exemple suivant montre comment utiliser la propriété  **OpenArgs** pour empêcher l'ouverture d'un formulaire à partir du volet de navigation.

 **Exemple de code fourni par :**[Référence du programmeur Microsoft Access 2010](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl)




```
Private Sub Form_Open(Cancel As Integer)

If Me.OpenArgs() <> "Valid User" Then
    MsgBox "You are not authorized to use this form!", _
        vbExclamation + vbOKOnly, "Invalid Access"
    Cancel = True
End If
End Sub
```


## À propos des collaborateurs
<a name="AboutContributors"> </a>

Wrox Press est guidé par la philosophie de programmeur à programmeur. Les livres Wrox sont écrits par des programmeurs pour des programmeurs et la marque Wrox est synonyme de solutions de référence aux problèmes de programmation concrets.


## Voir aussi
<a name="AboutContributors"> </a>


#### Concepts


[Objet Form](72ef9219-142b-b690-b696-3eba9a5d4522.md)
#### Autres ressources


[Membres de l'objet Form](e1976b58-28ca-8f76-cdf3-6732cb06ce6c.md)