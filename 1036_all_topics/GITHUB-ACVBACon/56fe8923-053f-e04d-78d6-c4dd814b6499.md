
# Créer un menu contextuel pour un formulaire, un contrôle de formulaire ou un rapport

 **Exemple de code fourni par :** Edwin Blancovitch,[Advanced Developers.net](http://advdev.net/)

Lors de la création d'un formulaire ou d'un rapport, vous pouvez fournir une méthode à l'utilisateur pour qu'il puisse facilement utiliser une commande qui s'applique uniquement au contexte actuel. Pour ce faire, créez un menu contextuel personnalisé et appliquez-le à un rapport ou un contrôle de formulaire. Le menu contextuel apparaît lorsque l'utilisateur clique avec le bouton droit sur l'objet auquel le menu contextuel est appliqué.

Dans les versions précédentes d' **Access**, vous pouviez utiliser la boîte de dialogue  **Personnaliser** pour créer des menus contextuels personnalisés. Dans Access 2013, vous devez utiliser un code Visual Basic pour Applications (VBA) pour créer un menu contextuel. Cet article décrit comment créer un menu contextuel avec VBA.

Pour créer un menu contextuel, vous devez d'abord créer un objet  **[CommandBar](http://msdn.microsoft.com/library/78603954-40aa-64cb-c407-2e0820d65231%28Office.15%29.aspx)**. L'objet **CommandBar** représente le menu contextuel. Ensuite, utilisez la méthode **[Add](http://msdn.microsoft.com/library/53e2b0b9-b11a-bf52-a1a3-523aae2c35d8%28Office.15%29.aspx)** pour créer des objets **[CommandBarControl](http://msdn.microsoft.com/library/b104ec00-beeb-a927-4b7b-108f4e3164f5%28Office.15%29.aspx)**. Chaque fois que vous créez un objet CommandBarControl, une commande est ajoutée au menu contextuel.
L'exemple suivant crée un menu contextuel nommé  **SimpleShortcutMenu** qui contient deux commandes, **Supprimer le filtre/tri** et **Filtrer par sélection**.

 **Remarque**  Pour utiliser les exemples suivants, vous devez définir une référence à la  **bibliothèque d'objets Microsoft Office 15.0**. Voir[Définir des références aux bibliothèques de types](6314a89b-89e9-d8c1-5964-889a361afcd1.md) pour plus d'informations sur la définition de références.




```
 
Sub CreateSimpleShortcutMenu() 
    Dim cmbShortcutMenu As Office.CommandBar 
     
    ' Create a shortcut menu named "SimpleShortcutMenu. 
    Set cmbShortcutMenu = CommandBars.Add("SimpleShortcutMenu", msoBarPopup, False, True) 
     
    ' Add the Remove Filter/Sort command. 
    cmbShortcutMenu.Controls.Add Type:=msoControlButton, Id:=605 
 
    ' Add the Filter By Selection command. 
    cmbShortcutMenu.Controls.Add Type:=msoControlButton, Id:=640 
     
    Set cmbShortcutMenu = Nothing 
     
End Sub
```

Une fois que vous avez exécuté le code, le menu contextuel est enregistré dans le cadre de la base de données. Vous n'avez pas besoin d'exécuter ce même code pour recréer le menu contextuel chaque fois que vous ouvrez la base de données.
Pour attribuer le menu contextuel à un formulaire, un contrôle de formulaire ou un rapport, définissez la propriété  **Menu contextuel** de l'objet sur **Oui** et définissez la propriété **Barre de menu contextuel** de l'objet sur le nom du menu contextuel. Pour cet exemple, définissez la propriété **Barre de menu contextuel** sur **SimpleShortcutMenu**.
L'exemple suivant crée un menu contextuel nommé  **cmdFormFiltering** qui contient des commandes pratiques à utiliser avec les formulaires continus. Dans cet exemple, la propriété **BeginGroup** est utilisée sur plusieurs contrôles pour les regrouper visuellement.



```
 
Sub CreateShortcutMenuWithGroups() 
    Dim cmbRightClick As Office.CommandBar 
 
 ' Create the shortcut menu. 
    Set cmbRightClick = CommandBars.Add("cmdFormFiltering", msoBarPopup, False, True) 
     
    With cmbRightClick 
        ' Add the Find command. 
        .Controls.Add msoControlButton, 141, , , True 
         
        ' Start a new grouping and add the Sort Ascending command. 
        .Controls.Add(msoControlButton, 210, , , True).BeginGroup = True 
         
        ' Add the Sort Descending command. 
        .Controls.Add msoControlButton, 211, , , True 
         
        ' Start a new grouping and add the Remove Filer/Sort command. 
        .Controls.Add(msoControlButton, 605, , , True).BeginGroup = True 
         
        ' Add the Filter by Selection command. 
        .Controls.Add msoControlButton, 640, , , True 
         
        ' Add the Filter Excluding Selection command. 
        .Controls.Add msoControlButton, 3017, , , True 
         
        ' Add the Between... command. 
        .Controls.Add msoControlButton, 10062, , , True 
    End With 
 
Set cmbRightClick = Nothing 
End Sub
```

L'exemple suivant crée un menu contextuel nommé  **cmdReportRightClick** qui contient des commandes pratiques à utiliser avec un rapport. Cet exemple indique comment modifier la propriété **Caption** de chaque contrôle à mesure qu'il est ajouté au menu contextuel.



```
 
Sub CreateReportShortcutMenu() 
    Dim cmbRightClick As Office.CommandBar 
    Dim cmbControl As Office.CommandBarControl 
 
   ' Create the shortcut menu. 
    Set cmbRightClick = CommandBars.Add("cmdReportRightClick", msoBarPopup, False, True) 
 
    With cmbRightClick 
         
        ' Add the Print command. 
        Set cmbControl = .Controls.Add(msoControlButton, 2521, , , True) 
        ' Change the caption displayed for the control. 
        cmbControl.Caption = "Quick Print" 
         
        ' Add the Print command. 
        Set cmbControl = .Controls.Add(msoControlButton, 15948, , , True) 
        ' Change the caption displayed for the control. 
        cmbControl.Caption = "Select Pages" 
         
        ' Add the Page Setup... command. 
        Set cmbControl = .Controls.Add(msoControlButton, 247, , , True) 
        ' Change the caption displayed for the control. 
        cmbControl.Caption = "Page Setup" 
         
        ' Add the Mail Recipient (as Attachment)... command. 
        Set cmbControl = .Controls.Add(msoControlButton, 2188, , , True) 
        ' Start a new group. 
        cmbControl.BeginGroup = True 
        ' Change the caption displayed for the control. 
        cmbControl.Caption = "Email Report as an Attachment" 
         
        ' Add the PDF or XPS command. 
        Set cmbControl = .Controls.Add(msoControlButton, 12499, , , True) 
        ' Change the caption displayed for the control. 
        cmbControl.Caption = "Save as PDF/XPS" 
         
        ' Add the Close command. 
        Set cmbControl = .Controls.Add(msoControlButton, 923, , , True) 
        ' Start a new group. 
        cmbControl.BeginGroup = True 
        ' Change the caption displayed for the control. 
        cmbControl.Caption = "Close Report" 
    End With 
     
    Set cmbControl = Nothing 
    Set cmbRightClick = Nothing 
End Sub
```


## À propos des collaborateurs
<a name="AboutContributors"> </a>

Edwin Blancovitch est président du site [Advanced Developers.net](http://advdev.net/), créateur d'[Easy Payroll](http://www.easypayroll.net/), un package logiciel permettant de gérer les besoins liés aux ressources humaines, aux paies, à la planification, aux horaires et à la présence.

