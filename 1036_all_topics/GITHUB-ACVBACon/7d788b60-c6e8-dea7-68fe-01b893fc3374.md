
# Se déplacer dans un jeu d'enregistrements DAO

Un objet  **[Recordset](http://msdn.microsoft.com/library/9774232C-E6DA-175B-FC7F-ED2AB7908FA0%28Office.15%29.aspx)** a généralement une position actuelle, la plupart du temps dans un enregistrement. Lorsque vous faites référence aux champs d'un **jeu d'enregistrements**, vous obtenez des valeurs de l'enregistrement à la position actuelle, réputée comme l'enregistrement actuel. Cependant, la position actuelle peut également se situer juste avant le premier enregistrement d'un **jeu d'enregistrements** ou juste après le dernier enregistrement. Dans certaines circonstances, la position actuelle n'est pas définie.

Vous pouvez utiliser les méthodes  **Move** suivantes pour parcourir les enregistrements d'un **jeu d'enregistrements**:

- La méthode  **[MoveFirst](http://msdn.microsoft.com/library/338F7E86-6997-B80A-FC7A-A395D10B4A62%28Office.15%29.aspx)** passe au premier enregistrement.
    
- La méthode  **[MoveLast](http://msdn.microsoft.com/library/FC0F7A33-1F55-9F5B-B00D-1B81F49B1C3E%28Office.15%29.aspx)** passe au dernier enregistrement.
    
- La méthode  **[MoveNext](http://msdn.microsoft.com/library/0A1315CF-92F8-B8EF-1542-081E8C2D5BE0%28Office.15%29.aspx)** passe à l'enregistrement suivant.
    
- La méthode  **[MovePrevious](http://msdn.microsoft.com/library/82A3BC3E-5221-9A1A-1350-47BC6759EDEB%28Office.15%29.aspx)** passe à l'enregistrement précédent.
    
- La méthode  **[Move](http://msdn.microsoft.com/library/21CA5AB5-FF71-1AE8-21B3-8991D5F795CF%28Office.15%29.aspx)** avance ou recule du nombre d'enregistrements spécifié dans sa syntaxe.
    
Vous pouvez utiliser chacune de ces méthodes sur des objets  **Recordset** de type Table, Feuille de réponse dynamique et Capture instantanée. Pour un objet **Recordset** de type Avant uniquement, vous pouvez seulement utiliser les méthodes **MoveNext** et **Move**. Si vous utilisez la méthode **Move** sur un objet **Recordset** de type Avant uniquement, l'argument qui spécifie le nombre de lignes doit être un nombre entier positif.
L'exemple de code suivant ouvre un objet  **Recordset** dans la table Employés qui contient tous les enregistrements ayant une valeur **Null** dans le champ SupHiérarchique. La fonction met ensuite à jour les enregistrements pour indiquer que ces employés sont des intérimaires. Pour chaque enregistrement du **jeu d'enregistrements**, l'exemple modifie les champs Titre et Notes et enregistre les modifications à l'aide de la méthode **[Update](http://msdn.microsoft.com/library/AAD4171A-DA95-ED72-86B3-714615EA0AC8%28Office.15%29.aspx)**. Il utilise la méthode **MoveNext** pour passer à l'enregistrement suivant.



```
Sub UpdateEmployees() 
 
Dim dbsNorthwind As DAO.Database 
Dim rstEmployees As DAO.Recordset 
Dim strSQL As String 
Dim intI As Integer 
 
On Error GoTo ErrorHandler 
 
   Set dbsNorthwind = CurrentDb 
 
   ' Open a recordset on all records from the Employees table that have 
   ' a Null value in the ReportsTo field. 
   strSQL = "SELECT * FROM Employees WHERE ReportsTo IS NULL" 
   Set rstEmployees = dbsNorthwind.OpenRecordset(strSQL, dbOpenDynaset) 
 
   ' If the recordset is empty, exit. 
   If rstEmployees.EOF Then Exit Sub 
 
   intI = 1 
   With rstEmployees 
      Do Until .EOF 
         .Edit 
         ![ReportsTo] = 5 
         ![Title] = "Temporary" 
         ![Notes] = rstEmployees![Notes] &amp; "Temp #" &amp; intI 
         .Update 
         .MoveNext 
         intI = intI + 1 
      Loop 
   End With 
 
   RstEmployees.Close 
   dbsNorthwind.Close 
 
   Set rstEmployees = Nothing 
   Set dbsNorthwind = Nothing 
 
   Exit Sub 
 
ErrorHandler: 
   MsgBox "Error #: " &amp; Err.Number &amp; vbCrLf &amp; vbCrLf &amp; Err.Description 
End Sub 

```


 **Remarque**  L'exemple ci-dessus a uniquement pour but d'illustrer les méthodes  **Update** et **MoveNext**. Pour bénéficier de performances optimales, nous vous conseillons d'effectuer cette opération en bloc avec une requête SQL UPDATE.

