
# Calculer le temps écoulé

Cette rubrique explique comment Access stocke le type de données Date/Heure et pourquoi vous pouvez obtenir des résultats inattendus lorsque vous comparez des dates et des heures.


## Stockage des données Date/Heure

Access stocke les données de type Date/Heure sous forme de nombre de double précision à virgule flottante (jusqu'à 15 décimales). La partie entière du nombre de double précision représente la date, la partie décimale, quant à elle, représente l'heure.

Les valeurs correctes sont comprises entre -647,434 (1er janvier de l'an 100) et 2,958,465 (31 décembre de l'an 9999). La valeur de date 0 représente le 30 décembre 1899. Access stocke les dates antérieures au 30 décembre 1899 en tant que nombres négatifs.

Les valeurs d'heure correctes sont comprises entre .0 (00:00:00) et .99999 (23:59:59). La valeur numérique représente une fraction d'une journée. Vous pouvez convertir la valeur numérique en heures, minutes et secondes en multipliant la valeur numérique par 24.


||||||
|:-----|:-----|:-----|:-----|:-----|
|**Nombre double**|**Partie relative à la date**|**Date réelle**|**Partie relative à l'heure**|**Heure réelle**|
|1,0|1|31 décembre 1899|0,0|00:00:00.|
|2,5|2|1er janvier 1900|0,5|12:00:00.|
|27468,96875|27468|15 mars 1975|0,96875|23:15:00.|
|33914,125|33914|6 novembre 1992|||

## Calcul des données d'heure

Étant donné qu'une valeur d'heure est stockée en tant que fraction d'une journée de 24 heures, vous pouvez obtenir des résultats incorrects de mise en forme lorsque vous calculez des intervalles de temps supérieurs à 24 heures. Pour éviter ce problème, vous pouvez créer votre propre fonction pour que les intervalles de temps soient correctement mis en forme.

La procédure suivante montre comment utiliser la fonction  **Format** pour mettre en forme les intervalles de temps. La procédure accepte deux valeurs d'heure et affiche, dans différents formats, l'intervalle entre elles dans la fenêtre Exécution.




```
Function ElapsedTime(endTime As Date, startTime As Date) 
    Dim strOutput As String 
    Dim Interval As Date 
     
    ' Calculate the time interval. 
    Interval = endTime - startTime 
  
    ' Format and print the time interval in seconds. 
    strOutput = Int(CSng(Interval * 24 * 3600)) &amp; " Seconds" 
    Debug.Print strOutput 
         
    ' Format and print the time interval in minutes and seconds. 
    strOutput = Int(CSng(Interval * 24 * 60)) &amp; ":" &amp; Format(Interval, "ss") _ 
        &amp; " Minutes:Seconds" 
    Debug.Print strOutput 
     
    ' Format and print the time interval in hours, minutes and seconds. 
    strOutput = Int(CSng(Interval * 24)) &amp; ":" &amp; Format(Interval, "nn:ss") _ 
           &amp; " Hours:Minutes:Seconds" 
    Debug.Print strOutput 
         
    ' Format and print the time interval in days, hours, minutes and seconds. 
    strOutput = Int(CSng(Interval)) &amp; " days " &amp; Format(Interval, "hh") _ 
        &amp; " Hours " &amp; Format(Interval, "nn") &amp; " Minutes " &amp; _ 
        Format(Interval, "ss") &amp; " Seconds" 
    Debug.Print strOutput 
 
End Function
```


## Comparaison des données de date

Étant donné que les dates et les heures sont stockées ensemble en tant que nombres de double précision, il peut arriver que vous obteniez des résultats inattendus lorsque vous comparez des données Date/Heure. Par exemple, si vous tapez l'expression suivante dans la fenêtre Exécution, vous obtenez un résultat  **faux** (0) même si la date du jour est 11/07/2006 :


```
? Now()=DateValue("7/11/2006")
```

La fonction  **[Now](http://msdn.microsoft.com/library/8F324994-2518-0C83-76C7-22CD67033B36%28Office.15%29.aspx)** renvoie un nombre de double précision représentant la date et l'heure actuelles. Cependant, la fonction **[DateValue](http://msdn.microsoft.com/library/8C9BD3D6-1614-EEB0-0714-4730EEEB1B95%28Office.15%29.aspx)** renvoie un nombre entier représentant la date mais pas une valeur d'heure fractionnelle. Par conséquent, la fonction **Now** ne produit une valeur égale à celle de la fonction **DateValue** que si **Now** renvoie 00:00:00 (12:00:00 A.M.).

Pour obtenir des résultats précis lorsque vous comparez des valeurs de date, utilisez une des fonctions suivantes. Pour tester chaque fonction, tapez celle-ci dans la fenêtre Exécution, remplacez 11/07/2006 par la date actuelle, puis appuyez sur Entrée :

Pour obtenir une valeur entière, utilisez la fonction  **[Date](http://msdn.microsoft.com/library/8AFD02C8-C5B5-F8F3-FF8E-9A2AC0EA94B9%28Office.15%29.aspx)**:




```
?Date()=DateValue("7/11/2006")
```

Pour supprimer la partie fractionnelle de la fonction  **Now**, utilisez la fonction **[Int](http://msdn.microsoft.com/library/32CE40AC-FDF8-BD6D-E7F9-154C480A9602%28Office.15%29.aspx)**:




```
?Int(Now())=DateValue("7/11/2006")
```


## Comparaison de données d'heure

Lorsque vous comparez des valeurs d'heure, il peut arriver que vous obteniez des résultats incohérents, car une valeur d'heure est stockée en tant que partie fractionnelle d'un nombre de double précision à virgule flottante. Par exemple, si vous tapez l'expression suivante dans la fenêtre Exécution, vous obtenez un résultat  **faux** (0) même si les deux valeurs d'heure semblent identiques :


```
var1 = #2:01:00 PM# 
var2 = DateAdd("n", 10, var1) 
? var2 = #2:11:00 PM# 
```

Lorsqu'Access convertit une valeur d'heure en fraction, le résultat calculé peut être l'équivalent exact de la valeur d'heure. La petite différence produite par le calcul est suffisante pour produire un résultat  **faux** (0) lorsque vous comparez une valeur stockée à une valeur constante.

Pour obtenir des résultats précis lorsque vous comparez des valeurs d'heure, utilisez une des méthodes suivantes. Pour tester chaque méthode, tapez celle-ci dans la fenêtre Exécution, puis appuyez sur Entrée :

Ajoutez une date associée à la comparaison d'heures :




```
var1 = #7/11/2006 2:00:00 PM# 
var2 = DateAdd("n", 10, var1) 
? var2 = #7/11/2006 2:10:00 PM#
```

Convertissez les valeurs d'heure en données de type  **String** avant de les comparer :




```
var1 = #2:00:00 PM# 
var2 = DateAdd("n", 10, var1) 
? CStr(var2) = CStr(#2:10:00 PM#)
```

Utilisez la fonction  **[DateDiff](http://msdn.microsoft.com/library/15C9DF5F-1403-B6A5-71B9-611E9820D804%28Office.15%29.aspx)** pour comparer des unités précises comme les secondes :




```
var1 = #2:00:00 PM# 
var2 = DateAdd("n", 10, var1) 
? DateDiff("s", var2, #2:10:00 PM#) = 0
```

 **Lien fourni par :**
![Icône de membre de la communauté](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) Communauté[UtterAccess](http://www.utteraccess.com)


- Page relative à [la somme du temps écoulé pouvant dépasser 24 heures](http://www.utteraccess.com/wiki/index.php/Summing_elapsed_time_that_could_go_over_24_hours)
    

## À propos des collaborateurs
<a name="AboutContributors"> </a>

UtterAccess est le premier forum d'aide et wiki de Microsoft Access. Cliquez ici pour le rejoindre.

