
# Déterminer la fin d'un processus Shell

Lorsque vous exécutez la fonction  **[Shell](http://msdn.microsoft.com/library/033bffb0-540f-2c17-2aed-d25d10bedd8c%28Office.15%29.aspx)** dans une procédure Visual Basic pour Applications (VBA), elle lance un programme exécutable de manière asynchrone et renvoie le contrôle à la procédure. Ce programme Shell continue de s'exécuter indépendamment de votre procédure jusqu'à ce que vous la fermiez.

Si votre procédure doit attendre la fin du processus Shell, vous pouvez utiliser l'API de Windows pour chercher le statut de l'application, mais cela n'est pas très efficace. Cette rubrique vous explique une méthode plus efficace.

L'API de Windows intègre une fonctionnalité qui permet à votre application d'attendre la fin d'un processus Shell. Pour utiliser ces fonctions, vous devez disposer d'une poignée sur le processus Shell. Pour cela, utilisez la fonction  **CreateProcess** au lieu de la fonction **Shell** pour commencer votre programme Shell.


## Création du processus Shell

Pour créer un processus adressable, utilisez la fonction  **CreateProcess** pour démarrer votre application Shell. La fonction **CreateProcess** donne à votre programme le handle du processus Shell via un de ses paramètres transmis.


## Attente de la fin du processus Shell

Vous pouvez transmettre cette poignée à la fonction  **WaitForSingleObject** après avoir utilisé la fonction **CreateProcess** pour obtenir un descripteur de processus. Votre procédure VBA suspend alors son exécution jusqu'à la fin du processus Shell.

Les étapes suivantes permettent de créer une procédure VBA qui utilise la fonction  **CreateProcess** pour exécuter le Bloc-notes de Windows. Ce code montre comment utiliser les fonctions **CreateProcess** et **WaitForSingleObject** de l'API de Windows pour attendre la fin d'un processus Shell avant de reprendre l'exécution.

La syntaxe de la fonction  **CreateProcess** est complexe. Dans l'exemple de code, elle est donc encapsulée dans une fonction appelée **ExecCmd**. **ExecCmd** comprend un paramètre, la ligne de commande de l'application à exécuter.

Créez un module standard et collez les lignes suivantes dans la section Déclarations :




```
Option Explicit 
 
Private Type STARTUPINFO 
 cb As Long 
 lpReserved As String 
 lpDesktop As String 
 lpTitle As String 
 dwX As Long 
 dwY As Long 
 dwXSize As Long 
 dwYSize As Long 
 dwXCountChars As Long 
 dwYCountChars As Long 
 dwFillAttribute As Long 
 dwFlags As Long 
 wShowWindow As Integer 
 cbReserved2 As Integer 
 lpReserved2 As Long 
 hStdInput As Long 
 hStdOutput As Long 
 hStdError As Long 
End Type 
 
Private Type PROCESS_INFORMATION 
 hProcess As Long 
 hThread As Long 
 dwProcessID As Long 
 dwThreadID As Long 
End Type 
 
Private Declare Function WaitForSingleObject Lib "kernel32" (ByVal _ 
 hHandle As Long, ByVal dwMilliseconds As Long) As Long 
 
Private Declare Function CreateProcessA Lib "kernel32" (ByVal _ 
 lpApplicationName As Long, ByVal lpCommandLine As String, ByVal _ 
 lpProcessAttributes As Long, ByVal lpThreadAttributes As Long, _ 
 ByVal bInheritHandles As Long, ByVal dwCreationFlags As Long, _ 
 ByVal lpEnvironment As Long, ByVal lpCurrentDirectory As Long, _ 
 lpStartupInfo As STARTUPINFO, lpProcessInformation As _ 
 PROCESS_INFORMATION) As Long 
 
Private Declare Function CloseHandle Lib "kernel32" (ByVal _ 
 hObject As Long) As Long 
 
Private Const NORMAL_PRIORITY_CLASS = &amp;H20&amp; 
Private Const INFINITE = -1&amp; 

```

Collez le code suivant dans le module :




```
Public Sub ExecCmd(cmdline As String) 
 Dim proc As PROCESS_INFORMATION 
 Dim start As STARTUPINFO 
 Dim ReturnValue As Integer 
 
 ' Initialize the STARTUPINFO structure: 
 start.cb = Len(start) 
 
 ' Start the shelled application: 
 ReturnValue = CreateProcessA(0&amp;, cmdline$, 0&amp;, 0&amp;, 1&amp;, _ 
 NORMAL_PRIORITY_CLASS, 0&amp;, 0&amp;, start, proc) 
 
 ' Wait for the shelled application to finish: 
 Do 
 ReturnValue = WaitForSingleObject(proc.hProcess, 0) 
 DoEvents 
 Loop Until ReturnValue <> 258 
 
 ReturnValue = CloseHandle(proc.hProcess) 
End Sub 
 
Sub Testing() 
 ExecCmd "NOTEPAD.EXE" 
 MsgBox "Process Finished" 
End Sub
```

Tapez  **Tests** dans la fenêtre **Exécution**. Le Bloc-notes démarre. Lorsque vous fermez le Bloc-notes, un message vous avertit que le processus est terminé.

