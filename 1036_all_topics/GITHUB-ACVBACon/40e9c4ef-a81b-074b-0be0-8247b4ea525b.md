
# Détection de la durée d'inactivité ou de l'inactivité d'un utilisateur

Cette rubrique explique comment créer une procédure qui s'exécutera lorsque l'application Access ne détecte aucune entrée utilisateur pendant une certaine période. Il s'agit notamment de créer un formulaire masqué,  **DetectIdleTime**, qui suit les périodes d'inactivité.

Suivez les étapes ci-après pour créer le formulaire  **DetectIdleTime**.

1. Créez un formulaire vierge qui ne se base pas sur une table ou une requête et nommez-la  **DetectIdleTime**.
    
2. Définissez les propriétés de formulaire suivantes :
    
     **Remarque**  Le paramètre  **IntervalleMinuterie** indique à quelle fréquence (en millisecondes) l'application vérifie l'inactivité de l'utilisateur. La valeur 1000 correspond à 1 seconde.


|**Propriété**|**Valeur**|
|:-----|:-----|
|OnTimer|[Procédure événementielle]|
|TimerInterval|1000|
3. Entrez le code suivant pour la procédure événementielle de propriété  **OnTimer**:
    
  ```
        Sub Form_Timer() 
         ' IDLEMINUTES determines how much idle time to wait for before 
         ' running the IdleTimeDetected subroutine. 
         Const IDLEMINUTES = 5 
 
         Static PrevControlName As String 
         Static PrevFormName As String 
         Static ExpiredTime 
 
         Dim ActiveFormName As String 
         Dim ActiveControlName As String 
         Dim ExpiredMinutes 
 
         On Error Resume Next 
 
         ' Get the active form and control name. 
 
         ActiveFormName = Screen.ActiveForm.Name 
         If Err Then 
            ActiveFormName = "No Active Form" 
            Err = 0 
         End If 
 
         ActiveControlName = Screen.ActiveControl.Name 
            If Err Then 
            ActiveControlName = "No Active Control" 
            Err = 0 
         End If 
 
         ' Record the current active names and reset ExpiredTime if: 
         '    1. They have not been recorded yet (code is running 
         '       for the first time). 
         '    2. The previous names are different than the current ones 
         '       (the user has done something different during the timer 
         '        interval). 
         If (PrevControlName = "") Or (PrevFormName = "") _ 
           Or (ActiveFormName <> PrevFormName) _ 
           Or (ActiveControlName <> PrevControlName) Then 
            PrevControlName = ActiveControlName 
            PrevFormName = ActiveFormName 
            ExpiredTime = 0 
         Else 
            ' ...otherwise the user was idle during the time interval, so 
            ' increment the total expired time. 
            ExpiredTime = ExpiredTime + Me.TimerInterval 
         End If 
 
         ' Does the total expired time exceed the IDLEMINUTES? 
         ExpiredMinutes = (ExpiredTime / 1000) / 60 
         If ExpiredMinutes >= IDLEMINUTES Then 
            ' ...if so, then reset the expired time to zero... 
            ExpiredTime = 0 
            ' ...and call the IdleTimeDetected subroutine. 
            IdleTimeDetected ExpiredMinutes 
         End If 
      End Sub
  ```

4. Créez la procédure suivante dans le module de formulaire :
    
  ```
        Sub IdleTimeDetected(ExpiredMinutes) 
         Dim Msg As String 
         Msg = "No user activity detected in the last " 
         Msg = Msg &amp; ExpiredMinutes &amp; " minute(s)!" 
         MsgBox Msg, 48 
      End Sub
  ```

Pour masquer le formulaire  **DetectIdleTime** lors de son ouverture, attribuez à l'argument _ModeFenêtre_ de la méthode **[OpenForm](A1C9D3A9-2AF8-C30A-ACB0-6428C70DCDB0.md)** la valeur **acHidden**.
