
# MailMerge.OpenDataSource Method (Word)

Attaches a data source to the specified document, which becomes a main document if it is not one already.


## Syntax

 _表达式_. **OpenDataSource**( ** _Name_**, ** _Format_**, ** _ConfirmConversions_**, ** _ReadOnly_**, ** _LinkToSource_**, ** _AddToRecentFiles_**, ** _PasswordDocument_**, ** _PasswordTemplate_**, ** _Revert_**, ** _WritePasswordDocument_**, ** _WritePasswordTemplate_**, ** _Connection_**, ** _SQLStatement_**, ** _SQLStatement1_**, ** _OpenExclusive_**, ** _SubType_** )

 _表达式_ Required. A variable that represents a **[MailMerge](b228c4d6-9ca7-8795-12f6-d32e62844a83.md)** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
| _Name_|必需|**String**|The data source file name. You can specify a Microsoft Query (.qry) file instead of specifying a data source, a connection string, and a query string.|
| _Format_|可选|**Variant**|The file converter used to open the document. Can be one of the  **WdOpenFormat** constants. To specify an external file format, use the **OpenFormat** property with the **FileConverter** object to determine the value to use with this argument.|
| _ConfirmConversions_|可选|**Variant**|**True** to display the **Convert File** dialog box if the file is not in Microsoft Word format.|
| _ReadOnly_|可选|**Variant**|**True** to open the data source on a read-only basis.|
| _LinkToSource_|可选|**Variant**|**True** to perform the query specified by Connection and SQLStatement each time the main document is opened.|
| _AddToRecentFiles_|可选|**Variant**|**True** to add the file name to the list of recently used files at the bottom of the **File** menu.|
| _PasswordDocument_|可选|**Variant**|The password used to open the data source. (See Remarks below.)|
| _PasswordTemplate_|可选|**Variant**|The password used to open the template. (See Remarks below.)|
| _Revert_|可选|**Variant**|Controls what happens if Name is the file name of an open document.  **True** to discard any unsaved changes to the open document and reopen the file; **False** to activate the open document.|
| _WritePasswordDocument_|可选|**Variant**|The password used to save changes to the document. (See Remarks below.)|
| _WritePasswordTemplate_|可选|**Variant**|The password used to save changes to the template. (See Remarks below.)|
| _Connection_|可选|**Variant**|A range within which the query specified by SQLStatement is to be performed. (See Remarks below.) |
| _SQLStatement_|可选|**Variant**|Defines query options for retrieving data. (See Remarks below.)|
| _SQLStatement1_|可选|**Variant**|If the query string is longer than 255 characters, SQLStatement specifies the first portion of the string, and SQLStatement1 specifies the second portion. (See Remarks below.)|
| _OpenExclusive_|可选|**Variant**|**True** to open exclusively.|
| _SubType_|可选|**Variant**|Can be one of the  **WdMergeSubType** constants.|

## Remarks

To determine the ODBC connection and query strings, set query options manually and use the  **QueryString** property to return the connection string. The following table includes some commonly used SQL keywords.



|**Keyword**|**Description**|
|:-----|:-----|
|DSN|The name of the ODBC data source|
|UID|The user logon ID|
|PWD|The user-specified password|
|DBQ|The database file name|
|FIL|The file type|
To instruct Word to use the same connection method as in earlier versions of Word (Dynamic Data Exchange (DDE) for Microsoft Office Access and Microsoft Office Excel data sources) use  `SubType:=wdMergeSubTypeWord2000`.

How you specify the range depends on how data is retrieved. For example:


- When retrieving data through Open Database Connectivity (ODBC), you specify a connection string.
    
- When retrieving data from Excel using dynamic data exchange (DDE), you specify a named range.
 **安全注释**  


    
- When retrieving data from Access, you specify the word "Table" or "Query" followed by the name of a table or query.
    

 **安全注释**  




 **安全注释**  



尽量避免在应用程序中使用硬编码的密码。 如果过程中需要使用密码，请向用户请求密码并将其存储在一个变量中，然后在代码中使用该变量。有关执行此操作的建议最佳实践，请参阅 [Microsoft Office 解决方案开发人员安全注意事项](HV01051489.md)。


## Example

This example creates a new main document and attaches the Orders table from an Access database named "Northwind.mdb."


```
Dim docNew As Document 
 
Set docNew = Documents.Add 
 
With docNew.MailMerge 
 .MainDocumentType = wdFormLetters 
 .OpenDataSource _ 
 Name:="C:\Program Files\Microsoft Office" &amp; _ 
 "\Office\Samples\Northwind.mdb", _ 
 LinkToSource:=True, AddToRecentFiles:=False, _ 
 Connection:="TABLE Orders" 
End With
```

This example creates a new main document and attaches the Excel worksheet named ?Names.xls.? The Connection argument retrieves data from the range named "Sales."




```
Dim docNew As Document 
 
Set docNew = Documents.Add 
 
With docNew.MailMerge 
 .MainDocumentType = wdCatalog 
 .OpenDataSource Name:="C:\Documents\Names.xls", _ 
 ReadOnly:=True, _ 
 Connection:="Sales" 
End With
```

This example uses ODBC to attach the Access database named "Northwind.mdb" to the active document. The SQLStatement argument selects the records in the Customers table.




```
Dim strConnection As String 
 
With ActiveDocument.MailMerge 
 .MainDocumentType = wdFormLetters 
 strConnection = "DSN=MS Access Databases;" _ 
 &amp; "DBQ=C:\Northwind.mdb;" _ 
 &amp; "FIL=RedISAM;" 
 .OpenDataSource Name:="C:\NorthWind.mdb", _ 
 Connection:=strConnection, _ 
 SQLStatement:="SELECT * FROM Customers" 
End With
```


## 另请参阅


#### 概念


[MailMerge Object](b228c4d6-9ca7-8795-12f6-d32e62844a83.md)
#### 其他资源


[MailMerge Object Members](http://msdn.microsoft.com/library/b4db0f00-0f03-4162-7312-b3aa417bea03%28Office.15%29.aspx)