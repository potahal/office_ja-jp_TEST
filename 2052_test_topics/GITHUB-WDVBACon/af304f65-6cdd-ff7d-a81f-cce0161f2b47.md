
# 处理 Document 对象

在 Visual Basic 中为修改文件的方法是 **[文档](8d83487a-2345-a036-a916-971c9db5b7fb.md)** 对象或 **[文档](fc4ac973-19c1-703a-5538-f4426b8b7564.md)** 集合的方法。本主题包含 Visual Basic 示例，与以下任务:


- [新建文档](#Creating)
    
- [打开文档](#Opening)
    
- [保存现有文档](#Saving1)
    
- [保存新文档](#Saving2)
    
- [激活文档](#Activating)
    
- [确定文档是否已打开](#Determining)
    
- [引用活动文档](#Referring)
    

## 新建文档

 **文档** 集合包括所有打开的文档。若要创建一个新文档，使用 **[Add](04b81417-cde9-4657-7737-90d266d05487.md)** 方法向 **文档** 集合中添加一个 **文档** 对象。下面的指令创建一个文档。


```
Documents.Add
```

创建文档的更好方法是将返回值赋给一个对象变量。 **Add** 方法返回一个引用新文档的 **Document** 对象。在下面的示例中，将 **Add** 方法返回的 **Document** 对象赋给一个对象变量。然后设置该 **Document** 对象的部分属性和方法。使用对象变量可以很容易地控制新文档。




```
Sub NewSampleDoc() 
    Dim docNew As Document 
    Set docNew = Documents.Add 
    With docNew 
        .Content.Font.Name = "Tahoma" 
        .SaveAs FileName:="Sample.doc" 
    End With 
End Sub
```


## 打开文档

若要打开一个现有文档，请使用 **文档** 集合使用 **[Open](9e61e9d5-58d1-833a-5f93-b87299deb400.md)** 方法。下面的指令将打开文档 sample.doc myfolder 文件夹文件夹中。


```
Sub OpenDocument() 
    Documents.Open FileName:="C:\MyFolder\Sample.doc" 
End Sub
```


## 保存现有文档

若要保存一个文档，使用 **文档** 对象中的 **[Save](7e329abc-0530-7016-7712-687de2c780a8.md)** 方法。下面的指令将命名为 Sales.doc 文档保存。


```
Sub SaveDocument() 
    Documents("Sales.doc").Save 
End Sub
```

通过对  **Documents** 集合应用 **Save** 方法，可以保存所有打开的文档。下列指令保存所有打开的文档。




```
Sub SaveAllOpenDocuments() 
    Documents.Save 
End Sub
```


## 保存新文档

若要保存单个文档，对 **文档** 对象使用 **[SaveAs2](aa491007-0e31-26f5-3a5e-477381529b6e.md)** 方法。下面的指令将活动文档另存为当前文件夹中的"Temp.doc"。


```
Sub SaveNewDocument() 
    ActiveDocument.SaveAs FileName:="Temp.doc" 
End Sub
```

 ** _FileName_** 参数可以仅包含文件名，也可以包含完整的路径（例如，"C:\Documents\Temporary File.doc"）。


## 关闭文档

要关闭单个文档，对 **文档** 对象使用 **[Close](59603a58-17ee-bc65-597b-6200e8be9fbc.md)** 方法。下面的指令关闭并保存名为 Sales.doc 文档。


```
Sub CloseDocument() 
    Documents("Sales.doc").Close SaveChanges:=wdSaveChanges 
End Sub
```

通过应用  **Documents** 集合的 **[Close](0284daf3-311e-97c9-ffc6-74d63b85fdcd.md)** 方法可关闭所有打开的文档。下面的指令在不保存更改的情况下关闭所有的文档。




```
Sub CloseAllDocuments() 
    Documents.Close SaveChanges:=wdDoNotSaveChanges 
End Sub
```

下面的示例在每个文档关闭之前提示用户保存文档。




```
Sub PromptToSaveAndClose() 
    Dim doc As Document 
    For Each doc In Documents 
        doc.Close SaveChanges:=wdPromptToSaveChanges 
    Next 
End Sub
```


## 激活文档

若要更改活动文档，可对  **Document** 对象使用 **[Activate](83cc5935-020b-470a-f7aa-7fea057ec08b.md)** 方法。下面的指令激活已打开的名为 Sales.doc 的文档。


```
Sub ActivateDocument() 
    Documents("Sales.doc").Activate 
End Sub
```


## 确定文档是否已打开

若要确定文档是否处于打开状态，可使用  **For Each...Next** 语句枚举 **Documents** 集合中的元素。在下面的示例中，如果文档 Sample.doc 是打开的，将激活该文档，如果该文档当前尚未打开，则将打开该文档。


```
Sub ActivateOrOpenDocument() 
    Dim doc As Document 
    Dim docFound As Boolean 
 
    For Each doc In Documents 
        If InStr(1, doc.Name, "sample.doc", 1) Then 
            doc.Activate 
            docFound = True 
            Exit For 
        Else 
            docFound = False 
        End If 
    Next doc 
 
    If docFound = False Then Documents.Open FileName:="Sample.doc" 
End Sub
```


## 引用活动文档

而不是按名称或索引号引用文档   例如，  `Documents("Sales.doc")`  **[ActiveDocument](c20a7c9f-f8a4-7913-f53f-10baa6807def.md)** 属性返回一个 **Document** 对象，该对象引用活动文档 (该文档具有焦点)。下面的示例显示活动文档的名称，或者如果没有打开的文档，则会显示一条消息。


```
Sub ActiveDocumentName() 
    If Documents.Count >= 1 Then 
        MsgBox ActiveDocument.Name 
    Else 
        MsgBox "No documents are open" 
    End If 
End Sub
```

