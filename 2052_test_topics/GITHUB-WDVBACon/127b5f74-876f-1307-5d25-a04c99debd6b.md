
# Tables.Add Method (Word)

Returns a  **Table** object that represents a new, blank table added to a document.


## Syntax

 _表达式_. **Add**( ** _Range_**, ** _NumRows_**, ** _NumColumns_**, ** _DefaultTableBehavior_**, ** _AutoFitBehavior_** )

 _表达式_ Required. A variable that represents a **[Tables](068a3d0f-0b19-3927-cb0a-7fb0d0fd8e52.md)** collection.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
| _Range_|必需|**Range object**|The range where you want the table to appear. The table replaces the range, if the range isn't collapsed.|
| _NumRows_|必需|**Long**|The number of rows you want to include in the table.|
| _NumColumns_|必需|**Long**|The number of columns you want to include in the table.|
| _DefaultTableBehavior_|可选|**Variant**|Sets a value that specifies whether Microsoft Word automatically resizes cells in tables to fit the cells? contents (AutoFit). Can be either of the following constants:  **wdWord8TableBehavior** (AutoFit disabled) or **wdWord9TableBehavior** (AutoFit enabled). The default constant is **wdWord8TableBehavior**.|
| _AutoFitBehavior_|可选|**Variant**|Sets the AutoFit rules for how Word sizes tables. Can be one of the  **WdAutoFitBehavior** constants.|

### Return Value

Table


## Example

This example adds a blank table with three rows and four columns at the beginning of the active document.


```
Set myRange = ActiveDocument.Range(0, 0) 
ActiveDocument.Tables.Add Range:=myRange, NumRows:=3, NumColumns:=4
```

This example adds a new, blank table with six rows and ten columns at the end of the active document




```
Set MyRange = ActiveDocument.Content 
MyRange.Collapse Direction:=wdCollapseEnd 
ActiveDocument.Tables.Add Range:=MyRange, NumRows:=6, _ 
 NumColumns:=10
```

This example adds a table with three rows and five columns to a new document and then inserts data into each cell in the table.




```
Sub NewTable() 
 Dim docNew As Document 
 Dim tblNew As Table 
 Dim intX As Integer 
 Dim intY As Integer 
 
 Set docNew = Documents.Add 
 Set tblNew = docNew.Tables.Add(Selection.Range, 3, 5) 
 With tblNew 
 For intX = 1 To 3 
 For intY = 1 To 5 
 .Cell(intX, intY).Range.InsertAfter "Cell: R" &amp; intX &amp; ", C" &amp; intY 
 Next intY 
 Next intX 
 .Columns.AutoFit 
 End With 
End Sub
```


## 另请参阅


#### 概念


[Tables Collection Object](068a3d0f-0b19-3927-cb0a-7fb0d0fd8e52.md)
#### 其他资源


[Tables Object Members](http://msdn.microsoft.com/library/8bde1cd9-9175-d49c-52e4-fa4e59ec8c13%28Office.15%29.aspx)