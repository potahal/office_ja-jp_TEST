
# 修改录制的 Visual Basic 宏

宏录制器是很好的工具发现的 Visual Basic 方法和要使用的属性。如果您不知道何种属性或方法来使用，打开宏录制器并手动执行操作。宏录制器转换为 Visual Basic 代码操作。有，但是，录制宏具有一些限制条件。您不能录制:


- 条件分支
    
- 变量指定
    
- 循环结构
    
- 自定义用户窗体
    
- 出错处理
    
- 用鼠标选定的文本（必须使用组合键）
    

若要增强宏的功能，可能需要修改录制到模块中的代码。


## 删除 Selection 属性

使用宏录制器创建的宏取决于所选内容。在大多数录制的宏指令的开头，请参阅 `Selection`。录制宏使用 **[Selection](71938a78-36ae-07ba-496b-911bef746444.md)** 属性来返回 **[所选内容](7b574a91-c33e-ecfd-6783-6b7528b2ed8f.md)** 的对象。例如，下面的示例将所选内容移动至 Temp 书签，并在书签之后插入文字。


```
Sub Macro1() 
    Selection.Goto What:=wdGotoBookmark, Name:="Temp" 
    Selection.MoveRight Unit:=wdCharacter, Count:=1 
    Selection.TypeText Text:="New text" 
End Sub
```

这个宏虽然可以完成任务，但是存在一些缺点。首先，如果文档中没有一个名为 Temp 的书签，该宏将导致出错。其次，该宏可能会移动不适当的选定内容。修改这个宏，使其不再使用  **Selection** 对象，就可解决上述两个问题。以下就是经修改后的宏。




```
Sub MyMacro() 
    If ActiveDocument.Bookmarks.Exists("Temp") = True Then 
        endloc = ActiveDocument.Bookmarks("Temp").End 
        ActiveDocument.Range(Start:=endloc, _ 
        End:=endloc).InsertAfter "New text" 
    End If 
End Sub
```

 **[Exists](7a9df80d-1a52-022f-f234-336369b73fca.md)** 方法用于检查存在名为 Temp 的书签。如果找到该书签，则书签的结束字符位置返回使用 **[End](05531b0d-b05e-0010-9ff8-ba6d90de560d.md)** 属性。最后， **文档** 对象的 **[Range](7dd33ac8-38f6-925d-a511-8677ca6437e6.md)** 方法用于返回一个 **[Range](15a7a1c4-5f3f-5b6e-60e9-29688de3f274.md)** 对象，该对象引用的书签的结束位置，以便可以使用 **Range** 对象的 **[新段落](25b2c0be-e9c7-1e42-09ea-308bbdcde7c6.md)** 插入文本。有关定义 **范围** 的对象的详细信息，请参阅[使用 Range 对象](9e240aa7-8608-9d70-aee3-2e202687459e.md)。


## 使用 With…End With

可以使用  **With…End With** 结构来简化引用相同对象的宏指令。例如，在文档顶部添加标题时，将录制下面的宏。


```
Sub Macro1() 
    Selection.HomeKey Unit:=wdStory 
    Selection.TypeText Text:="Title" 
    Selection.ParagraphAlignment.Alignment = wdAlignParagraphCenter 
End Sub
```

每个指令都使用  **Selection** 属性返回一个 **Selection** 对象。可以简化这个宏，以便只需使用 **Selection** 属性一次。




```
Sub MyMacro() 
    With Selection 
        .HomeKey Unit:=wdStory 
        .TypeText Text:="Title" 
        .ParagraphAlignment.Alignment = wdAlignParagraphCenter 
    End With 
End Sub
```

不使用  **Selection** 对象也可以完成相同的任务。下面的宏在活动文档的开头使用 **Range** 对象来完成相同的任务。




```
Sub MyMacro() 
    With ActiveDocument.Range(Start:=0, End:=0) 
        .InsertAfter "Title" 
        .ParagraphFormat.Alignment = wdAlignParagraphCenter 
    End With 
End Sub
```


## 删除不必要的属性

如果录制的宏涉及到在对话框中选择某个选项，则即使只更改一个或两个选项，宏录制器也会记录该对话框中的所有选项的设置。如果不希望更改所有的选项，可从录制的宏中删除不必要的属性。下面录制的宏包含"格式"菜单的"段落"对话框中的许多选项。


```
Sub Macro1() 
    With Selection.ParagraphFormat 
        .LeftIndent = InchesToPoints(0) 
        .RightIndent = InchesToPoints(0) 
        .SpaceBefore = 6 
        .SpaceAfter = 6 
        .LineSpacingRule = 0 
        .Alignment = wdAlignParagraphLeft 
        .WidowControl = True 
        .KeepWithNext = False 
        .KeepTogether = False 
        .PageBreakBefore = False 
        .NoLineNumber = False 
        .Hyphenation = True 
        .FirstLineIndent = InchesToPoints(0) 
        .OutlineLevel = 10 
    End With 
End Sub
```

但是，如果只需更改段前和段后间距，可将宏更改为：




```
Sub MyMacro() 
    With Selection.ParagraphFormat 
        .SpaceBefore = 6 
        .SpaceAfter = 6 
    End With 
End Sub
```

因为只设置了较少的属性，所以简化后的宏运行得更快。只更改段前和段后间距，其他所有设置都保持不变。


## 删除不必要的参数

当宏录制器记录一个方法时，会包含所有参数的值。名为 Test.doc 文档打开时录制下面的宏。所得到的宏包含所有 **[打开](9e61e9d5-58d1-833a-5f93-b87299deb400.md)** 的方法的参数。


```
Sub Macro1() 
    Documents.Open FileName:="C:\My Documents\Test.doc", _ 
        ConfirmConversions:= False, ReadOnly:=False, _ 
        AddToRecentFiles:=False, PasswordDocument:="", _ 
        PasswordTemplate:="", Revert:=False, _ 
        WritePasswordDocument:="", _ 
        WritePasswordTemplate:="", Format:=wdOpenFormatAuto 
End Sub
```

可从录制的宏中删除不需要的参数。例如，可以删除所有的参数设置为空字符串 (例如，  `WritePasswordDocument:=""`)，如所示。




```
Sub MyMacro() 
    Documents.Open FileName:="C:\My Documents\Test.doc", _ 
        ConfirmConversions:= False, _ 
        ReadOnly:=False, AddToRecentFiles:=False, _ 
        Revert:=False, Format:=wdOpenFormatAuto 
End Sub
```

