
# 修改文档的某一部分

Visual Basic 包含可用于修改下列文档元素的对象: 字符、 单词、 句子、 段落和节。下表包含与这些文档元素和它们返回的对象相对应的属性。



|**表达式**|**返回的对象**|
|:-----|:-----|
|**单词** ( _索引_ )|**[Range](15a7a1c4-5f3f-5b6e-60e9-29688de3f274.md)**|
|**字符** ( _索引_ )|**[Range](15a7a1c4-5f3f-5b6e-60e9-29688de3f274.md)**|
|**句子** ( _索引_ )|**[Range](15a7a1c4-5f3f-5b6e-60e9-29688de3f274.md)**|
|**段落** ( _索引_ )|**[Paragraph](0a704079-a082-4ab1-841b-fc0d49dd26d4.md)**|
|**部分** ( _索引_ )|**[Section](3fe563d8-fc05-c17a-e67b-c50eea7e7f13.md)**|

当使用这些属性时不进行索引时，则返回具有相同名称的集合对象。例如， **段落** 属性返回的 **[段落](bdc7a183-2a98-7d47-c86a-5cecd6c91449.md)** 集合对象。但是，如果通过索引序号识别集合中的项会返回表的第二列中的对象。例如， `Words(1)`会返回一个 **Range** 对象。 **Range** 对象后，可以使用区域属性或方法的任何修改的 **Range** 对象。例如，下面的指令选定内容到剪贴板中复制第一个单词。




```
Sub CopyWord() 
    Selection.Words(1).Copy 
End Sub
```


 **注释**   **[段落](bdc7a183-2a98-7d47-c86a-5cecd6c91449.md)** 和 **[分区](cf6f77ba-9eee-5614-e697-bc031c4c6dcd.md)** 集合中的项是单数形式的专门 **段落** 的对象和 **部分** 对象集合中，而不是 **Range** 对象。事实上，Word 对象模型中的大多数集合还可以使用单数形式的对象。但是， **范围** 属性 (它将返回一个 **Range** 对象) 可从 **段落** 对象和 **部分** 对象，以及大多数其他对象的子级的集合。例如，下列指令将活动文档第一段复制到剪贴板。




```
Sub CopyParagraph() 
    ActiveDocument.Paragraphs(1).Range.Copy 
End Sub
```

上述表格中的所有文档元素属性对  **Document** 、 **Selection** 和 **Range** 对象都可用。下面的示例阐明了如何使用 **[Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)** 、 **[Selection](7b574a91-c33e-ecfd-6783-6b7528b2ed8f.md)** 和 **Range** 对象的这些属性。
下面的示例设置活动文档中第一个单词的大小写。



```
Sub ChangeCase() 
    ActiveDocument.Words(1).Case = wdUpperCase 
End Sub
```

下面的示例将当前节的下边距设为 0.5 英寸。



```
Sub ChangeSectionMargin() 
    Selection.Sections(1).PageSetup.BottomMargin = InchesToPoints(0.5) 
End Sub
```

下面的示例双间隔 ( **[内容](80578329-a648-1d4b-f83d-4b2d289813fb.md)** 属性将返回一个 **Range** 对象) 为活动文档中的文本。



```
Sub DoubleSpaceDocument() 
    ActiveDocument.Content.ParagraphFormat.Space2 
End Sub
```


## 修改一组文档元素

要修改由一组文档元素（字符、字词、句子、段落或节）组成的文本区域，需要创建一个  **Range** 对象。 **Range** 方法根据起点和终点创建 **Range** 对象。例如，下面的指令创建一个引用活动文档前十个字符的 **Range** 对象。


```
Sub SetRangeForFirstTenCharacters() 
    Dim rngTenCharacters As Range 
    Set rngTenCharacters = ActiveDocument.Range(Start:=0, End:=10) 
End Sub
```

使用带有 **Range** 对象的 **[Start](aadedbb7-1ee2-9e5a-296d-0ebe25b6d8f4.md)** 和 **[End](fe90f321-c7b5-bea2-fa60-e6b750b33cf7.md)** 属性，您可以创建新的 **Range** 对象，它指的是一组文档元素。例如，下面的指令创建一个引用活动文档前三个词的 **Range** 对象 ( `rngThreeWords`)。




```
Sub SetRangeForFirstThreeWords() 
    Dim docActive As Document 
    Dim rngThreeWords As Range 
    Set docActive = ActiveDocument 
    Set rngThreeWords = docActive.Range(Start:=docActive.Words(1).Start, _ 
        End:=docActive.Words(3).End) 
End Sub
```

下面的示例创建一个 **Range** 对象 ( `rngParagraphs`) 的第二段开头开始和结束后的第三段。




```
Sub SetParagraphRange() 
    Dim docActive As Document 
    Dim rngParagraphs As Range 
    Set docActive = ActiveDocument 
    Set rngParagraphs = docActive.Range(Start:=docActive.Paragraphs(2).Range.Start, _ 
        End:=docActive.Paragraphs(3).Range.End) 
End Sub
```

有关定义  **Range** 对象的详细信息，请参阅[处理 Range 对象](9e240aa7-8608-9d70-aee3-2e202687459e.md)。

