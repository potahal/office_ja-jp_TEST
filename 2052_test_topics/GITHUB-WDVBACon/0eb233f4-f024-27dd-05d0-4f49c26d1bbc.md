
# 筛选内容控件中的构建基块列表

如果同时使用内容控件和构建基块，那么您可以通过指出可以插入哪些内容并限制用户的选择来帮助用户。您可以通过筛选所显示的内容控件的构建基块列表来达到此目的。要筛选内容控件的构建基块列表，请使用内容控件的  **[BuildingBlockType](6fe28ef5-fe7d-792e-f73a-b6726c802790.md)** 属性。还可以通过设置内容控件的 **[BuildingBlockCategory](ca799bde-8556-381e-c9ca-74c5ac250d99.md)** 属性进一步筛选构建基块列表。

在创建内容控件时，可以筛选内容控件的构建基块列表。不过，还可以根据光标的位置或其他内容控件的值以编程方式筛选构建基块列表。要进行筛选，必须具备自定义构建基块库。要了解如何创建自定义构建基块库，请参阅[创建自定义构建基块库](472688b6-205c-c88d-5a7e-26334ec5eeeb.md)。




-  **[BuildingBlock](2558b89f-8552-bb71-fa40-101cab2635ba.md)**
    
-  **[ContentControl](783dec26-9b63-11f8-6187-985f9c815f27.md)**
    

## 示例 1

以下示例显示了如何将构建基块库列表筛选到特定库中。


 **注释**  在运行本主题中的代码之前，请先运行"创建自定义构建基块库"主题中的代码。


```
Sub CreateFilteredContentControl_SampleOneA() 
 Dim objCC As ContentControl 
 
 Set objCC = ActiveDocument.ContentControls.Add( _ 
 wdContentControlBuildingBlockGallery, Selection.Range) 
 
 objCC.BuildingBlockType = wdTypeCustom1 
End Sub
```

可以通过在库中指定特定类别来进一步筛选构建基块列表。以下示例显示了如何筛选构建基块列表，以只包括指定库类别中的构建基块。




```
Sub CreateFilteredContentControl_SampleOneB() 
 Dim objCC As ContentControl 
 
 Set objCC = ActiveDocument.ContentControls.Add( _ 
 wdContentControlBuildingBlockGallery, Selection.Range) 
 
 objCC.BuildingBlockType = wdTypeCustom1 
 objCC.BuildingBlockCategory = "Tertiary Headings" 
End Sub
```


## 示例 2

要根据光标的位置筛选构建基块列表，需要使用  **ContentControlOnEnter** 事件。例如，如果名为 Report Type 的内容控件可以设置为 financial 或 marketing，那么可以用内容控件的构建基块显示可能的免责声明列表。如果未设置 Report Type，则免责声明内容控件将显示所有免责声明，如果设置了该属性，则只显示适当的子集。以下示例根据文档中另一个内容控件的值筛选内容控件的构建基块列表。


```
Private Sub Document_ContentControlOnEnter(ByVal ContentControl As ContentControl) 
 Dim objCC As ContentControl 
 Dim objType As ContentControl 
 
 Set objCC = ContentControl 
 Set objType = ActiveDocument.ContentControls.Item("Report Type") 
 
 If objCC.Title = "Disclaimer" Then 
 Select Case objType.Range.Text 
 Case "Financial" 
 objCC.BuildingBlockType = wdTypeCustom1 
 objCC.BuildingBlockCategory = "Financial Disclaimers" 
 
 Case "Marketing" 
 objCC.BuildingBlockType = wdTypeCustom1 
 objCC.BuildingBlockCategory = "Marketing Disclaimers" 
 
 End Select 
 End If 
End Sub
```


## 其他资源


- [使用构建基块](c32a8972-a6fc-bb66-b62a-039b88580b37.md)
    
- [使用内容控件](b4092c71-a383-f1db-8d68-de69e8d8a86b.md)
    
