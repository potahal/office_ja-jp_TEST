
# CanvasShapes Object (Word)

Use the  **CanvasItems** property of either a **[Shape](604029ce-9b2f-9748-5d4e-b458796fa2f0.md)** or **[ShapeRange](7112acc0-e241-16ef-77bc-101b72d05af0.md)** object to return a **CanvasShapes** collection.


## Remarks

To add shapes to a drawing canvas, use the following methods of the  **CanvasShapes** collection: **[AddCallout](87affac3-523e-165f-690a-75bd9e0b9961.md)**, **[AddConnector](0bfb15ae-0d0d-1864-bdf3-941e22090696.md)** **[AddCurve](d66512d3-77a7-4f52-ad94-44712c75e87d.md)**, **[AddLabel](a789aa04-039c-f455-56ed-ca864e0de6ee.md)**, **[AddLine](74e5fd57-6a7f-8f5f-6350-e980afd514e7.md)**, **[AddPicture](445a19cf-1388-34d7-69d8-778d889d917f.md)**, **[AddPolyline](101a0380-f28d-4212-859f-9bca247da1be.md)**, **[AddShape](b23c69f1-8653-a98f-d7f4-6648e0e214fa.md)**, **[AddTextbox](b8688f8f-db7e-7cf6-12ea-d0712b4ce26b.md)**, **[AddTextEffect](8f9c51b7-1588-6090-85b8-b94620c9e4bb.md)**, or **[BuildFreeform](eb960023-aeda-d272-c9c8-5474fb5867b2.md)**. The following example adds a drawing canvas to the active document and then adds three shapes to the drawing canvas.


```
Sub AddCanvasShapes() 
 Dim shpCanvas As Shape 
 Dim shpCanvasShapes As CanvasShapes 
 Dim shpCnvItem As Shape 
 
 'Adds a new canvas to the document 
 Set shpCanvas = ActiveDocument.Shapes _ 
 .AddCanvas(Left:=100, Top:=75, _ 
 Width:=50, Height:=75) 
 Set shpCanvasShapes = shpCanvas.CanvasItems 
 
 'Adds shapes to the CanvasShapes collection 
 With shpCanvasShapes 
 .AddShape Type:=msoShapeRectangle, _ 
 Left:=0, Top:=0, Width:=50, Height:=50 
 .AddShape Type:=msoShapeOval, _ 
 Left:=5, Top:=5, Width:=40, Height:=40 
 .AddShape Type:=msoShapeIsoscelesTriangle, _ 
 Left:=0, Top:=25, Width:=50, Height:=50 
 End With 
End Sub
```

Use  **CanvasItems** (index), where index is the name or the index number, to return a single shape in the **CanvasShapes** collection. The following example sets the **Line** and **Fill** properties and vertically flips the third shape in a drawing canvas.




```
Sub CanvasShapeThree() 
 With ActiveDocument.Shapes(1).CanvasItems(3) 
 .Line.ForeColor.RGB = RGB(50, 0, 255) 
 .Fill.ForeColor.RGB = RGB(50, 0, 255) 
 .Flip msoFlipVertical 
 End With 
End Sub
```

Each shape is assigned a default name when it is created. For example, if you add three different shapes to a document, they might be named Rectangle 2, TextBox 3, and Oval 4. Use the  **Name** property to reference the default name or to assign a more meaningful name to a shape.


## 另请参阅


#### 概念


[Word Object Model Reference](be452561-b436-bb9b-6f94-3faa9a74a6fd.md)
#### 其他资源


[CanvasShapes Object Members](http://msdn.microsoft.com/library/77b4d2f5-0ced-82ec-68ee-895c398d0c9f%28Office.15%29.aspx)