
# 创建框架页

在 Word 中，您可以使用框架网页设计中可以使信息更有条理，可以轻松地访问。框架页也称为框架集，是一个 Web 页，分为两个或多个部分，每个点到另一个网页。框架页中的框架也可以指向其他的框架页。有关在 Word 用户界面中创建框架和框架页的详细内容，请参阅 Word 帮助。

框架和框架页用一系列 HTML 标记创建。通过检查其 HTML 标记，可更好地理解带有框架和框架页的 Visual Basic 对象模块。

## HTML 中的框架页

在 HTML 中，框架页及其所包含的框架用一系列分级的 <FRAMESET> 和 <FRAME> 标记建立。框架集可包含框架和其他的框架集。例如，下列 HTML 建立了一个框架集，其上部有一个框架，在该框架下面紧接着有一个框架集；该框架集的左侧包含一个框架，右侧包含一个框架集；而该框架集又包含两个框架，其中一个在另一个的上方。


```XML
<FRAMESET ROWS="100, *"> 
    <FRAME NAME=top SRC="banner.htm"> 
    <FRAMESET COLS="20%, *"> 
        <FRAME NAME=left SRC="contents.htm"> 
        <FRAMESET ROWS="75%, *"> 
            <FRAME NAME=main SRC="main.htm"> 
            <FRAME NAME=bottom SRC="footer.htm"> 
        </FRAMESET> 
    </FRAMESET> 
</FRAMESET>
```


 **注释**  若要更好地理解上述 HTML 示例，可将该示例粘贴到空白的文本文档中，将文档重命名为"framespage.htm"，然后在 Word 或 Web 浏览器中打开该文档。


## Frameset 对象

 **[框架集](d76806db-c82f-f7b6-fb85-28a649de48a7.md)** 对象涵盖了这两个标记的功能。每个 **框架集** 的对象是类型 **wdFramesetTypeFrameset** 或 **wdFramesetTypeFrame** ，后者表示 HTML 标记 < 框架集 > 和 < 框架 >，分别。以"框架集"开头的属性应用于 **框架** 对象的类型 **wdFramesetTypeFrameset** ( **[FramesetBorderColor](c47a7b7e-17e0-1741-fd1c-22cde123b42f.md)** 和 **[FramesetBorderWidth](6d828372-78a3-83f1-d162-91b000af2023.md)** 。属性开头"框架"将应用于类型 **wdFramesetTypeFrame** ( **[FrameDefaultURL](596f57d4-2514-8cd0-2d97-20618051fd6c.md)** 、 **[FrameDisplayBorders](a1993b72-2737-92d8-d1bc-b4bc0182b23a.md)** 、 **[FrameLinkToFile](a27ce637-a892-3697-a727-e7c60eb26aaf.md)** 、 **[FrameName](f0b22dfe-3d12-0f75-1af2-23467b83a4ad.md)** 、 **[FrameResizable](5a373e57-3193-c2a3-52b6-42702237f6c3.md)** 和 **[FrameScrollBarType](dacd6394-872e-beac-85dc-575234f9ce29.md)** ) 的 **框架** 对象。


## 剖析 Frameset 对象的层次结构

框架网页被定义为一组分层的 HTML 标记，因为访问 **框架集** 的对象的对象模型也是分层的。使用 **[ChildFramesetItem](a0210de1-5556-0c20-a694-a6892dc7eddf.md)** 和 **[ParentFrameset](aa2759c6-4072-00c6-0c4f-ef12ecc19bd6.md)** 属性遍历层次结构 **框架集** 的对象。例如，


```
MyFrameset.ChildFramesetItem(n)
```

返回一个对应于 _n_ 的 **框架** 对象第一级 < 框架集 > 或 < 框架 > 标记之间 < 框架集 > 和 < / 框架集 > 标记对应于 `MyFrameset`。

如果 `MyFrameset`是一个 **Frameset** 对象对应于最外层 < 框架集 > 标记前面的 HTML 示例中， `MyFrameset.ChildFramesetItem(1)`返回到名为"上限"的框架的 **框架集** 对象的类型相对应的 **wdFramesetTypeFrame** 同样， `MyFrameset.ChildFramesetItem(2)`返回类型 **wdFramesetTypeFrameset** ，本身包含两个 **框架** 的 **框架集** 对象: 第一个对象对应于名为"左，"帧类型 **wdFramesetTypeFrameset** 的是第二个。

 **wdFramesetTypeFrame** 类型的 **Frameset** 对象没有子框架，而 **wdFramesetTypeFrameset** 类型的该对象至少有一个子框架。

下列 Visual Basic 示例显示了上面 HTML 示例中定义的四个框架的名称。




```
Dim MyFrameset As Frameset 
Dim Name1 As String 
Dim Name2 As String 
Dim Name3 As String 
Dim Name4 As String 
 
Set MyFrameset = ActiveWindow.Document.Frameset 
 
With MyFrameset 
    Name1 = .ChildFramesetItem(1).FrameName 
    With .ChildFramesetItem(2) 
        Name2 = .ChildFramesetItem(1).FrameName 
        With .ChildFramesetItem(2) 
            Name3 = .ChildFramesetItem(1).FrameName 
            Name4 = .ChildFramesetItem(2).FrameName 
        End With 
    End With 
End With 
 
Debug.Print Name1, Name2, Name3, Name4
```


## 单个框架和整个框架页

返回与特定框架页上框架的 **框架集** 对象，使用 **[框架集](6bab63ae-aa83-e2b8-9b92-e472c2433246.md)** 属性 **[窗格](4a0c2690-d9d2-4e34-fef4-cc41365f5251.md)** 对象。例如，


```
ActiveWindow.Panes(1).Frameset
```

返回与框架页上第一个框架相对应的  **Frameset** 对象。

框架网页本身就是分开构成单个框架内容的文档的文档。与框架网页的 **框架** 对象可从其相应的 **[文档](8d83487a-2345-a036-a916-971c9db5b7fb.md)** 对象，又可从在其中显示的框架网页的 **[窗口](d92f83f9-ae44-56c0-4584-7a9359253c6d.md)** 对象。例如，




```
ActiveWindow.Document.Frameset
```

返回活动窗口中框架页的  **Frameset** 对象。


 **注释**  当使用框架页，  **[ActiveDocument](c20a7c9f-f8a4-7913-f53f-10baa6807def.md)** 属性返回在活动窗格中，而不是整个框架网页的框架与关联的 **文档** 对象。


## 从头创建一个框架页及其内容

本示例新建一个带有三个框架的框架页，将文本添加到每个框架，并为每个框架设置背景色。它将两个超链接插入左侧的框架中：第一个超链接在主框架中打开名为 One.htm 的文档，而第二个超链接在整个窗口中打开名为 Two.htm 的文档。为了使超链接工作，必须创建名为 One.htm 和 Two.htm 的文件，或将字符串更改为现有文件的名称。


 **注释**  创建每个框架时，Word 将新建一篇文档，其内容将装载于新的框架中。本示例在保存框架页时自动保存与三个框架相关的文档。


```
Sub FramesetExample1() 
 
    ' Create new frames page. 
    Documents.Add DocumentType:=wdNewFrameset 
 
    With ActiveWindow 
        ' Add text and color to first frame. 
        Selection.TypeText Text:="BANNER FRAME" 
        With ActiveDocument.Background.Fill 
            .ForeColor.RGB = RGB(204, 153, 255) 
            .Visible = msoTrue 
        End With 
 
        ' Add new frame below top frame. 
        .ActivePane.Frameset.AddNewFrame _ 
            wdFramesetNewFrameBelow 
        ' Add text and color to bottom frame. 
        .ActivePane.Frameset.FrameName = "main" 
        Selection.TypeText Text:="MAIN FRAME" 
        With ActiveDocument.Background.Fill 
            .ForeColor.RGB = RGB(0, 128, 128) 
            .Visible = msoTrue 
        End With 
 
        ' Add new frame to left of bottom frame. 
        .ActivePane.Frameset.AddNewFrame _ 
            wdFramesetNewFrameLeft 
        ' Set the width to 25% of the window width. 
        With .ActivePane.Frameset 
            .WidthType = wdFramesetSizeTypePercent 
            .Width = 25 
            .FrameName = "left" 
        End With 
        ' Add text and color to left frame. 
        Selection.TypeText Text:="LEFT FRAME" 
        With ActiveDocument.Background.Fill 
            .ForeColor.RGB = RGB(204, 255, 255) 
            .Visible = msoTrue 
        End With 
        Selection.TypeParagraph 
        Selection.TypeParagraph 
        ' Add hyperlinks to left frame. 
        With ActiveDocument.Hyperlinks 
            .Add Anchor:=Selection.Range, _ 
                Address:="one.htm", Target:="main" 
            Selection.TypeParagraph 
            Selection.TypeParagraph 
            .Add Anchor:=Selection.Range, _ 
                Address:="two.htm", Target:="_top" 
        End With 
        
        ' Activate top frame. 
        .Panes(1).Activate 
        ' Set the height to 1 inch. 
        With .ActivePane.Frameset 
            .HeightType = wdFramesetSizeTypeFixed 
            .Height = InchesToPoints(1) 
            .FrameName = "top" 
        End With 
 
        ' Save the frames page and its associated files. 
        .Document.SaveAs FileName:="default.htm", _ 
            FileFormat:=wdFormatHTML 
    End With 
 
End Sub
```


## 创建显示现有文件内容的框架页

本示例创建一个与上例类似的框架页，但是将每个框架的默认 URL 设置为一个现有的文档，以便在框架中显示该文档的内容。为了使本示例工作，必须创建名为 Main.htm、Left.htm 和 Banner.htm 的文件，或将本示例中的字符串更改为现有文件的名称。


```
Sub FramesetExample2() 
    
    ' Create new frames page. 
    Documents.Add DocumentType:=wdNewFrameset 
 
    With ActiveWindow 
        ' Add new frame below top frame. 
        .ActivePane.Frameset.AddNewFrame _ 
            wdFramesetNewFrameBelow 
        ' Set the name and initial page for the frame. 
        With .ActivePane.Frameset 
            .FrameName = "main" 
            .FrameDefaultURL = "main.htm" 
        End With 
        
        ' Add new frame to left of bottom frame. 
        .ActivePane.Frameset.AddNewFrame _ 
            wdFramesetNewFrameLeft 
        With .ActivePane.Frameset 
            ' Set the width to 25% of the window width. 
            .WidthType = wdFramesetSizeTypePercent 
            .Width = 25 
            ' Set the name and initial page for the frame. 
            .FrameName = "left" 
            .FrameDefaultURL = "left.htm" 
        End With 
    
        ' Activate top frame. 
        .Panes(1).Activate 
        With .ActivePane.Frameset 
            ' Set the height to 1 inch. 
            .HeightType = wdFramesetSizeTypeFixed 
            .Height = InchesToPoints(1) 
            ' Set the name and initial page for the frame. 
            .FrameName = "top" 
            .FrameDefaultURL = "banner.htm" 
        End With 
 
        ' Save the frameset. 
        .Document.SaveAs FileName:="default.htm", _ 
            FileFormat:=wdFormatHTML 
    End With 
 
End Sub
```

