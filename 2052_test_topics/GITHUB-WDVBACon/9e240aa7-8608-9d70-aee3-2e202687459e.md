
# 处理 Range 对象

使用 Visual Basic 完成的一种常见任务是在文档中指定一个区域，然后对该区域进行某些操作，如插入文字或应用格式。例如，您可能需要编写一个宏，用于在文档的某部分中查找一个单词或词组。该文档部分可以用  **[Range](15a7a1c4-5f3f-5b6e-60e9-29688de3f274.md)** 对象表示。标识 **Range** 对象后，就可以应用 **Range** 对象的方法和属性来修改该区域的内容。

 **Range** 对象引用文档中的某一连续区域。每个 **Range** 对象都是通过开始和结束字符位置来定义的。与在文档中使用书签的方法类似，在 Visual Basic 过程中使用 **Range** 对象可以标识文档中的特定部分。 **Range** 对象可以小至一个插入点，或大至整个文档。但是与书签不同， **Range** 对象只在定义它的过程运行时才存在。

 **[Start](aadedbb7-1ee2-9e5a-296d-0ebe25b6d8f4.md)** 、 **[End](fe90f321-c7b5-bea2-fa60-e6b750b33cf7.md)** 和 **[StoryType](bf11ba94-de45-ae76-09fa-9463cd2c4723.md)** 属性唯一地标识一个 **Range** 对象。 **Start** 和 **End** 属性返回或设置 **Range** 对象的开始和结束字符的位置。文档开始处的字符位置为 0，第一个字符后的位置为 1，依此类推。 **StoryType** 属性的 **WdStoryType** 常量可以代表 11 种不同的文章类型。


 **注释**   **Range** 对象与所选内容无关。也就是说，可以定义和修改某区域而不改变当前的所选内容。也可定义文档中的多个区域，而每个文档窗格中只有一处所选内容。


## 使用 Range 方法

使用  **[Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)** 对象的 **[Range](e28026bf-aa5e-8cf4-e765-7350afd57741.md)** 方法可创建一个 **Range** 对象，该对象位于主文章中并具有给定的起点和终点。下面的示例创建一个 **Range** 对象，该对象从第一个字符的开始位置开始并延伸到第十个字符。


```
Sub SetNewRange() 
 Dim rngDoc As Range 
 Set rngDoc = ActiveDocument.Range(Start:=0, End:=10) 
End Sub
```

将属性或方法应用于  **Range** 对象时，可以看到已经创建了 **Range** 对象。下面的示例对活动文档的前 10 个字符应用加粗格式。




```
Sub SetBoldRange() 
 Dim rngDoc As Range 
 Set rngDoc = ActiveDocument.Range(Start:=0, End:=10) 
 rngDoc.Bold = True 
End Sub
```

如果需要多次引用一个  **Range** 对象，可使用 **Set** 语句设置一个其值为该 **Range** 对象的变量。但是，如果仅需要对 **Range** 对象执行一次操作，则不必将该对象存储在变量中。只使用一个标识区域并更改 **[Bold](04723b36-43bb-4721-90a5-33447a9b742e.md)** 属性的指令，也能得到相同的结果。




```
Sub BoldRange() 
 ActiveDocument.Range(Start:=0, End:=10).Bold = True 
End Sub
```

类似于书签，一个区域可以跨越一组字符或标记文档中的某个位置。下列示例中的  **Range** 对象的开始和结束位置相同。该区域不包含任何文字。下列示例在活动文档的开头插入文字。




```
Sub InsertTextBeforeRange() 
 Dim rngDoc As Range 
 Set rngDoc = ActiveDocument.Range(Start:=0, End:=0) 
 rngDoc.InsertBefore "Hello " 
End Sub
```

可以使用上述字符位置编号，或通过将  **Start** 和 **End** 属性用于 **[Selection](7b574a91-c33e-ecfd-6783-6b7528b2ed8f.md)** 、 **[Bookmark](be6b0c7b-60ca-97e7-ef19-6de335da3197.md)** 或 **Range** 等对象，来定义区域的开始和结束位置。下面的示例创建一个 **Range** 对象，该对象从第二段开头开始，至第三段末尾结束。




```
Sub NewRange() 
 Dim doc As Document 
 Dim rngDoc As Range 
 
 Set doc = ActiveDocument 
 Set rngDoc = doc.Range(Start:=doc.Paragraphs(2).Range.Start, _ 
 End:=doc.Paragraphs(3).Range.End) 
End Sub
```

有关其他信息和示例，请参阅  **[Range](e28026bf-aa5e-8cf4-e765-7350afd57741.md)** 方法。


## 使用 Range 属性

有多个对象具有  **Range** 属性，如 **[Paragraph](0a704079-a082-4ab1-841b-fc0d49dd26d4.md)** 、 **[Bookmark](be6b0c7b-60ca-97e7-ef19-6de335da3197.md)** 和 **[Cell](cbe6ae71-b2da-63a9-1446-0a2f81ab8b14.md)** 。该属性用于返回 **Range** 对象。下面的示例返回一个 **Range** 对象，该对象引用活动文档中的第一段。


```
Sub SetParagraphRange() 
 Dim rngParagraph As Range 
 Set rngParagraph = ActiveDocument.Paragraphs(1).Range 
End Sub
```

得到  **Range** 对象后，可使用它的任何属性或方法来修改该 **Range** 对象。下列示例选定活动文档中的第二个段落并将所选内容居中。




```
Sub FormatRange() 
 ActiveDocument.Paragraphs(2).Range.Select 
 Selection.ParagraphFormat.Alignment = wdAlignParagraphCenter 
End Sub
```

若要对同一个  **Range** 对象应用多种属性或方法，可使用 **With…End With** 结构。下列示例设置活动文档中第一段的文字格式。




```
Sub FormatFirstParagraph() 
 Dim rngParagraph As Range 
 Set rngParagraph = ActiveDocument.Paragraphs(1).Range 
 With rngParagraph 
 .Bold = True 
 .ParagraphFormat.Alignment = wdAlignParagraphCenter 
 With .Font 
 .Name = "Stencil" 
 .Size = 15 
 End With 
 End With 
End Sub
```

有关其他信息和示例，请参阅  **[Range](983f7bd3-10b4-882f-5b4d-01e44127676f.md)** 属性主题。


## 重新定义 Range 对象

使用  **[SetRange](91097079-406c-98f4-d37c-cca8dab7aef0.md)** 方法可重新定义现有的 **Range** 对象。下面的示例将一个区域定义为当前的所选内容。然后应用 **SetRange** 方法以重新定义该区域，使之包含当前所选内容和接下来的 10 个字符。


```
Sub ExpandRange() 
 Dim rngParagraph As Range 
 Set rngParagraph = Selection.Range 
 rngParagraph.SetRange Start:=rngParagraph.Start, _ 
 End:=rngParagraph.End + 10 
End Sub
```

有关其他信息和示例，请参阅  **[Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)** 的 **[Range](e28026bf-aa5e-8cf4-e765-7350afd57741.md)** 方法。


 **注释**  调试宏时，可以使用  **Select** 方法确保 **Range** 对象引用正确的文本区域。例如，下面的示例选择一个 **Range** 对象，该对象引用活动文档中的第二段和第三段，然后设置所选内容的字体格式。




```
Sub SelectRange() 
 Dim rngParagraph As Range 
 
 Set rngParagraph = ActiveDocument.Paragraphs(2).Range 
 
 rngParagraph.SetRange Start:=rngParagraph.Start, _ 
 End:=ActiveDocument.Paragraphs(3).Range.End 
 rngParagraph.Select 
 
 Selection.Font.Italic = True 
End Sub
```

