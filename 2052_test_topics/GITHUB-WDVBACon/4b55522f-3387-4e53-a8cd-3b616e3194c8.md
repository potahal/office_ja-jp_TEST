
# 创建自定义书目样式

![操作方法](images/mod_icon_badge_howto.png)


了解如何在Word中创建一个自定义书目样式

了解您需要构建一个简单的自定义样式的步骤 (和 XML 代码)。此外，学会制作更复杂的样式文件。在开始之前，还有一些需要了解的信息:

您创建的书目源所有列在下面的文件: %APPDATA%\Microsoft\Bibliography\Sources.xml

 **注释**  在Word中创建第一个书目源之前，不会存在 \Bibliography\Sources.xml 文件。

所有书目样式都存储在 %APPDATA%\Microsoft\Bibliography\Style。

## 构建一个基本的参考书目样式
<a name="Biblio_BuildBasicStyle"> </a>

首先，创建自定义的样式将遵循一个基本书目样式。


### 设置书目样式

若要创建书目样式，我们将创建一个 XML 样式表;也就是说，.xsl 文件调用 MyBookStyle.xsl，使用您喜欢的 XML 编辑器。记事本将正常进行。如名称所示，我们的示例中将"book"源类型的样式。

在文件的顶部，添加以下代码:




```XML
<?xml version="1.0" ?> 

<!--List of the external resources that we are referencing-->
 
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:b="http://schemas.openxmlformats.org/officeDocument/2006/bibliography">
 
<!--When the bibliography or citation is in your document, it's just HTML-->
 
<xsl:output method="html" encoding="us-ascii"/>
   
<!--Match the root element, and dispatch to its children-->
   
<xsl:template match="/">

<xsl:apply-templates select="*" />

</xsl:template>
```

当注释中描述， Word使用 HTML 表示参考书目或文档中的引文。上面的 XML 代码大部分是只准备的样式更值得关注的部分。例如，可以使您的样式来跟踪所做的如下面的示例中所示的版本号。




```XML
<!--Set an optional version number for this style--> 

<xsl:template match="b:version"> 

   <xsl:text>2006.5.07</xsl:text>

</xsl:template>

```

更重要的是，您可以使您的样式名称。将此标记添加: < xsl:when 测试"b:StyleNameLocalized"= >;然后为您的样式名称，在您选择的语言中通过使用下面的代码。




```XML
<xsl:when test="b:StyleNameLocalized/b:Lcid='1033'">

   <xsl:text>[Your Style Name] </xsl:text>
 
</xsl:when>
```

本部分包含区域设置名称的样式。对于我们的示例文件，我们希望我们的自定义书目样式名称，"简单簿样式，"在 **引用**选项卡上的 **样式**下拉列表中显示。为此，请添加下面的 XML 代码，指定该样式名称在英语语言环境 (Lcid 决定的语言)。




```XML
<!--Defines the name of the style in the References dropdown list-->
<xsl:when test="b:StyleNameLocalized"> 
   <xsl:choose> 
      <xsl:when test="b:StyleNameLocalized/b:Lcid='1033'"> 
         <xsl:text>Simple Book Style</xsl:text> 
      </xsl:when> 
</xsl:when>
```

您的样式将显示在其自己的应用程序中的 **书目样式**下拉列表框中的名称。

现在，请检查样式的详细信息。每个源类型 (例如，书籍、 电影、 期刊，文章等) 的 Word 中已经内置可用于书目的字段的列表。若要查看所有可用给定的源类型，字段在 **引用**选项卡上，选择 **管理源**，然后在 **源管理器**对话框中，选择 **新建**，打开 **创建源**对话框。然后选择 **显示所有书目域**。

源类型包含可用以下字段:


- 作者
    
- 标题
    
- 年
    
- City
    
- 省/市/自治区
    
- 国家/地区
    
- Publisher
    
- Editor
    
- 批量
    
- 卷的数量
    
- 在线翻译
    
- 简短的标题
    
- 标准号
    
- 页面
    
- 版
    
- 注释
    
在代码中，您可以指定的字段对书目样式而言非常重要。即使清除了 **显示所有书目域**，则这些字段将显示，并带有红色星号旁边。对于我们簿的示例中，要确保输入作者、 标题、 年份、 城和发布者，所以我想这些字段就可以警告用户这些建议应填写的字段旁边显示一个红色星号。




```XML
<!--Specifies which fields should appear in the Create Source dialog box when in a collapsed state (The Show All Bibliography Fields check box is cleared)-->

<xsl:template match="b:GetImportantFields[b:SourceType = 'Book']"> 
   <b:ImportantFields> 
      <b:ImportantField> 
         <xsl:text>b:Author/b:Author/b:NameList</xsl:text> 
      </b:ImportantField> 
      <b:ImportantField> 
         <xsl:text>b:Title</xsl:text> 
      </b:ImportantField> 
     <b:ImportantField> 
         <xsl:text>b:Year</xsl:text> 
      </b:ImportantField> 
      <b:ImportantField> 
         <xsl:text>b:City</xsl:text>
      </b:ImportantField> 
      <b:ImportantField> 
         <xsl:text>b:Publisher</xsl:text> 
      </b:ImportantField> 
   </b:ImportantFields> 
</xsl:template>
```

在 < xsl:text > 标记中的文本是对 Sources.xml 文件的引用。这些引用拉出将填充每个字段的数据。检查 Sources.xml (%APPDATA%\Microsoft\Bibliography\Sources.xml)，以获取有关这些引用匹配的方式更好地了解到什么是 XML 文件中。


### 设计布局
<a name="Biblio_DesignLayout"> </a>

书目和引文的输出都在一个 Word 文档作为 HTML，因此来定义我们的自定义书目和引文样式的外观在 Word 中，我们不得不向我们的样式表添加一些 HTML。

假设您想要以这种方式您参考书目中的每个条目的格式:

姓，名。(年)。标题。城市: 发布服务器

若要执行此操作所需的 HTML，如下所示将样式表中嵌入。




```XML
<!--Defines the output format for a simple Book (in the Bibliography) with important fields defined-->

<xsl:template match="b:Source[b:SourceType = 'Book']"> 

<!--Label the paragraph as an Office Bibliography paragraph-->

   <p> 
      <xsl:value-of select="b:Author/b:Author/b:NameList/b:Person/b:Last"/> 
      <xsl:text>, </xsl:text> 
      <xsl:value-of select="b:Author/b:Author/b:NameList/b:Person/b:First"/> 
      <xsl:text>. (</xsl:text> 
      <xsl:value-of select="b:Year"/> 
      <xsl:text>). </xsl:text> 
      <i> 
         <xsl:value-of select="b:Title"/> 
         <xsl:text>. </xsl:text> 
      </i> 
      <xsl:value-of select="b:City"/> 
      <xsl:text>: </xsl:text> 
      <xsl:value-of select="b:Publisher"/> 
      <xsl:text>.</xsl:text> 
   </p> 
</xsl:template>
```

当在Word文档中引用的书源时， Word需要访问此 HTML，以便它可以使用自定义样式来显示源代码，所以您必须将代码添加到您的自定义样式表，以使 Word 可以执行此操作。




```XML
<!--Defines the output of the entire Bibliography-->
 
<xsl:template match="b:Bibliography"> 

   <html xmlns="http://www.w3.org/TR/REC-html40"> 
   
      <body> 

         <xsl:apply-templates select ="b:Source[b:SourceType = 'Book']"> 

         </xsl:apply-templates> 

      </body> 
   
   </html> 
</xsl:template>
```

以类似的方式，您将需要执行相同的操作的引文输出。按照文档中单独的引文模式 (作者、 年)。




```XML
<!--Defines the output of the Citation-->
<xsl:template match="b:Citation/b:Source[b:SourceType = 'Book']"> 
   <html xmlns="http://www.w3.org/TR/REC-html40"> 
      <body> 
         <!-- Defines the output format as (Author, Year)--> 
         <xsl:text>(</xsl:text> 
            <xsl:value-of select="b:Author/b:Author/b:NameList/b:Person/b:Last"/> 
         <xsl:text>, </xsl:text> 
         <xsl:value-of select="b:Year"/> 
         <xsl:text>)</xsl:text> 
      </body> 
   </html> 
</xsl:template>
```

使用以下命令行文件关闭。




```
<xsl:template match="text()" /> </xsl:stylesheet>
```

将该文件另存为 MyBookStyle.XSL，并将其放入样式目录 (%appdata%\microsoft\bibliography\style)。重新启动Word和您的风格现在是在样式下拉列表。您可以开始使用新样式。


## 创建复杂的样式
<a name="Biblio_CreateComplexStyle"> </a>

使书目样式更为复杂的问题之一是它们通常需要有大量的条件逻辑。例如，如果指定日期，则您需要显示日期，而如果未指定日期，您可能需要使用缩写表示该来源没有日期。

有关的更具体的示例，在 APA 样式中，如果网站源未指定日期，缩写为"n.d."用于表示没有日期，和样式应自动执行此操作。下面是一个示例:

APA 网站源输入无日期: Kwan，Y.(n.d.)。日期输入检索网站 www.microsoft.com APA 来源: Kwan，Y.(2006 年，1 月 18)。从 www.microsoft.com 检索

正如您所看到的是取决于输入的数据上显示的内容。

几乎每个样式的输出需要更改取决于您是否"公司作者"普通作者"。如果未指定公司的作者，您将看到如何使用最常用的规则之一，实现这种逻辑到您的样式，使您可以指定公司的作者，则显示公司作者和普通作者。


### 解决方案概述

若要显示公司的作者才合适，请使用以下过程。


### 若要显示公司的作者


1. 添加变量来计算公司代码的引文部分中作者的人数。
    
2. 如果填入了公司作者引文显示公司的作者。如果公司的作者没有填写引文显示正常的创作。
    
3. 添加变量来计算公司代码的书目部分中作者的人数。
    
4. 如果公司的作者填写，参考书目中显示公司的作者。如果公司作者没有填写，请显示普通作者参考书目中。
    

### 开始使用

首先通过更改引文。以下是上一次的引文的代码。


```XML
<!--Defines the output of the Citation-->
<xsl:template match="b:Citation/b:Source[b:SourceType = 'Book']"> 
   <html xmlns="http://www.w3.org/TR/REC-html40"> 
      <body> 
         <!--Defines the output format as (Author, Year)-->
         <xsl:text>(</xsl:text> 
         <xsl:value-of select="b:Author/b:Author/b:NameList/b:Person/b:Last"/>
         <xsl:text>, </xsl:text> 
         <xsl:value-of select="b:Year"/> 
         <xsl:text>)</xsl:text> 
      </body>
   </html> 
</xsl:template>
```


### 步骤 1: 数公司作者的引文部分中定义新的变量

声明一个新变量，以帮助确定公司的作者是否可用。此变量是次数的公司作者字段的源中存在的计数。


```
<!--Defines the output of the Citation-->
<html xmlns="http://www.w3.org/TR/REC-html40">
   <!--Count the number of Corporate Authors (can only be 0 or 1)-->
      <xsl:variable name="cCorporateAuthors"> 
         <xsl:value-of select="count(b:Author/b:Author/b:Corporate)" /> 
      </xsl:variable>
```


### 第 2 步: 验证公司的作者已填写

请验证公司的作者已填写。你可以通过确定公司的作者在计数为非零值。如果公司作者存在，请将其显示出来。如果不存在，则显示正常的作者。


```XML

<xsl:text>(</xsl:text> 
<xsl:choose>
<!--When the corporate author exists, display the corporate author-->
<xsl:when test ="$cCorporateAuthors!=0"> 
<xsl:value-of select="b:Author/b:Author/b:Corporate"/> 
</xsl:when>
<!-- When the corporate author does not exist, display the normal author--> 
<xsl:otherwise> 
<xsl:value-of select="b:Author/b:Author/b:NameList/b:Person/b:Last"/> 
</xsl:otherwise> 
</xsl:choose> 
<xsl:text>, </xsl:text>
```

既然所做更改的引文，请参考书目的更改。下面是来自的书目部分本文前面。




```XML
<!--Defines the output format for a simple Book (in the Bibliography) with important fields defined-->
<xsl: template match="b:Source[b:SourceType = 'Book']">
<!--Label the paragraph as an Office Bibliography paragraph--> 
<p> 
<xsl:value-of select="b:Author/b:Author/b:NameList/b:Person/b:Last"/> 
<xsl:text>, </xsl:text> 
<xsl:value-of select="b:Author/b:Author/b:NameList/b:Person/b:First"/> 
<xsl:text>. (</xsl:text> 
<xsl:value-of select="b:Year"/> 
<xsl:text>). </xsl:text> 
<i>

```


### 第 3 步: 书目部分中定义的新变量

再一次让我们添加开始盘点的变量。


```XML
<!--Defines the output format for a simple Book (in the Bibliography) with important fields defined-->
<xsl: template match="b:Source[b:SourceType = 'Book']"> 
<!--Count the number of Corporate Authors (can only be 0 or 1)-->
<xsl:variable name="cCorporateAuthors"> 
<xsl:value-of select="count(b:Author/b:Author/b:Corporate)" /> 
</xsl:variable>
```


### 步骤 4: 确认公司作者已填写

请确认存在公司的作者。


```XML
…..
<xsl:variable name="cCorporateAuthors"> 
<xsl:value-of select="count(b:Author/b:Author/b:Corporate)" /> 
</xsl:variable> 
<p> 
<xsl:choose>
<!--When the corporate author exists display the corporate author-->
<xsl:when test ="$cCorporateAuthors!=0"> 
<xsl:value-of select="b:Author/b:Author/b:Corporate"/> 
<xsl:text>. (</xsl:text> 
</xsl:when> 
<xsl:otherwise> 
<!--When the corporate author does not exist, display the normal author-->
<xsl:value-of select="b:Author/b:Author/b:NameList/b:Person/b:Last"/> 
<xsl:text>, </xsl:text> 
<xsl:value-of select="b:Author/b:Author/b:NameList/b:Person/b:First"/> 
<xsl:text>. (</xsl:text>
</xsl:otherwise> 
</xsl:choose>
```

这是最终的完整代码。




```XML
<?xml version="1.0" ?> 
<!--List of the external resources that we are referencing-->
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:msxsl="urn:schemas-microsoft-com:xslt" xmlns:b="http://schemas.openxmlformats.org/officeDocument/2006/bibliography">
   <!--When the bibliography or citation is in your document, it's just HTML-->
   <xsl:output method="html" encoding="us-ascii"/> 
   <!--Match the root element, and dispatch to its children-->
   <xsl:template match="/"> 
      <xsl:apply-templates select="*" /> 
   </xsl:template>
   <!--Set an optional version number for this style-->
   <xsl:template match="b:version"> 
      <xsl:text>2006.5.07</xsl:text> 
   </xsl:template> 
   <!--Defines the name of the style in the References dropdown-->
   <xsl:template match="b:StyleName">     
      <xsl:text>Simple Book Style</xsl:text> 
   </xsl:template> 
   <!--Specifies which fields should appear in the Create Source dialog when in a collapsed state (The Show All Bibliography Fieldscheckbox is cleared)-->
   <xsl:template match="b:GetImportantFields[b:SourceType = 'Book']"> 
      <b:ImportantFields> 
         <b:ImportantField><xsl:text>b:Author/b:Author/b:NameList</xsl:text> </b:ImportantField> 
         <b:ImportantField> <xsl:text>b:Title</xsl:text> </b:ImportantField> 
         <b:ImportantField> <xsl:text>b:Year</xsl:text> </b:ImportantField> 
         <b:ImportantField> <xsl:text>b:City</xsl:text> </b:ImportantField> 
         <b:ImportantField> <xsl:text>b:Publisher</xsl:text> </b:ImportantField> 
      </b:ImportantFields> 
   </xsl:template>
   <!--Defines the output format for a simple Book (in the Bibliography) with important fields defined-->
   <xsl:template match="b:Source[b:SourceType = 'Book']">
   <!--Count the number of Corporate Authors (can only be 0 or 1-->
   <xsl:variable name="cCorporateAuthors">
      <xsl:value-of select="count(b:Author/b:Author/b:Corporate)" />
   </xsl:variable>
   <!--Label the paragraph as an Office Bibliography paragraph-->
   <p>
      <xsl:choose>
         <xsl:when test ="$cCorporateAuthors!=0">
         <!--When the corporate author exists display the corporate author-->
            <xsl:value-of select="b:Author/b:Author/b:Corporate"/>
            <xsl:text>. (</xsl:text>
         </xsl:when>
         <xsl:otherwise>
            <!--When the corporate author does not exist, display the normal author-->
            <xsl:value-of select="b:Author/b:Author/b:NameList/b:Person/b:Last"/>
            <xsl:text>, </xsl:text>
            <xsl:value-of select="b:Author/b:Author/b:NameList/b:Person/b:First"/>
            <xsl:text>. (</xsl:text>
         </xsl:otherwise>
      </xsl:choose>
      <xsl:value-of select="b:Year"/>
      <xsl:text>). </xsl:text>
      <i>
         <xsl:value-of select="b:Title"/>
         <xsl:text>. </xsl:text>
      </i> 
         <xsl:value-of select="b:City"/>
         <xsl:text>: </xsl:text>
         <xsl:value-of select="b:Publisher"/>
         <xsl:text>.</xsl:text>
      </p>
   </xsl:template>
   <!--Defines the output of the entire Bibliography-->
   <xsl:template match="b:Bibliography"> 
      <html xmlns="http://www.w3.org/TR/REC-html40"> 
         <body>
            <xsl:apply-templates select ="*">
            </xsl:apply-templates>
         </body>
      </html>
   </xsl:template>
   <!--Defines the output of the Citation-->
   <xsl:template match="b:Citation/b:Source[b:SourceType = 'Book']">
      <html xmlns="http://www.w3.org/TR/REC-html40"> 
         <xsl:variable name="cCorporateAuthors"> 
            <xsl:value-of select="count(b:Author/b:Author/b:Corporate)" /> 
         </xsl:variable> 
         <body> 
         <!--Defines the output format as (Author, Year--> 
            <xsl:text>(</xsl:text>
            <xsl:choose> 
            <!--When the corporate author exists display the corporate author-->
               <xsl:when test ="$cCorporateAuthors!=0">
                  <xsl:value-of select="b:Author/b:Author/b:Corporate"/>
               </xsl:when>
               <!--When the corporate author does not exist, display the normal author-->
               <xsl:otherwise> 
                  <xsl:value-of select="b:Author/b:Author/b:NameList/b:Person/b:Last"/> 
               </xsl:otherwise>
               </xsl:choose>
               <xsl:text>, </xsl:text> 
               <xsl:value-of select="b:Year"/>
               <xsl:text>)</xsl:text> 
            </body> 
         </html>
   </xsl:template>
   <xsl:template match="text()" />
</xsl:stylesheet>
```


## 结论
<a name="Biblio_Conclusion"> </a>

本文介绍了如何创建自定义书目样式中Word，第一次通过创建一个简单的样式，然后通过使用条件语句来创建更复杂的样式。


## 其他资源
<a name="Biblio_AdditionalResources"> </a>


- [Word 中面向开发人员的新增功能](http://msdn.microsoft.com/library/d7de81f7-ac7f-a88c-4765-7e8f8c7df4b4%28Office.15%29.aspx)
    
- [Office and Office 365 Developer Blog](http://blogs.msdn.com/b/officedevdocs/.aspx)
    
- [Word for developers website](http://msdn.microsoft.com/en-us/office/aa905482.aspx.aspx)
    
