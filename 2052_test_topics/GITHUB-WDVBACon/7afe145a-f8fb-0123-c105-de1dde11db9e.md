
# 使用图表

在 Word 2007 Service Pack 2 (SP2) 及更高版本中，可以在 Word 中使用 VBA 对象模型以编程方式访问和处理图表。Word 中的图表对象是通过与 Excel 所用相同的共享 Office 绘图层实现来绘制的，所以如果您熟悉 Excel 中的图表对象模型，则只需将用于处理图表的 Excel VBA 代码迁移到 Word VBA 代码中。


## Chart 对象用法

在 Word 中，图表由 [Chart](366a825e-0daf-dbb7-b6f2-e7ce1a5ee2ef.md) 对象来表示。[Chart](366a825e-0daf-dbb7-b6f2-e7ce1a5ee2ef.md) 对象包含在 InlineShape 或 Shape 中。可以使用[Document](8d83487a-2345-a036-a916-971c9db5b7fb.md) 对象的[InlineShapes](88c632b2-80de-c96a-8879-a98461b38bd0.md) 集合或[Shapes](0907eed3-886e-8e73-0e5e-71f4b37ddd5b.md) 集合来添加新图表或访问现有图表。使用这两个集合的[AddChart](http://msdn.microsoft.com/library/1b168e7b-543a-a817-51b0-8171beecc946%28Office.15%29.aspx) 方法可添加新图表，该方法可指定文档内的图表类型和位置。

可以使用 [HasChart](f8b88eef-ec41-fc03-f58b-e346d240a121.md) 属性来确定[InlineShape](a8fd110a-4aa7-c4b9-1559-32022787d955.md) 对象或[Shape](604029ce-9b2f-9748-5d4e-b458796fa2f0.md) 对象是否包含图表。如果[HasChart](f8b88eef-ec41-fc03-f58b-e346d240a121.md) 返回 True，则可以使用[Chart](33d577fe-58b0-8e2f-a859-5bd3b34ed892.md) 属性获取对代表图表的[Chart](366a825e-0daf-dbb7-b6f2-e7ce1a5ee2ef.md) 对象的引用。此时，实现实际上与 Excel 的实现相同，并且在大多数情况下，VBA 代码可以在这两个程序之间转用。

例如，下面的 VBA 代码示例向 Excel 活动工作表中添加一个新的二维堆积柱形图，并将图表的源数据设置为 Sheet1 工作表中的区域 A1:C3。




```
Sub AddChart_Excel()
    Dim objShape As Shape
    
    ' Create a chart and return a Shape object reference.
    ' The Shape object reference contains the chart.
    Set objShape = ActiveSheet.Shapes.AddChart(XlChartType.xlColumnStacked100)
    
    ' Ensure the Shape object contains a chart. If so,
    ' set the source data for the chart to the range A1:C3.
    If objShape.HasChart Then
        objShape.Chart.SetSourceData Source:=Range("'Sheet1'!$A$1:$C$3")
    End If
End Sub

```

作为比较，下面的 VBA 代码示例向活动文档中添加一个新的二维堆积柱形图，并将图表的源数据设置为与该图表相关联的图表数据中的范围 A1:C3。




```
Sub AddChart_Word()
    Dim objShape As InlineShape
    
    ' Create a chart and return a Shape object reference.
    ' The Shape object reference contains the chart.
    Set objShape = ActiveDocument.InlineShapes.AddChart(XlChartType.xlColumnStacked100)
    
    ' Ensure the Shape object contains a chart. If so,
    ' set the source data for the chart to the range A1:C3.
    If objShape.HasChart Then
        objShape.Chart.SetSourceData Source:="'Sheet1'!$A$1:$C$3"
    End If
End Sub
```


## Word 中 Chart 对象与 Excel 中 ChartObject 对象的重要区别

即使大多数情况下您在 Excel 和 Word 中处理图表的方式几乎相同，但是了解这两种实现在哪些重要方面有所不同还是有帮助的：




- 在 Word 中以编程方式创建或处理 [ChartData](323ee62c-9b70-8280-d448-79cf4d2b6953.md) 对象需要运行 Excel。
    
- 图表属性和操作的图表工作表的方法未实现。图表工作表的概念是特定于 Excel。在 Word 中，不使用图表工作表，因此已禁用这些应用程序方法和属性来引用或操作图表工作表。
    
- 属性和方法，在 Excel 中正常情况下立即采取[区域](http://msdn.microsoft.com/library/b8207778-0dcc-4570-1234-f130532cc8cd%28Office.15%29.aspx)对象引用在 Word 中执行范围地址。Word 中的[Range](15a7a1c4-5f3f-5b6e-60e9-29688de3f274.md)对象是不同于 Excel 中的[Range](http://msdn.microsoft.com/library/b8207778-0dcc-4570-1234-f130532cc8cd%28Office.15%29.aspx)对象。为了防止混淆，在 Word 中的图表对象模型接受范围地址字符串，如"= Sheet1! $A$ 1: $D$ 5"，在这些属性和方法 (如图表对象的[SetSourceData](8c5b056a-6680-7e4e-ce67-a3b76b2d7d25.md)方法) 接受在 Excel 中的 Range 对象。
    
- 新的对象， [ChartData](323ee62c-9b70-8280-d448-79cf4d2b6953.md)，已添加到 Word 提供对图表的基础的链接或嵌入数据访问 VBA 对象模型。每个图表有，与之关联，用于绘制 Word 中的图表的数据。图表数据可以从外部 Excel 工作簿，链接或嵌入图表本身的一部分。[ChartData](323ee62c-9b70-8280-d448-79cf4d2b6953.md)对象封装在 Word 中的给定图表对数据的访问。例如，下面的 VBA 代码示例显示，然后最小化，包含活动文档在 Word 中的每个图表的图表数据。
    



```
Sub ShowWorkbook_Word() 
    Dim objShape As InlineShape 
     
    ' Iterates each inline shape in the active document. 
    ' If the inline shape contains a chart, then display the 
    ' data associated with that chart and minimize the application 
    ' used to display the data. 
    For Each objShape In ActiveDocument.InlineShapes 
        If objShape.HasChart Then 
 
            ' Activate the topmost window of the application used to 
            ' display the data for the chart. 
            objShape.Chart.ChartData.Activate 
             
            ' Minimize the application used to display the data for 
            ' the chart. 
            objShape.Chart.ChartData.Workbook.Application.WindowState = -4140 
        End If 
    Next 
End Sub 
```

