
# 在宏结束时存储值

宏结束时，存储在变量中的值并未自动存储在磁盘上。如果宏需要保留一个值，则必须在宏的执行操作完成以前，将该值保存在宏的外部。本主题介绍了五个易于存储和检索宏值的位置。


## 文档变量

文档变量允许您将值作为文档或模板的一部分存储。例如，您可能在文档或模板宏所在存储宏的值。您可以将变量添加到文档或模板使用 **[变量](9719d0d4-319d-c710-d243-12a9dee45880.md)** 集合的 **[Add](5c38d785-539b-7e6c-9cd0-cfa48e1aef33.md)** 方法。下面的示例将一个文档变量保存在相同的位置 (文档或模板) 时运行的宏使用 **ActiveDocument** 属性。


```
Sub AddDocumentVariable() 
 ActiveDocument.Variables.Add Name:="Age", Value:=12 
End Sub
```

下面的示例使用带有 **[变量](e6a75f54-6f91-75b4-7ca0-9be302e8dbe0.md)** 对象的 **[Value](6a687fff-062a-4e27-abc7-2f49d6f9c76b.md)** 属性返回文档变量的值。




```
Sub UseDocumentVariable() 
 Dim intAge As Integer 
 intAge = ActiveDocument.Variables("Age").Value 
End Sub
```


## 注解

可用 DOCVARIABLE 域将文档变量插入文档。


## 文档属性

与文档变量相同，文档属性允许将值作为文档或模板的一部分存储。可在"属性"对话框中查看文档属性。

Word 对象模型将文档属性分为两组: 内置和自定义。自定义文档属性包括 **属性** 对话框中的 **自定义** 选项卡上显示的属性。内置文档属性包括 **属性** 对话框中除 **自定义** 选项卡中的所有选项卡上的属性。

若要访问内置属性，请使用 **BuiltInDocumentProperties** 属性返回一个 **DocumentProperties** 集合，其中包含内置文档属性。使用 **[文档](8d83487a-2345-a036-a916-971c9db5b7fb.md)** 对象或 **[模板](47d1d92d-bba9-3f2a-9c71-22ac43159bd3.md)** 对象的 **CustomDocumentProperties** 属性返回一个 **DocumentProperties** 集合，其中包含自定义文档属性。下面的示例创建名为"YourName"所在的位置 (文档或模板) 时运行的宏的自定义文档属性。




```
Sub AddCustomDocumentProperties() 
 ActiveDocument.CustomDocumentProperties.Add Name:="YourName", _ 
 LinkToContent:=False, Value:="Joe", Type:=msoPropertyTypeString 
End Sub
```

内置文档属性不能添至  **[Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)** 对象或 **[Template](47d1d92d-bba9-3f2a-9c71-22ac43159bd3.md)** 对象的 **BuiltInDocumentProperties** 属性返回的 **DocumentProperties** 集合。但是，可检索内置文档属性的内容或更改可读写的内置文档属性的值。


## 注解

可用 DOCPROPERTY 域将文档属性插入文档中。


## "自动图文集"词条

自动图文集词条可用于在模板中存储的信息。与文档变量或属性，不同自动图文集词条可包含除宏变量，如已设置格式的文本或图形条目。使用 **[AutoTextEntries](4e4d92b3-d259-84b7-061f-82065e177c29.md)** 集合 **[Add](7ffa87f9-a23c-1847-3907-84c95f2b7f73.md)** 方法来创建新的自动图文集词条。下面的示例创建名为"MyText"的包含所选内容的自动图文集词条。如果下面的指令是模板宏的一部分，新的自动图文集词条将存储在模板中，否则，自动图文集词条不存储指令所驻留的文档选用的模板中。


```
Sub AddAutoTextEntry() 
 ActiveDocument.AttachedTemplate.AutoTextEntries.Add Name:="MyText", _ 
 Range:=Selection.Range 
End Sub
```

对一个 **[AutoTextEntry](37a2cf05-ae07-d411-9bd8-ab4726b303a9.md)** 对象，可以使用 **[Value](0b8b92d7-955b-3f4c-8bbd-924786b3aa9c.md)** 属性来检索的自动图文集条目对象中的内容。


## 设置文件

您可以设置并使用 **[系统](db15d780-3bbc-9515-a988-ea798777496f.md)** 对象的 **[PrivateProfileString](737fb157-4665-5e31-240a-347bd7334005.md)** 属性设置文件中检索信息。Windows 设置文件的结构是相同 Windows 3.1 的赢取。INI 文件。下面的示例设置为 1，Macro.ini 文件中的 DocTracker 部分下的 DocNum 键。


```
Sub MacroSystemFile() 
 System.PrivateProfileString( _ 
 FileName:="C:\My Documents\Macro.ini", _ 
 Section:="DocTracker", Key:="DocNum") = 1 
End Sub
```

运行上述指令后，Macro.ini 文件包含下列文本。




```
[DocTracker] 
DocNum=1
```

 **PrivateProfileString** 属性有三个参数： ** _FileName_** 、 ** _Section_** 和 ** _Key_** 。 ** _FileName_** 参数用于指定设置文件的路径和文件名。 ** _Section_** 参数指定出现在相关的键前面的括号之间的节名（不包括节名的括号）。 ** _Key_** 参数指定其后跟随等号 (=) 和设置的键名。

使用相同的  **PrivateProfileString** 属性可从设置文件中检索设置。下列示例检索 Macro.ini 文件中 DocTracker 节下的 DocNum 设置。




```
Sub GetSystemFileInfo() 
 Dim intDocNum As Integer 
 intDocNum = System.PrivateProfileString( _ 
 FileName:="C:\My Documents\Macro.ini", _ 
 Section:="DocTracker", Key:="DocNum") 
 MsgBox "DocNum is " &amp; intDocNum 
End Sub
```


## Windows 注册表

您可以设置并使用 **PrivateProfileString** 属性的 Windows 注册表中检索信息。下面的示例从 Windows 注册表中检索 Word 2007 程序目录。


```
Sub GetRegistryInfo() 
 Dim strSection As String 
 Dim strPgmDir As String 
 strSection = "HKEY_CURRENT_USER\Software\Microsoft" _ 
 &amp; "\Office\12.0\Word\Options" 
 strPgmDir = System.PrivateProfileString(FileName:="", _ 
 Section:=strSection, Key:="PROGRAMDIR") 
 MsgBox "The directory for Word is - " &amp; strPgmDir 
End Sub
```

 **PrivateProfileString** 属性有三个参数： ** _FileName_** 、 ** _Section_** 和 ** _Key_** 。若要返回或设置注册表项的值，应将 ** _FileName_** 参数指定为空字符串 ("")。 ** _Section_** 参数应为注册表子键的完整路径。 ** _Key_** 参数应为 ** _Section_** 指定的子键中项的名称。

也可用下面的  **PrivateProfileString** 语法在 Windows 注册表中设置信息。

 **System.PrivateProfileString** ( ** _FileName_**, ** _Section_**, ** _Key_** ) **=** _value_

下面的示例将 Windows 注册表中 Office Word 2007 的 Option 子键的 DOC-PATH 项设为"C:\My Documents"。




```
Sub SetDocumentDirectory() 
 Dim strDocDirectory As String 
 strDocDirectory = "HKEY_CURRENT_USER\Software\Microsoft" _ 
 &amp; "\Office\10.0\Word\Options" 
 System.PrivateProfileString(FileName:="", _ 
 Section:=strDocDirectory, Key:="DOC-PATH") = "C:\My Documents" 
End Sub
```

