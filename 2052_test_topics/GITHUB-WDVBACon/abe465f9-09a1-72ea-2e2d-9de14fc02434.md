
# 显示内置 Word 对话框

本主题包含下列信息和示例：


- [显示内置对话框](#item1)
    
- [返回和更改对话框设置](#item2)
    
- [检查对话框的关闭方式](#item4)
    

## 显示内置对话框

您可以显示内置对话框来获取用户输入或者控制 Word 使用 Visual Basic for Applications (VBA)。 **[Show](6b236493-342d-934b-f360-00b7846789e8.md)** **[Dialog](f90f6e6d-aaa0-c127-ab37-ca074144eff1.md)** 对象的方法将显示并执行在内置的 Word 对话框中执行的任何操作。若要访问特定的内置 Word 对话框中，指定 **[对话框的](17acdfab-32d2-ddb8-04aa-692f9ffb20b8.md)** 属性 **[WdWordDialog](af867214-8cd6-0477-6c88-2efe06589730.md)** 常量。例如，下列宏指令显示 **打开** 对话框中 ( **wdDialogFileOpen** )。


```
Sub ShowOpenDialog() 
 Dialogs(wdDialogFileOpen).Show 
End Sub
```

选定文件并单击 **"确定"** 按钮后，会将文件打开（执行操作）。下列示例显示 **"打印"** 对话框 ( **wdDialogFilePrint** )。




```
Sub ShowPrintDialog() 
 Dialogs(wdDialogFilePrint).Show 
End Sub
```

设置 **[DefaultTab](22de708e-fb23-b27a-00f0-dc43787c7eaf.md)** 属性来访问特定选项卡的 Word 对话框中。下面的示例显示 **边框和底纹** 对话框中的 **页面边框** 选项卡。




```
Sub ShowBorderDialog() 
 With Dialogs(wdDialogFormatBordersAndShading) 
 .DefaultTab = wdDialogFormatBordersAndShadingTabPageBorder 
 .Show 
 End With 
End Sub
```


 **注释**  您可以使用 Word 中的 VBA 属性而不会显示对话框中显示的用户信息。下面的示例使用 **[应用程序](d1cf6f8f-4e88-bf01-93b4-90a83f79cb44.md)** 对象的 **[用户名](96f5ffb6-a20d-96f0-e3a4-0ad2dd47bf99.md)** 属性而不会显示 **用户信息** 对话框中显示的应用程序的用户名。




```
Sub DisplayUserInfo() 
 MsgBox Application.UserName 
End Sub
```

如果用户名已更改上面的示例中，在对话框中未设置更改。使用 **[Execute](7f7dce3a-40ef-988c-f5ea-06a25c0ccc4b.md)** 方法来执行对话框中的设置而不显示对话框。下面的示例显示 **用户信息** 对话框，如果名称不是空字符串，设置在对话框中使用 **Execute** 方法。




```
Sub ShowAndSetUserInfoDialogBox() 
 With Dialogs(wdDialogToolsOptionsUserInfo) 
 .Display 
 If .Name <> "" Then .Execute 
 End With 
End Sub
```


 **注释**  使用 Word 中的 VBA 属性和方法可设置用户信息而不显示对话框。下列代码示例通过  **Application** 对象的 **UserName** 属性更改用户名称，然后显示 **"用户信息"** 对话框，显示已进行了更改。请注意，更改对话框值时，并非必须显示对话框。




```
Sub SetUserName() 
 Application.UserName = "Jeff Smith" 
 Dialogs(wdDialogToolsOptionsUserInfo).Display 
End Sub
```


## 返回和更改对话框设置

如果可以使用属性或方法返回或更改对话框值，则使用  **Dialog** 对象返回或更改该值并非一种很有效的方法。而且在大多数情况下（如果不是在所有情况下），当使用 VBA 代码来代替访问 **Dialog** 对象时，代码更简单、更简短。因此，下面的示例同时包含使用相应的 VBA 属性执行相同任务的示例。

返回或更改对话框设置使用 **[Dialog](f90f6e6d-aaa0-c127-ab37-ca074144eff1.md)** 对象之前, 您需要识别单独的对话框。这是一个 **[WdWordDialog](af867214-8cd6-0477-6c88-2efe06589730.md)** 常量中使用 **[对话框的](1c734758-d00a-31d7-d4f7-bf8c406ab6b5.md)** 属性。已实例化 **Dialog** 对象后，可以返回或设置在对话框中的选项。下面的示例显示 **段落** 对话框的右缩进。




```
Sub ShowRightIndent() 
 Dim dlgParagraph As Dialog 
 Set dlgParagraph = Dialogs(wdDialogFormatParagraph) 
 MsgBox "Right indent = " &amp; dlgParagraph.RightIndent 
End Sub
```


 **注释**  可以使用的 VBA 属性和方法的 Word 显示段落的右缩进设置。下面的示例使用 **[ParagraphFormat](712d754a-dc92-f1a3-531d-dfae74a42c23.md)** 对象的 **[RightIndent](de69209e-d88d-d367-9d84-94faa07a30bd.md)** 属性以显示在插入点位置的段落的右缩进。




```
Sub ShowRightIndexForSelectedParagraph() 
 MsgBox Selection.ParagraphFormat.RightIndent 
End Sub
```

类似于返回对话框值，您也可以设置对话框值。下列示例标记 **"段落"** 对话框中的 **"与下段同页"** 复选框。




```
Sub SetKeepWithNext() 
 With Dialogs(wdDialogFormatParagraph) 
 .KeepWithNext = 1 
 .Execute 
 End With 
End Sub
```


 **注释**  您还可以使用 VBA 属性和方法来更改段落的右缩进。下面的示例使用 **ParagraphFormat** 对象的 **[KeepWithNext](5fc8ad97-d839-7837-04c7-dac2efe1d1c2.md)** 属性需要与下面的段落中将所选的段落。




```
Sub SetKeepWithNextForSelectedParagraph() 
 Selection.ParagraphFormat.KeepWithNext = True 
End Sub
```


 **注释**  使用 **[Update](7adf7403-77eb-85da-8a5a-092d1c8c548f.md)** 方法以确保对话框的值反映的当前值。它可能需要如果先前在宏中定义了对话框变量，之后想恢复或更改当前设置，请使用 **Update** 方法。


## 检查对话框的关闭方式

 **Show** 和 **Display** 方法返回的值标明关闭对话框时单击的按钮。下列示例显示 **"分隔符"** 对话框，如果单击 **"确定"** 按钮，则在状态栏中显示一条消息。


```
Sub DialogBoxButtons() 
 If Dialogs(wdDialogInsertBreak).Show = -1 Then 
 StatusBar = "Break inserted" 
 End If 
End Sub
```

下面的表格说明了与对话框中的按钮关联的返回值。



|**返回值**|**说明**|
|:-----|:-----|
|-2|"关闭"按钮。|
|-1|"确定"按钮。|
|0（零）|"取消"按钮。|
|> 0（零）|命令按钮：1 代表第一个按钮，2 代表第二个按钮，依此类推。|
