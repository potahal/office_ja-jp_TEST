
# InsideHeight 属性

使用  **InsideHeight** 属性和 **InsideWidth** 属性可以确定包含窗体的窗口的高度和宽度（以缇为单位）。 **Long** 型，可读写。


## 语法

 _表达式_. **InsideHeight**

 _表达式_ 一个代表 **Form** 对象的变量。


## 注解

如果要确定窗体本身的内部尺寸，可以使用  **Width** 属性来确定窗体的宽度，并使用窗体的可见部分的高度之和来确定窗体的高度（ **Height** 属性只应用于窗体的各个部分，而不是窗体）。窗体的内部是指窗体内的区域，不包括滚动条和记录选择器。

也可以使用  **WindowHeight** 和 **WindowWidth** 属性来确定包含窗体的窗口的高度和宽度。

如果一个窗口处于最大化状态，那么对这些属性进行的设置只有在窗口恢复为正常大小时才起作用。


## 示例

下面的示例显示如何使用  **InsideHeight** 属性和 **InsideWidth** 属性对窗体的内部高度、内部宽度和包含窗体的窗口的高度、宽度进行比较。如果窗口与窗体大小不相等，则重新设置窗口的大小，使其与窗体的高度和宽度相一致。


```
Sub ResetWindowSize(frm As Form)
    Dim intWindowHeight As Integer
    Dim intWindowWidth As Integer
    Dim intTotalFormHeight As Integer
    Dim intTotalFormWidth As Integer
    Dim intHeightHeader As Integer
    Dim intHeightDetail As Integer
    Dim intHeightFooter As Integer

    ' Determine form's height.
    intHeightHeader = frm.Section(acHeader).Height
    intHeightDetail = frm.Section(acDetail).Height
    intHeightFooter = frm.Section(acFooter).Height
    intTotalFormHeight = intHeightHeader _
        + intHeightDetail + intHeightFooter
    ' Determine form's width.
    intTotalFormWidth = frm.Width
    ' Determine window's height and width.
    intWindowHeight = frm.InsideHeight
    intWindowWidth = frm.InsideWidth

    If intWindowWidth <> intTotalFormWidth Then
        frm.InsideWidth = intTotalFormWidth
    End If
    If intWindowHeight <> intTotalFormHeight Then
        frm.InsideHeight = intTotalFormHeight
    End If
End Sub
```

