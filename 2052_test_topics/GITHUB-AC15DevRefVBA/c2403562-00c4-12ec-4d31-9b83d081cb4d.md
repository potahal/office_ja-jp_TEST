
# ListBox.ItemsSelected 属性 (Access)

可以使用  **ItemsSelected** 属性返回对隐藏的 **ItemsSelected** 集合的只读引用。此隐藏集合可用于访问多选列表框控件中选定行中的数据。


## 语法

 _表达式_. **ItemsSelected**

 _表达式_ 一个代表 **ListBox** 对象的变量。


## 注解

 **ItemsSelected** 集合与其他集合的不同之处在于，它是 **Variants** 集合，而不是对象的集合。每个 **Variant** 都是一个整型索引，引用列表框或组合框中选定的行。

将  **ItemsSelected** 集合与 **Column** 属性或 **ItemData** 属性结合使用，可以从列表框或组合框的选定行中检索数据。可以使用 **For Each...Next** 语句列出 **ItemsSelected** 集合。

例如，如果窗体上有一个"雇员"列表框，则可以列出  **ItemsSelected** 集合，并使用控件的 **ItemData** 属性返回列表框中每个选定行的绑定列的值。

要启用列表框中行的多选功能，请将控件的  **MultiSelect** 属性设置为"简单"或"展开的"。

 **ItemsSelected** 集合没有方法，只有两个属性： **Count** 和 **Item** 属性。


## 示例

下面的示例打印 Contacts 窗体上 Names 列表框中每个选定行的绑定列的值。要试用此示例，请创建该列表框，根据需要设置其  **BoundColumn** 属性并将其 **MultiSelect** 属性设置为"简单"或"展开的"。然后再切换到"窗体"视图，在列表框中选择几行，并运行以下代码：


```
Sub BoundData() 
 Dim frm As Form, ctl As Control 
 Dim varItm As Variant 
 
 Set frm = Forms!Contacts 
 Set ctl = frm!Names 
 For Each varItm In ctl.ItemsSelected 
 Debug.Print ctl.ItemData(varItm) 
 Next varItm 
End Sub
```

下一个示例使用相同的列表框控件，但打印列表框中每个选定行每一列的值，而不只是绑定列中的值。




```
Sub AllSelectedData() 
 Dim frm As Form, ctl As Control 
 Dim varItm As Variant, intI As Integer 
 
 Set frm = Forms!Contacts 
 Set ctl = frm!Names 
 For Each varItm In ctl.ItemsSelected 
 For intI = 0 To ctl.ColumnCount - 1 
 Debug.Print ctl.Column(intI, varItm) 
 Next intI 
 Debug.Print 
 Next varItm 
End Sub
```


## 另请参阅


#### 概念


[列表框对象](6bc00755-34e7-4fc2-8e72-40dae2010dd8.md)
#### 其他资源


[列表框对象成员](d87ad51b-9a46-21f3-f6d6-ef98ea8aaf6d.md)