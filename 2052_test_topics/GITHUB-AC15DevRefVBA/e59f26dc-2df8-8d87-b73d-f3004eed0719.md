
# DoCmd.TransferText 方法 (Access)

在 Visual Basic 中， **TransferText** 方法执行 **TransferText** 操作。


## 语法

 _表达式_. **TransferText**( ** _TransferType_**, ** _SpecificationName_**, ** _TableName_**, ** _FileName_**, ** _HasFieldNames_**, ** _HTMLTableName_**, ** _CodePage_** )

 _表达式_ 一个代表 **DoCmd** 对象的变量。


### 参数



|**名称**|**必需/可选**|**数据类型**|**说明**|
|:-----|:-----|:-----|:-----|
| _TransferType_|可选|**[AcTextTransferType](56aeb63e-e249-9267-3184-9a370f766fd2.md)**|传输的类型。对带分隔符的文本文件或固定宽度的文本文件或 HTML 文件中的数据可以进行导入、导出或链接操作。默认值为  **acImportDelim** 。在 Microsoft Access 项目 (.adp) 中仅支持 **acImportDelim** 、 **acImportFixed** 、 **acExportDelim** 、 **acExportFixed** 或 **acExportMerge** 传输类型。|
| _SpecificationName_|可选|**Variant**|字符串表达式，表示在当前数据库中创建并保存的导入或导出规范的名称。对于固定长度的文本文件，必须指定参数或使用 schema.ini 文件，该文件还必须保存在导入、链接或导出的文本文件的同一个文件夹中。若要创建一个方案文件，可使用文本导入/导出向导创建此文件。对于分隔的文本文件和 Microsoft Word 邮件合并数据文件，可以将该参数保留为空，以便选择默认的导入/导出规范。|
| _TableName_|可选|**Variant**|字符串表达式，表示要向其导入文本数据、从中导出文本数据或链接文本数据的 Microsoft Access 表的名称，或者要将其结果导出到文本文件的 Microsoft Access 查询的名称。|
| _FileName_|可选|**Variant**|字符串表达式，表示要从中导入、导出到或链接到的文本文件的完整名称（包括路径）。|
| _HasFieldNames_|可选|**Variant**|使用  **True** (?1) 将在导入、导出或链接时，使用文本文件中的第一行作为字段名。使用 **False** (0) 会将文本文件中的第一行看成普通数据。如果将该参数保留为空，将采用默认值 ( **False** )。对于 Microsoft Word 邮件合并数据文件（这些文件的第一行中必须始终包含字段名），将参数将被忽略。|
| _HTMLTableName_|可选|**Variant**|字符串表达式，表示要导入或链接的 HTML 文件中的表或列表的名称。除非  _TransferType_ 参数设为 **acImportHTML** 或 **acLinkHTML** ，否则该参数将被忽略。如果将该参数保留为空，则导入或链接 HTML 文件中的第一个表或列表。如果 HTML 文件中存在 **<CAPTION>** 标记，则 HTML 文件的表或列表名称由 **<CAPTION>** 标记所指定的文本决定。如果没有 **<CAPTION>** 标记，则名称由 **<TITLE>** 标记指定的文本决定。如果有多个表或列表具有相同的名称，则 Microsoft Access 将通过向每个表或列表名称的末尾添加一个数字（如 Employees1 和 Employees2）来区分它们。|
|代码页[](https://msdn.microsoft.com/zh-cn/library/windows/desktop/dd317756 %28v=vs.85%29.aspx)|可选|**Variant**|**Long** 型值，用于标识代码页的字符集。|

## 注解

用  **TransferText** 方法可以在当前的 Microsoft Access 数据库或 Microsoft Access 项目 (.adp) 与文本文件之间导入或导出文本。还可以将文本文件中的数据链接到当前的 Access 数据库中。通过链接的文本文件，在仍然允许字处理程序完全访问该文本文件中的数据的同时，还可以用 Access 来查看这些数据。也可以导入、导出或链接到 HTML 文件 (*.html) 中的表或列表。

可以将 Access 选择查询中的数据导出到文本文件中。Access 将像导出表一样导出查询的结果集。


## 示例

下面的示例使用规范"标准输出"，从 Microsoft Access 表"外部报表" 将数据导出到带有分隔符的文本文件 April.doc 中：


```
DoCmd.TransferText acExportDelim, "Standard Output", _ 
    "External Report", "C:\Txtfiles\April.doc"
```

下面的代码演示如何创建新的 Microsoft Word 文档和使用客户表中存储的数据执行邮件合并。

 **示例代码提供者：**[Microsoft Access 2010 程序员参考](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl)




```
Public Sub DoMailMerge(strFileSavePath As String)

    ' Create new Word App, add a document and set it visible
    Dim wdApp As New Word.Application
    wdApp.Documents.Add
    wdApp.Visible = True

    ' Open the data set from this database
    wdApp.ActiveDocument.MailMerge.OpenDataSource _
        Name:=Application.CurrentProject.FullName, _
        OpenExclusive:=False, _
        LinkToSource:=True, _
        Connection:="TABLE Customers", _
        SQLStatement:="SELECT Customers.* FROM Customers;"
              
    ' Add fields to the mail merge document
    Dim oSel As Object
    Set oSel = wdApp.Selection
    With wdApp.ActiveDocument.MailMerge.Fields
    
        oSel.TypeText vbNewLine &amp; vbNewLine
        .Add oSel.range, "First_Name"
        oSel.TypeText " "
        .Add oSel.range, "Last_Name"
        oSel.TypeText vbNewLine
        .Add oSel.range, "Company"
        oSel.TypeText vbNewLine
        .Add oSel.range, "Address"
        oSel.TypeText vbNewLine
        .Add oSel.range, "City"
        oSel.TypeText ", "
        .Add oSel.range, "State"
        oSel.TypeText " "
        .Add oSel.range, "Zip"
        oSel.TypeText vbNewLine
        oSel.TypeParagraph
        oSel.TypeText "Dear "
        .Add oSel.range, "First_Name"
        oSel.TypeText ","
        oSel.TypeText vbNewLine
        oSel.TypeParagraph
        oSel.TypeText "We have created this mail just for you..."
        oSel.TypeText vbNewLine
        oSel.TypeText vbNewLine
        oSel.TypeText "Sincerely," &amp; vbNewLine &amp; "John Q. Public"
        oSel.TypeText vbFormFeed
        
    End With
    
    ' Execute the mail merge and save the document
    wdApp.ActiveDocument.MailMerge.Execute
    wdApp.ActiveDocument.SaveAs strFileSavePath
        
    ' Close everything and Cleanup Variables
    Set oSel = Nothing
    wdApp.ActiveDocument.Close False
    Set wdApp = Nothing

End Sub
```


## 关于参与者
<a name="AboutContributors"> </a>

Wrox Press 秉承程序员到程序员的理念。Wrox 书籍由程序员为程序员编写，Wrox 品牌意味着对实际编程问题的权威解决方案。


## 另请参阅
<a name="AboutContributors"> </a>


#### 概念


[DoCmd 对象](3ce44cca-9979-0a1e-9787-079a52ce528f.md)
#### 其他资源


[DoCmd 对象成员](3e7ade9e-86e4-0751-188b-5d31c9101651.md)