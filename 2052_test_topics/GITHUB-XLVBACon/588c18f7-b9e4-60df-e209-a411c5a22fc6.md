
# 在某一 Microsoft Office 应用程序中控制其他 Microsoft Office 应用程序

如果要在一个 Microsoft Office 应用程序中运行使用另一个应用程序中对象的代码，请按照下列步骤操作。


### 运行代码


1. 在 **引用** 对话框 ( **工具** 菜单) 中设置其他应用程序的类型库的引用。然后，对象、 属性和方法将显示在对象浏览器，并在编译时将检查语法。此外可以对这些区分上下文的帮助。
    
2. 声明对象变量将引用在其他应用程序中为特定类型的对象。有资格提供对象的应用程序的每种类型的名称。例如，下面的语句声明的变量指向 Microsoft Word 文档，另一个是指 Microsoft Excel 工作簿。
    
  ```
  Dim appWD As Word.Application, wbXL As Excel.Workbook
  ```


     **注释**  如果希望代码早被绑定，则必须遵循前面的步骤。
3.  **使用与您要使用的其他应用程序，如下面的示例中所示的对象的[OLE 程序标识符](http://msdn.microsoft.com/library/9d3418b1-cf9e-4c4d-c387-07952f41608e%28Office.15%29.aspx)。** 若要查看另一个应用程序会话，设置为 **True** 的 **Visible** 属性。
    
  ```
  Dim appWD As Word.Application 
 
Set appWD = CreateObject("Word.Application") 
appWd.Visible = True
  ```

4. 对包含在变量中的对象应用属性和方法。例如，下述指令创建新的 Word 文档。
    
  ```
  Dim appWD As Word.Application 
 
Set appWD = CreateObject("Word.Application") 
appWD.Documents.Add
  ```

5. 在完成的使用另一个应用程序，使用 **Quit** 方法并将其关闭，然后将其对象变量设置为 **Nothing** 以释放任何内存时它使用，如下面的示例中所示。
    
  ```
  appWd.Quit 
Set appWd = Nothing
  ```

 **示例提供的代码:** Bill Jelen，[MrExcel.com](http://www.mrexcel.com/)
下面的代码示例电子表格中创建新的 Microsoft Office Word 文件的每行数据。



```
' You must pick Microsoft Word Object Library from Tools>References
' in the VB editor to execute Word commands.
Sub ControlWord()
    Dim appWD As Word.Application
    ' Create a new instance of Word and make it visible
    Set appWD = CreateObject("Word.Application.12")
    appWD.Visible = True

    ' Find the last row with data in the spreadsheet
    FinalRow = Range("A9999").End(xlUp).Row
    For i = 1 To FinalRow
        ' Copy the current row
        Worksheets("Sheet1").Rows(i).Copy
        ' Tell Word to create a new document
        appWD.Documents.Add
        ' Tell Word to paste the contents of the clipboard into the new document.
        appWD.Selection.Paste
        ' Save the new document with a sequential file name.
        appWD.ActiveDocument.SaveAs Filename:="File" &amp; i
        ' Close the new Word document.
        appWD.ActiveDocument.Close
    Next i
    ' Close the Word application.
    appWD.Quit
End Sub

```

 **示例提供的代码:** 金士 Wallentin，[VSTO 和.NET 和 Excel](http://xldennis.wordpress.com/)
本示例采用命名区域，  **W_Data** ，它包含三个值，将这些值插入到 Word 文档中的单元值。将值插入名为 **td1** 和 **td2** ， **td3** 用书签标记的位置。
若要运行此示例，必须具有一个名为 **W_Data** ，其中包含工作簿中 **工作表 Sheet1** 上的三个值的范围。您必须具有名为 **Test.docx** 保存在 Excel 工作簿中，相同的位置中的 Word 文档和 Word 文档必须具有名为 **td1** 和 **td2** ， **td3** 三个书签。



```
' You must pick Microsoft Word Object Library from Tools>References
' in the Visual Basic editor to execute Word commands.

Option Explicit

Sub Add_Single_Values_Word()
Dim wdApp As Word.Application
Dim wdDoc As Word.Document
Dim wdRange1 As Word.Range
Dim wdRange2 As Word.Range
Dim wdRange3 As Word.Range

Dim wbBook As Workbook
Dim wsSheet As Worksheet

Dim vaData As Variant

Set wbBook = ThisWorkbook
Set wsSheet = wbBook.Worksheets("Sheet1")

vaData = wsSheet.Range("W_Data").Value

' Instatiate the Word Objects.
Set wdApp = New Word.Application
Set wdDoc = wdApp.Documents.Open(wbBook.Path &amp; "\Test.docx")

With wdDoc
    Set wdRange1 = .Bookmarks("td1").Range
    Set wdRange2 = .Bookmarks("td2").Range
    Set wdRange3 = .Bookmarks("td3").Range
End With

' Write values to the bookmarks.
wdRange1.Text = vaData(1, 1)
wdRange2.Text = vaData(2, 1)
wdRange3.Text = vaData(3, 1)

With wdDoc
    .Save
    .Close
End With

wdApp.Quit

' Release the objects from memory.
Set wdRange1 = Nothing
Set wdRange2 = Nothing
Set wdRange3 = Nothing
Set wdDoc = Nothing
Set wdApp = Nothing

End Sub
```


## 关于参与者
<a name="AboutContributor"> </a>

MVP Bill Jelen 是二十多部关于 Microsoft Excel 书籍的作者。他是 Leo Laporte 的 TechTV 节目的常客，也是包括 300,000 多个关于 Excel 的问题和解答的 MrExcel.com 网站的主办人。

Dennis Wallentin 是 VSTO &amp; .NET &amp; Excel（专门介绍适用于 Excel 和 Excel Services 的 .NET Framework 解决方案的博客）的作者。二十多年来，Dennis 一直在开发 Excel 解决方案，并且他还是 "Professional Excel Development: The Definitive Guide to Developing Applications Using Microsoft Excel, VBA and .NET (2nd Edition)" 的合著作者。

