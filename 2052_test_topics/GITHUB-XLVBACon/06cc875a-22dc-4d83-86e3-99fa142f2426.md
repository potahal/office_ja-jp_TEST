
# 基于单元格中的数据的内容表格创建一个 HTML 文件

此代码示例演示如何将从工作表中的数据以及如何在 HTML 文件中创建的目录。工作表应该在第一个，相对应的 A、 B 和 C 列中的数据目录中的目录层次结构的第二和第三个级别。HTML 文件存储在相同的工作文件夹为活动工作簿中。

 **示例提供的代码:** 神圣的宏!书中，[神圣宏!它是 2500 Excel VBA 示例](http://www.mrexcel.com/store/index.php?l=product_detail&amp;p=1)



```
Sub CreateHTML()
   'Define your variables.
   Dim iRow As Long
   Dim iStage As Integer
   Dim iCounter As Integer
   Dim iPage As Integer
   
   'Create an .htm file in the same directory as your active workbook.
   Dim sFile As String
   sFile = ActiveWorkbook.Path &amp; "\test.htm"
   Close
   
   'Open up the temp HTML file and format the header.
   Open sFile For Output As #1
   Print #1, "<html>"
   Print #1, "<head>"
   Print #1, "<style type=""text/css"">"
   Print #1, "  body { font-size:12px;font-family:tahoma } "
   Print #1, "</style>"
   Print #1, "</head>"
   Print #1, "<body>"
   
   'Start on the 2nd row to avoid the header.
   iRow = 2
   
   'Translate the first column of the table into the first level of the hierarchy.
   Do While WorksheetFunction.CountA(Rows(iRow)) > 0
      If Not IsEmpty(Cells(iRow, 1)) Then
         For iCounter = 1 To iStage
            Print #1, "</ul>"
            iStage = iStage - 1
         Next iCounter
         Print #1, "<ul>"
         Print #1, "<li><a href=""" &amp; iPage &amp; ".html"">" &amp; Cells(iRow, 1).Value &amp; "</a>"
         iPage = iPage + 1
         If iStage < 1 Then
            iStage = iStage + 1
         End If
      End If
      
    'Translate the second column of the table into the second level of the hierarchy.
      If Not IsEmpty(Cells(iRow, 2)) Then
         For iCounter = 2 To iStage
            Print #1, "</ul>"
            iStage = iStage - 1
         Next iCounter
         Print #1, "<ul>"
         Print #1, "<li><a href=""" &amp; iPage &amp; ".html"">" &amp; Cells(iRow, 2).Value &amp; "</a>"
         iPage = iPage + 1
         If iStage < 2 Then
            iStage = iStage + 1
         End If
      End If
      
      'Translate the third column of the table into the third level of the hierarchy.
      If Not IsEmpty(Cells(iRow, 3)) Then
         If iStage < 3 Then
            Print #1, "<ul>"
         End If
         Print #1, "<li><a href=""" &amp; iPage &amp; ".html"">" &amp; Cells(iRow, 3).Value &amp; "</a>"
         iPage = iPage + 1
         If iStage < 3 Then
            iStage = iStage + 1
         End If
      End If
      iRow = iRow + 1
   Loop
   
   'Add ending HTML tags
   For iCounter = 2 To iStage
      Print #1, "    </ul>"
      iStage = iStage - 1
   Next iCounter
   Print #1, "</body>"
   Print #1, "</html>"
   Close
   Shell "hh " &amp; vbLf &amp; sFile, vbMaximizedFocus
End Sub
```


## 有关参与者
<a name="AboutContributor"> </a>

Holy Macro! Books 主要出版娱乐书籍，供使用 Microsoft Office 的人员阅读。单击 MrExcel.com，查看完整的目录。

