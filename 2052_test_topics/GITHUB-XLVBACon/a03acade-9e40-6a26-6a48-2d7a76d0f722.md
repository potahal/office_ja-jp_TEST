
# 引用行和列

可用  **Rows** 属性或 **Columns** 属性来处理整行或整列。这两个属性返回代表单元格区域的 **Range** 对象。在下例中， `Rows(1)` 返回 Sheet1 上的第一行，然后将区域的 **Font** 对象的 **Bold** 属性设置为 **True** 。


```
Sub RowBold() 
    Worksheets("Sheet1").Rows(1).Font.Bold = True 
End Sub
```


下表举例说明了使用  **Rows** 和 **Columns** 属性的一些行和列的引用。



|**引用**|**含义**|
|:-----|:-----|
| `Rows(1)`|第一行|
| `Rows`|工作表上所有的行|
| `Columns(1)`|第一列|
| `Columns("A")`|第一列|
| `Columns`|工作表上所有的列|
若要同时处理若干行或列，请创建一个对象变量并使用  **Union** 方法，将对 **Rows** 属性或 **Columns** 属性的多个调用组合起来。下例将活动工作簿中第一张工作表上的第一行、第三行和第五行的字体设置为加粗。



```
Sub SeveralRows() 
    Worksheets("Sheet1").Activate 
    Dim myUnion As Range 
    Set myUnion = Union(Rows(1), Rows(3), Rows(5)) 
    myUnion.Font.Bold = True 
End Sub
```

 **示例提供的代码:** 金士 Wallentin，[VSTO 和.NET 和 Excel](http://xldennis.wordpress.com/)
本示例从选定区域删除为空的行。



```
Sub Delete_Empty_Rows()
    'The range from which to delete the rows.
    Dim rnSelection As Range
    
    'Row and count variables used in the deletion process.
    Dim lnLastRow As Long
    Dim lnRowCount As Long
    Dim lnDeletedRows As Long
    
    'Initialize the number of deleted rows.
    lnDeletedRows = 0
    
    'Confirm that a range is selected, and that the range is contiguous.
    If TypeName(Selection) = "Range" Then
        If Selection.Areas.Count = 1 Then
            
            'Initialize the range to what the user has selected, and initialize the count for the upcoming FOR loop.
            Set rnSelection = Application.Selection
            lnLastRow = rnSelection.Rows.Count
        
            'Start at the bottom row and work up: if the row is empty then
            'delete the row and increment the deleted row count.
            For lnRowCount = lnLastRow To 1 Step -1
                If Application.CountA(rnSelection.Rows(lnRowCount)) = 0 Then
                    rnSelection.Rows(lnRowCount).Delete
                    lnDeletedRows = lnDeletedRows + 1
                End If
            Next lnRowCount
        
            rnSelection.Resize(lnLastRow - lnDeletedRows).Select
         Else
            MsgBox "Please select only one area.", vbInformation
         End If
    Else
        MsgBox "Please select a range.", vbInformation
    End If
    
    'Turn screen updating back on.
    Application.ScreenUpdating = True

End Sub
```

本示例删除选定区域中的空列。



```
Sub Delete_Empty_Columns()
    'The range from which to delete the columns.
    Dim rnSelection As Range
    
    'Column and count variables used in the deletion process.
    Dim lnLastColumn As Long
    Dim lnColumnCount As Long
    Dim lnDeletedColumns As Long
    
    lnDeletedColumns = 0
    
    'Confirm that a range is selected, and that the range is contiguous.
    If TypeName(Selection) = "Range" Then
        If Selection.Areas.Count = 1 Then
            
            'Initialize the range to what the user has selected, and initialize the count for the upcoming FOR loop.
            Set rnSelection = Application.Selection
            lnLastColumn = rnSelection.Columns.Count
        
            'Start at the far-right column and work left: if the column is empty then
            'delete the column and increment the deleted column count.
            For lnColumnCount = lnLastColumn To 1 Step -1
                If Application.CountA(rnSelection.Columns(lnColumnCount)) = 0 Then
                    rnSelection.Columns(lnColumnCount).Delete
                    lnDeletedColumns = lnDeletedColumns + 1
                End If
            Next lnColumnCount
    
            rnSelection.Resize(lnLastColumn - lnDeletedColumns).Select
        Else
            MsgBox "Please select only one area.", vbInformation
        End If
    Else
        MsgBox "Please select a range.", vbInformation
    End If
    
    'Turn screen updating back on.
    Application.ScreenUpdating = True

End Sub
```


## 有关参与者
<a name="AboutContributor"> </a>

Dennis Wallentin 是 VSTO &amp; .NET &amp; Excel（专门介绍适用于 Excel 和 Excel Services 的 .NET Framework 解决方案的博客）的作者。二十多年来，Dennis 一直在开发 Excel 解决方案，并且他还是 "Professional Excel Development: The Definitive Guide to Developing Applications Using Microsoft Excel, VBA and .NET (2nd Edition)" 的合著作者。

