
# 删除某一范围中的重复条目

下面的示例演示如何删除重复项并采取 A 列中的数据的范围。此示例使用 **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** 对象的 **[AdvancedFilter](fe1a19fc-ab0f-6149-25d9-6102d5789757.md)** 方法与 _Unique_参数等于 **True**获取数据的唯一列表。 _Action_参数等于 **xlFilterInPlace**，指定对数据进行筛选到位。如果您想要保留您的原始数据，将 _Action_参数设置为 **xlFilterCopy** ，并指定您希望筛选的数据复制在 _CopyToRange_参数中的位置。一旦筛选唯一值，本示例使用 **Range** 对象的 **[SpecialCells](30c2035c-34e3-3b1a-f243-69a9fed97f3b.md)** 方法来查找任何剩余空白的行并将它们删除。

 **示例提供的代码:** Tom Urtis，[Atlas 编程管理](http://www.atlaspm.com/)



```
Sub DeleteDuplicates()
    With Application
        ' Turn off screen updating to increase performance
        .ScreenUpdating = False
        Dim LastColumn As Integer
        LastColumn = Cells.Find(What:="*", After:=Range("A1"), SearchOrder:=xlByColumns, SearchDirection:=xlPrevious).Column + 1
        With Range("A1:A" &amp; Cells(Rows.Count, 1).End(xlUp).Row)
            ' Use AdvanceFilter to filter unique values
            .AdvancedFilter Action:=xlFilterInPlace, Unique:=True
            .SpecialCells(xlCellTypeVisible).Offset(0, LastColumn - 1).Value = 1
            On Error Resume Next
            ActiveSheet.ShowAllData
            'Delete the blank rows
            Columns(LastColumn).SpecialCells(xlCellTypeBlanks).EntireRow.Delete
            Err.Clear
        End With
        Columns(LastColumn).Clear
        .ScreenUpdating = True
    End With
End Sub
```


## 有关参与者
<a name="AboutContributor"> </a>

MVP Tom Urtis 是 Atlas Programming Management（提供 Microsoft Office 和 Excel 业务解决方案全面服务的公司，位于硅谷）的创始人。Tom 在业务管理和开发 Microsoft Office 应用程序方面拥有超过 25 年的经验，并协助编写了"Holy Macro! It's 2,500 Excel VBA Examples" 。

