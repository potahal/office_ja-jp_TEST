
# 将目录添加到工作簿

以下示例显示了将目录添加到 Excel 工作簿的不同方法。 


 **示例代码提供者：** Dennis Wallentin，[VSTO &amp; .NET &amp; Excel](http://xldennis.wordpress.com/)

本示例使用 [Pages.Count Property (Excel)](8057db6b-1f13-3950-5508-0bbcec892359.md) 属性计算每个表上的页数。此外，TOC 中的条目会链接到相应的表以改进屏幕上的工作簿导航。




```
Option Explicit 
Sub Create_TOC() 
Dim wbBook As Workbook 
Dim wsActive As Worksheet 
Dim wsSheet As Worksheet 
Dim lnRow As Long 
Dim lnPages As Long 
Dim lnCount As Long 
Set wbBook = ActiveWorkbook 
With Application 
    .DisplayAlerts = False 
    .ScreenUpdating = False 
End With 
'If the TOC sheet already exist delete it and add a new 
'worksheet. 
On Error Resume Next 
With wbBook 
    .Worksheets("TOC").Delete 
    .Worksheets.Add Before:=.Worksheets(1) 
End With 
On Error GoTo 0 
Set wsActive = wbBook.ActiveSheet 
With wsActive 
    .Name = "TOC" 
    With .Range("A1:B1") 
        .Value = VBA.Array("Table of Contents", "Sheet # - # of Pages") 
        .Font.Bold = True 
    End With 
End With 
lnRow = 2 
lnCount = 1 
'Iterate through the worksheets in the workbook and create 
'sheetnames, add hyperlink and count &amp; write the running number 
'of pages to be printed for each sheet on the TOC sheet. 
For Each wsSheet In wbBook.Worksheets 
    If wsSheet.Name <> wsActive.Name Then 
        wsSheet.Activate 
        With wsActive 
            .Hyperlinks.Add .Cells(lnRow, 1), "", _ 
            SubAddress:="'" &amp; wsSheet.Name &amp; "'!A1", _ 
            TextToDisplay:=wsSheet.Name 
            lnPages = wsSheet.PageSetup.Pages().Count 
            .Cells(lnRow, 2).Value = "'" &amp; lnCount &amp; "-" &amp; lnPages 
        End With 
        lnRow = lnRow + 1 
        lnCount = lnCount + 1 
    End If 
Next wsSheet 
wsActive.Activate 
wsActive.Columns("A:B").EntireColumn.AutoFit 
With Application 
    .DisplayAlerts = True 
    .ScreenUpdating = True 
End With 
End Sub

```

 **示例代码提供者：** Bill Jelen，[MrExcel.com](http://www.mrexcel.com/)
本示例验证名为"TOC"的表已存在。如果存在，示例将更新目录。否则，该示例将在工作簿开头创建一个新的 TOC 表。每个工作表的名称以及相应的打印页码将列在目录中。要检索页码，示例将打开"打印预览"对话框。您必须关闭此对话框，然后将创建目录。



```
Sub CreateTableOfContents() 
    ' Determine if there is already a Table of Contents 
    ' Assume it is there, and if it is not, it will raise an error 
    ' if the Err system variable is > 0, you know the sheet is not there 
    Dim WST As Worksheet 
    On Error Resume Next 
    Set WST = Worksheets("TOC") 
    If Not Err = 0 Then 
        ' The Table of contents doesn't exist. Add it 
        Set WST = Worksheets.Add(Before:=Worksheets(1)) 
        WST.Name = "TOC" 
    End If 
    On Error GoTo 0 
     
    ' Set up the table of contents page 
    WST.[A2] = "Table of Contents" 
    With WST.[A6] 
        .CurrentRegion.Clear 
        .Value = "Subject" 
    End With 
    WST.[B6] = "Page(s)" 
    WST.Range("A1:B1").ColumnWidth = Array(36, 12) 
    TOCRow = 7 
    PageCount = 0 
 
    ' Do a print preview on all sheets so Excel calcs page breaks 
    ' The user must manually close the PrintPreview window 
    Msg = "Excel needs to do a print preview to calculate the number of pages. " 
    Msg = Msg &amp; "Please dismiss the print preview by clicking close." 
    MsgBox Msg 
    ActiveWindow.SelectedSheets.PrintPreview 
 
    ' Loop through each sheet, collecting TOC information 
    For Each S In Worksheets 
        If S.Visible = -1 Then 
            S.Select 
            ThisName = ActiveSheet.Name 
            HPages = ActiveSheet.HPageBreaks.Count + 1 
            VPages = ActiveSheet.VPageBreaks.Count + 1 
            ThisPages = HPages * VPages 
 
            ' Enter info about this sheet on TOC 
            Sheets("TOC").Select 
            Range("A" &amp; TOCRow).Value = ThisName 
            Range("B" &amp; TOCRow).NumberFormat = "@" 
            If ThisPages = 1 Then 
                Range("B" &amp; TOCRow).Value = PageCount + 1 &amp; " " 
            Else 
                Range("B" &amp; TOCRow).Value = PageCount + 1 &amp; " - " &amp; PageCount + ThisPages 
            End If 
        PageCount = PageCount + ThisPages 
        TOCRow = TOCRow + 1 
        End If 
    Next S 
End Sub 

```


## 关于参与者
<a name="AboutContributor"> </a>

Dennis Wallentin 是 VSTO &amp; .NET &amp; Excel（专门介绍适用于 Excel 和 Excel Services 的 .NET Framework 解决方案的博客）的作者。二十多年来，Dennis 一直在开发 Excel 解决方案，并且他还是 "Professional Excel Development: The Definitive Guide to Developing Applications Using Microsoft Excel, VBA and .NET (2nd Edition)" 的合著作者。

MVP Bill Jelen 是二十多部关于 Microsoft Excel 书籍的作者。他是 Leo Laporte 的 TechTV 节目的常客，也是包括 300,000 多个关于 Excel 的问题和解答的 MrExcel.com 网站的主办人。

