
# 突出显示活动单元格、 行或列

下面的代码示例显示突出显示活动单元格或者行和列中包含活动单元格的方法。这些示例中使用的 **[工作表](182b705e-854a-81cc-a4b0-59b942de55ae.md)** 对象的 **[SelectionChange](183e2ca7-06b2-f689-1f77-182dbfbf1e1d.md)** 事件。

 **示例提供的代码:** Tom Urtis，[Atlas 编程管理](http://www.atlaspm.com/)

## 突出显示活动单元格

下面的代码示例通过设置 **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** 属性等于 0，清除工作表中的所有单元格的颜色，然后通过设置 **ColorIndex**属性等于 8 (青绿色) 中突出显示活动单元格。


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    Application.ScreenUpdating = False
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    ' Highlight the active cell
    Target.Interior.ColorIndex = 8
    Application.ScreenUpdating = True
End Sub
```


## 突出显示整个行和列包含活动单元格

下面的代码示例通过设置 **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** 属性等于 0，清除工作表中的所有单元格的颜色，然后突出显示整个行，通过使用 **[EntireRow](9e66da51-6cef-4109-ea4e-2acaad42aa1f.md)** 和 **[EntireColumn](7be55670-75fd-fb02-dc1a-9d70e3a9d80d.md)** 属性包含活动单元格的列。


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    If Target.Cells.Count > 1 Then Exit Sub
    Application.ScreenUpdating = False
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    With Target
        ' Highlight the entire row and column that contain the active cell
        .EntireRow.Interior.ColorIndex = 8
        .EntireColumn.Interior.ColorIndex = 8
    End With
    Application.ScreenUpdating = True
End Sub
```


## 突出显示的行和列包含当前区域中的活动单元格

下面的代码示例通过设置 **[ColorIndex](6d1a5bc9-7157-61e0-1e1d-b44974002c78.md)** 属性等于 0，清除工作表中的所有单元格的颜色，然后突出显示的行和列包含活动单元格，使用 **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** 对象的 **[CurrentRegion](39277cc5-07ff-8453-7330-b272b365f9dc.md)** 属性的当前区域中。


```
Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    ' Clear the color of all the cells
    Cells.Interior.ColorIndex = 0
    If IsEmpty(Target) Or Target.Cells.Count > 1 Then Exit Sub
    Application.ScreenUpdating = False
    With ActiveCell
        ' Highlight the row and column that contain the active cell, within the current region
        Range(Cells(.Row, .CurrentRegion.Column), Cells(.Row, .CurrentRegion.Columns.Count + .CurrentRegion.Column - 1)).Interior.ColorIndex = 8
        Range(Cells(.CurrentRegion.Row, .Column), Cells(.CurrentRegion.Rows.Count + .CurrentRegion.Row - 1, .Column)).Interior.ColorIndex = 8
    End With
    Application.ScreenUpdating = True
End Sub
```


## 有关参与者
<a name="AboutContributor"> </a>

MVP Tom Urtis 是 Atlas Programming Management（提供 Microsoft Office 和 Excel 业务解决方案全面服务的公司，位于硅谷）的创始人。Tom 在业务管理和开发 Microsoft Office 应用程序方面拥有超过 25 年的经验，并协助编写了"Holy Macro! It's 2,500 Excel VBA Examples" 。

