
# 在工作表上使用 ActiveX 控件

本主题包括在工作表或图表工作表上使用 ActiveX 控件的特定信息。有关添加和处理控件的一般信息，请参阅[在文档中使用 ActiveX 控件](063c3f84-3826-1bc2-5e60-12ff5760c8d4.md)和[创建自定义对话框](ee156753-b6f8-3394-0a4c-a3940026579d.md)。

在工作表上处理控件时，必须记住以下几点：

- 除了可用于 ActiveX 控件的标准属性，可以使用 ActiveX 控件，在 Excel 中使用以下属性:  **[BottomRightCell](daa803f5-76d7-270e-9116-9b5da9f497c4.md)** 、 **[LinkedCell](6f45dffd-5d16-de5a-ec7c-7fd45f1116f0.md)** 、 **[ListFillRange](d8a44f9f-49bb-237b-66c8-9f6c06fe82ac.md)** 、 **[放置](03cc3dc9-7e91-3410-be95-29e62b09331d.md)** 、 **[PrintObject](23cfc576-c4f1-5b60-1daa-4fc0a5e12d7e.md)** 、 **[TopLeftCell](a040afe1-71a9-7d3f-fc48-74f49c75df76.md)** 和 **[zorder 方法](dd7c2c81-6582-5de9-d254-66061d4345ef.md)** 。
    
    
这些属性可用 ActiveX 控件的名称来设置和返回。下例滚动工作簿窗口，使 CommandButton1 位于窗口的左上角。
    


  ```
  Set t = Sheet1.CommandButton1.TopLeftCell
With ActiveWindow
    .ScrollRow = t.Row
    .ScrollColumn = t.Column
End With

  ```


- 激活的 ActiveX 控件时，将禁用某些 Microsoft Excel Visual Basic 方法和属性。例如，当控件处于活动状态，因此在按钮下面的代码将无效 (因为用户单击它后，该控件是仍处于活动状态)，则单击事件过程，不能使用 **Sort** 方法。
    
  ```
  Private Sub CommandButton1.Click 
    Range("a1:a10").Sort Key1:=Range("a1") 
End Sub 
  ```


    您可以通过使用属性或方法之前激活工作表上的其他一些元素来变通解决此问题。例如，下面的代码对单元格区域:
    


  ```
  Private Sub CommandButton1.Click 
    Range("a1").Activate 
    Range("a1:a10").Sort Key1:=Range("a1") 
    CommandButton1.Activate 
End Sub 

  ```


- 当用户双击嵌入在其他应用程序文档中的 Microsoft Excel 工作簿对其进行编辑时，该工作簿上的控件将不能正常工作。如果用户右键单击工作簿，然后选择快捷菜单上的"打开"命令，工作簿上的控件将正常工作。
    
- 用 Microsoft Excel 5.0/95 工作簿文件格式保存 Microsoft Excel 工作簿时，将丢失 ActiveX 控件信息。
    
- 工作表上 ActiveX 控件的事件过程中的  **Me** 关键字引用的是工作表，而不是控件。
    

## 用 Visual Basic 添加控件

在 Microsoft Excel 中，用  **OLEObjects** 集合中的 **OLEObject** 对象代表 ActiveX 控件（所有 **OLEObject** 对象也都包含在 **Shapes** 集合中）。若要用编程方式向工作表添加 ActiveX 控件，请用 **OLEObjects** 集合的 **Add** 方法。下例向第一张工作表添加命令按钮。


```
Worksheets(1).OLEObjects.Add "Forms.CommandButton.1", _ 
    Left:=10, Top:=10, Height:=20, Width:=100
```


## 通过 Visual Basic 使用控件属性

绝大多数情况下，Visual Basic 代码可用名称引用 ActiveX 控件。下例更改了控件"CommandButton1"的标题。


```
Sheet1.CommandButton1.Caption = "Run"
```

请注意，当在控件所在的工作表的类模块之外使用控件的名称时，必须用工作表的名称限定该控件的名称。

若要更改在 Visual Basic 代码中所用的控件名称，请先选择控件，然后在"属性"窗口中设置控件的"(名称)"属性。

因为 ActiveX 控件也可用  **OLEObjects** 集合中的 **OLEObject** 对象代表，所以也可用该集合中的对象来设置控件的属性。下例设置了控件"CommandButton1"的左边位置。




```
Worksheets(1).OLEObjects("CommandButton1").Left = 10
```

没有显示为  **OLEObject** 对象属性的控件属性可通过由 **Object** 属性返回的实际控件对象来设置。下例设置了控件"CommandButton1"的标题。




```
Worksheets(1).OLEObjects("CommandButton1"). _ 
    Object.Caption = "run me"
```

因为所有 OLE 对象也是  **Shapes** 集合的成员，所以也可用该集合设置多个控件的属性。下例对齐第一张工作表上所有控件的左边缘。




```
For Each s In Worksheets(1).Shapes 
    If s.Type = msoOLEControlObject Then s.Left = 10 
Next
```


## 使用 Shapes 和 OLEObjects 集合的控件名称

工作表上的 ActiveX 控件具有两个名称：包含控件的形状的名称（查看工作表时，可在"名称"框中看到此名称）；控件的代码名称（在"属性"窗口的"(名称)"右侧单元格中可看到此名称）。第一次向工作表添加控件时，形状名称和代码名称是匹配的。但是，如果更改其中之一（形状名称或代码名称），另一个名称不会自动随之更改。

在控件的事件过程名称中使用的是控件代码名称。但是，从工作表的  **Shapes** 或 **OLEObjects** 集合返回控件时，必须使用形状名称而不是代码名称来通过名称引用控件。例如，假定要向工作表添加一个复选框，默认的形状名称和代码名称都是 CheckBox1。如果通过在"属性"窗口的"(名称)"旁边键入"chkFinished"更改控件代码名称，则必须在事件过程名称中使用"chkFinished"，但仍需要使用 CheckBox1 才能从 **Shapes** 或 **OLEObject** 集合返回控件，如下例所示。




```
Private Sub chkFinished_Click() 
    ActiveSheet.OLEObjects("CheckBox1").Object.Value = 1 
End Sub
```

