
# 处理形状（图形对象）

形状或绘图对象，由三个不同的对象:  **[Shapes](f9c6548c-d028-1b70-a11c-c4b45ff19177.md)** 集合、 **[ShapeRange](e1b8229c-73a0-4a77-5e00-4bcec9032260.md)** 集合和 **[形状](8f01fcd1-b7d9-5216-2de5-40fb6648a403.md)** 对象。一般情况下，您可用 **Shapes** 集合创建形状和循环访问给定的工作表上的所有形状您使用 **Shape** 对象设置格式或修改单个形状;并使用 **ShapeRange** 集合可修改多个形状相同的方式使用用户界面中的多个形状。


## 设置形状的属性

形状的许多格式属性不是由直接应用于  **Shape** 或 **ShapeRange** 对象的属性设置的。相反，相关的形状属性在第二级对象内进行组合，例如 **FillFormat** 对象，其中包含了与形状填充相关的所有属性；又如 **LinkFormat** 对象，其中包含了链接的 OLE 对象特有的所有属性。要设置一个形状的属性，必须先返回代表相关形状属性集的对象，然后设置该返回的对象的属性。例如，使用 **Fill** 属性返回 **FillFormat** 对象，然后设置 **FillFormat** 对象的 **ForeColor** 属性，从而设置指定形状的前景填充色，如以下示例所示。


```
Worksheets(1).Shapes(1).Fill.ForeColor.RGB = RGB(255, 0, 0)
```


## 同时对多个形状应用属性或方法

在用户界面中，可以对选择的多个形状执行某些操作。例如，可以一次选择多个形状并设置每个形状的填充。有些操作只能对选择的单个形状执行。例如，只有选择单个形状，才能编辑其文本。

在 Visual Basic 中，对一组形状应用属性和方法有两种方式。这两种方式使您可以对若干形状执行在单个形状上能执行的所有操作，而不管是否可以在用户界面上执行这些操作。


- 如果该操作在用户界面上影响多个选定的形状，则要在 Visual Basic 中执行该操作时，只需先建立一个包含要使用的形状的  **ShapeRange** 集合，然后直接对该 **ShapeRange** 集合应用适当的属性和方法。
    
- 如果不能在用户界面上对多个选定的形状同时执行某个操作，则仍然可以在 Visual Basic 中通过以下方法来执行该操作：遍历包含要处理的形状的  **Shapes** 集合或 **ShapeRange** 集合，对集合中每个单独的 **Shape** 对象应用适当的属性和方法。
    
许多应用于  **Shape** 对象和 **ShapeRange** 集合的属性和方法，在应用于特定类型的形状时将失败。例如，将 **TextFrame** 属性应用于不能包含文本的形状时，该属性将失败。如果不能确定 **ShapeRange** 集合中的每个形状是否都能应用某一特定的属性或方法，就不要对 **ShapeRange** 集合应用该属性或方法。如果要对某一形状集合应用这些属性或方法之一，必须在该集合中循环，并在对每个形状应用该属性或方法之前，检测该形状是否属于能够应用该属性或方法的类型。


## 创建包含工作表上所有形状的 ShapeRange 集合

通过选定图形，然后使用  **ShapeRange** 属性来返回包含选定图形的 **ShapeRange** 对象，可创建包含工作表上所有 **Shape** 对象的 **ShapeRange** 对象。


```
Worksheets(1).Shapes.Select 
Set sr = Selection.ShapeRange
```

在 Microsoft Excel 中， **Shapes** 集合的 **Range** 属性的 ** _Index_** 参数不是可选的，所以不能用不带参数的该属性来创建包含 **Shapes** 集合中所有形状的 **ShapeRange** 对象。


## 对 ShapeRange 集合应用属性或方法

如果可在用户界面上对若干选定的图形同时执行某一操作，那么通过构造  **ShapeRange** 集合，并对该集合应用适当的属性和方法，可在编程中实现相同的操作。下例在 `myDocument` 上构造了包含图形"Big Star"和"Little Star"的图形区域，并对这些图形应用渐变填充格式。


```
Set myDocument = Worksheets(1) 
Set myRange = myDocument.Shapes.Range(Array("Big Star", _ 
 "Little Star")) 
myRange.Fill.PresetGradient _ 
 msoGradientHorizontal, 1, msoGradientBrass
```

下面是如何将属性和方法应用于  **ShapeRange** 集合的一般原则。


- 对该集合应用某一方法等价于对该集合内每个单独的  **Shape** 对象应用该方法。
    
- 设置集合的属性值等于设置该范围内每个形状的属性值。
    
- 如果集合中所有形状的某一返回常数的属性值相同，则集合的该属性将返回单个形状的该属性值。如果集合中所有形状的该属性值不相同，则返回"mixed"常数。
    
- 如果集合中所有形状的某一返回简单数据类型的属性值相同（例如  **Long** 、 **Single** 或 **String** ），集合的该属性将返回单个形状的该属性值。
    
- 有些属性值仅当集合中仅有一个形状时才能返回或设置。如果集合包含多个形状，将发生运行时错误。这种情况通常发生在用户界面中与返回或设置属性等价的操作不能同时对多个形状进行时（例如，编辑形状中的文本或编辑任意多边形的点）。
    
设置组合在  **ShapeRange** 集合第二级对象中的形状的属性时，上述原则同样适用。例如 **FillFormat** 对象。如果第二级对象代表可在用户界面中对多个选定对象执行的操作，则可以从 **ShapeRange** 集合返回该对象并设置其属性。例如，可以使用 **Fill** 属性返回 **FillFormat** 对象，该对象代表 **ShapeRange** 集合中所有形状的填充。设置该 **FillFormat** 对象的属性将设置 **ShapeRange** 集合中所有形状的相同属性。


## 在 Shapes 集合或 ShapeRange 集合中循环

即使不能在用户界面上通过选择多个形状并执行某一命令来对这些形状同时进行某一操作，也可以通过下述编程方式来执行该操作：在包含要处理的形状的  **Shapes** 集合或 **ShapeRange** 集合中循环，对集合中的单个 **Shape** 对象应用适当的属性和方法。下例在 `myDocument` 上的所有形状中循环，并更改每个自选图形的前景色。


```
Set myDocument = Worksheets(1) 
For Each sh In myDocument.Shapes 
 If sh.Type = msoAutoShape Then 
 sh.Fill.ForeColor.RGB = RGB(255, 0, 0) 
 End If 
Next
```

下例构造了包含活动窗口中所有当前选定的图形的  **ShapeRange** 集合，并设置每个选定图形的前景色。




```
For Each sh in ActiveWindow.Selection.ShapeRange 
 sh.Fill.ForeColor.RGB = RGB(255, 0, 0) 
Next
```


## 在形状区域中对齐、分布和组合形状

 **[对齐](7a4e6442-6730-ab7d-93b5-4c091ada6b14.md)** 和 **[分布](cef14a4b-4d6e-758e-928a-99233f893ddc.md)** 方法用于将一组形状相对于彼此或相对于包含它们的文档。使用 **[Group](f0ad9b81-42ad-0ee6-d2e2-ff2a88d47a97.md)** 方法或 **[重新组合](d30d3064-c37e-84b0-10a6-11dcd18c593e.md)** 的方法形成的一组形状的单个组合的形状。

