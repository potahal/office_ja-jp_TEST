
# 选择一个范围

这些示例显示如何选择所用的区域，其中包含已设置格式的单元格不包含数据，以及如何选择数据区域，其中包括包含实际数据的单元格。

 **示例提供的代码:** Tom Urtis，[Atlas 编程管理](http://www.atlaspm.com/)

## 选择使用的范围

此示例演示如何选择当前工作表，其中包含格式的单元格不包含数据，通过使用 **[UsedRange](f004b93c-d785-de19-1fb4-bbe0b2e9b6cd.md)** 属性 **[工作表](182b705e-854a-81cc-a4b0-59b942de55ae.md)** 对象和 **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** 对象的 **[Select](46c12f85-fae5-15ea-3500-81ff8be49cdb.md)** 方法的使用的范围。然后向用户显示该范围的地址。


```
Sub SelectUsedRange()
    ActiveSheet.UsedRange.Select
    MsgBox "The used range address is " &amp; ActiveSheet.UsedRange.Address(0, 0) &amp; ".", 64, "Used range address:"
End Sub
```


## 选择单元格 A1 开始的数据区域

此示例演示如何选择在当前表中，从单元格 A1 开始的数据区域，并向用户显示该范围的地址。数据区域不包括单元格的格式设置不包含数据。若要获取的数据区域，本示例查找最后一行和最后一列包含实际数据的使用 **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** 对象的 **[Find](d9585265-8164-cb4d-a9e3-262f6e06b6b8.md)** 方法。


```
Sub SelectDataRange()
    Dim LastRow As Long, LastColumn As Long
    LastRow = Cells.Find(What:="*", After:=Range("A1"), SearchOrder:=xlByRows, SearchDirection:=xlPrevious).Row
    LastColumn = Cells.Find(What:="*", After:=Range("A1"), SearchOrder:=xlByColumns, SearchDirection:=xlPrevious).Column
    Range("A1").Resize(LastRow, LastColumn).Select
    MsgBox "The data range address is " &amp; Selection.Address(0, 0) &amp; ".", 64, "Data-containing range address:"
End Sub
```


## 选择数据范围的未知的起始位置

此示例演示如何选择当前工作表的数据区域，当您不知道的起始位置，并向用户显示该范围的地址。数据区域不包括单元格的格式设置不包含数据。若要获取的数据区域，本示例查找第一个和最后一个行和列包含实际数据的使用 **[Range](b8207778-0dcc-4570-1234-f130532cc8cd.md)** 对象的 **[Find](d9585265-8164-cb4d-a9e3-262f6e06b6b8.md)** 方法。


```
Sub UnknownRange()
    If WorksheetFunction.CountA(Cells) = 0 Then
        MsgBox "There is no range to be selected.", , "No cells contain any values."
        Exit Sub
    Else
        Dim FirstRow&amp;, FirstCol&amp;, LastRow&amp;, LastCol&amp;
        Dim myUsedRange As Range
        FirstRow = Cells.Find(What:="*", SearchDirection:=xlNext, SearchOrder:=xlByRows).Row
        
        On Error Resume Next
        FirstCol = Cells.Find(What:="*", SearchDirection:=xlNext, SearchOrder:=xlByColumns).Column
        If Err.Number <> 0 Then
            Err.Clear
            MsgBox _
            "There are horizontally merged cells on the sheet" &amp; vbCrLf &amp; _
            "that should be removed in order to locate the range.", 64, "Please unmerge all cells."
            Exit Sub
        End If
        
        LastRow = Cells.Find(What:="*", SearchDirection:=xlPrevious, SearchOrder:=xlByRows).Row
        LastCol = Cells.Find(What:="*", SearchDirection:=xlPrevious, SearchOrder:=xlByColumns).Column
        Set myUsedRange = Range(Cells(FirstRow, FirstCol), Cells(LastRow, LastCol))
        myUsedRange.Select
        MsgBox "The data range on this worksheet is " &amp; myUsedRange.Address(0, 0) &amp; ".", 64, "Range address:"
    End If
End Sub
```


## 有关参与者
<a name="AboutContributor"> </a>

MVP Tom Urtis 是 Atlas Programming Management（提供 Microsoft Office 和 Excel 业务解决方案全面服务的公司，位于硅谷）的创始人。Tom 在业务管理和开发 Microsoft Office 应用程序方面拥有超过 25 年的经验，并协助编写了"Holy Macro! It's 2,500 Excel VBA Examples" 。

