
# 引用命名区域

用名称比用 A1 样式记号更容易标识单元格区域。若要命名选定的单元格区域，请单击编辑栏左端的名称框，键入名称，再按 Enter。

 **注意:** 有两种类型的命名范围: 命名范围的工作簿和工作表特定命名区域。

 **命名区域的工作簿**

特定范围从任意位置 (其应用全局) 工作簿中的工作簿名称区域引用和通常由编辑栏左端的名称框中输入名称。不允许有空格的名称。
 **工作表中特定的命名的区域**
工作表中特定命名区域指的是特定的工作表的区域，不在工作簿中所有工作表的全局。可以指这只是在相同的表中，名称命名的区域，但从另一个工作表，您必须使用工作表名称和"!"&amp; 然后是名称的范围;例如，如果您区域命名为"名称""= Sheet1!名称")。这样做的好处是您可以使用 VBA 代码来生成这些表中的相同范围的名称相同的新工作表，而获得错误信息，提示该名称已经存在。

### 创建工作表特定命名区域


1. 选择要命名的范围。
    
2. 在窗口顶部的 Excel 功能区上的"公式"选项卡上单击。
    
3. 单击公式选项卡中的"定义名称"按钮。
    
4. 在"新名称"对话框中，在"范围"字段中选择特定的工作表"Sheet1") (例如，位于您要定义的范围。这将使名称特定到该工作表。如果您选择"工作簿"，然后将工作簿的名称)。
    
工作表特定已命名范围的范例是"为名称的所选区域 a1: a10"。所选区域的名称是相同工作表中的"名称"。通过从不同表中的"= 名称"单元格中输入下列引用的已命名的"名称"。通过在单元格中包括工作表名称引用工作表的特定区域"= Sheet1! 名称"。

## 引用命名区域

以下示例引用名为"MyBook.xls"的工作簿中名为"MyRange"的区域。


```
vba
Sub FormatRange() 
    Range("MyBook.xls!MyRange").Font.Italic = True 
End Sub
```

以下示例引用名为"Report.xls"的工作簿中特定于工作表的区域"Sheet1!Sales"。




```
vba
Sub FormatSales() 
    Range("[Report.xls]Sheet1!Sales").BorderAround Weight:=xlthin 
End Sub
```

要选定命名区域，请使用  **GoTo** 方法，该方法将激活工作簿和工作表，然后选定该区域。




```
vba
Sub ClearRange() 
    Application.Goto Reference:="MyBook.xls!MyRange" 
    Selection.ClearContents 
End Sub
```

以下示例显示对于活动工作簿将如何编写相同的过程。




```
vba
Sub ClearRange() 
    Application.Goto Reference:="MyRange" 
    Selection.ClearContents 
End Sub
```

 **示例代码提供者：** Dennis Wallentin，[VSTO &amp; .NET &amp; Excel](http://xldennis.wordpress.com/)

此示例使用一个命名的范围进行数据有效性检验的公式。此示例要求要在 A2:A100 的范围表 2 上的验证数据。此验证数据用于验证数据输入工作表 1 上注意的范围。




```
vba
Sub Add_Data_Validation_From_Other_Worksheet()
'The current Excel workbook and worksheet, a range to define the data to be validated, and the target range
'to place the data in.
Dim wbBook As Workbook
Dim wsTarget As Worksheet
Dim wsSource As Worksheet
Dim rnTarget As Range
Dim rnSource As Range

'Initialize the Excel objects and delete any artifacts from the last time the macro was run.
Set wbBook = ThisWorkbook
With wbBook
    Set wsSource = .Worksheets("Sheet2")
    Set wsTarget = .Worksheets("Sheet1")
    On Error Resume Next
    .Names("Source").Delete
    On Error GoTo 0
End With

'On the source worksheet, create a range in column A of up to 98 cells long, and name it "Source".
With wsSource
    .Range(.Range("A2"), .Range("A100").End(xlUp)).Name = "Source"
End With

'On the target worksheet, create a range 8 cells long in column D.
Set rnTarget = wsTarget.Range("D2:D10")

'Clear out any artifacts from previous macro runs, then set up the target range with the validation data.
With rnTarget
    .ClearContents
    With .Validation
        .Delete
        .Add Type:=xlValidateList, _
             AlertStyle:=xlValidAlertStop, _
             Formula1:="=Source"
        
'Set up the Error dialog with the appropriate title and message
        .ErrorTitle = "Value Error"
        .ErrorMessage = "You can only choose from the list."
    End With
End With

End Sub
```


## 在命名区域中的单元格上循环

以下示例使用  **For Each...Next** 循环语句在命名区域中的每一个单元格中循环。如果该区域中的任一单元格的值超过 `Limit` 的值，则该单元格的颜色会变为黄色。


```
vba
Sub ApplyColor() 
    Const Limit As Integer = 25 
    For Each c In Range("MyRange") 
        If c.Value > Limit Then 
            c.Interior.ColorIndex = 27 
        End If 
    Next c 
End Sub
```


## 关于参与者
<a name="AboutContributor"> </a>

Dennis Wallentin 是 VSTO &amp; .NET &amp; Excel（专门介绍适用于 Excel 和 Excel Services 的 .NET Framework 解决方案的博客）的作者。二十多年来，Dennis 一直在开发 Excel 解决方案，并且他还是 "Professional Excel Development: The Definitive Guide to Developing Applications Using Microsoft Excel, VBA and .NET (2nd Edition)" 的合著作者。

