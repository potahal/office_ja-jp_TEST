
# Selection.Item 方法 （Outlook）

返回所选内容的Microsoft Outlook项或对话头。


## 语法

 _表达式_. **Item**( ** _Index_** )

 _表达式_ 一个代表 **[Selection](0b06a3ce-0445-db8f-e6e8-bb7bd469c50f.md)** 对象的变量。


### 参数



|**名称**|**必需/可选**|**数据类型**|**说明**|
|:-----|:-----|:-----|:-----|
| _Index_|必需|**Variant**|或者是对象的索引号，或者是用于匹配集合中某个对象的默认属性的值。|

### 返回值

代表指定的项目或对话头 **Object** 。


## 注解

不做任何假设的 **Item** 方法返回类型;您的代码应该能够处理多个项目类型或一个 **[ConversationHeader](5142d5f7-55c1-4d9d-3a11-d25c8763fcb7.md)** 对象。例如， **Item** 方法可以在收件箱文件夹中的根据 **[Selection.Location](8a2db72a-8db0-840e-349e-5d9d22f3affb.md)** 属性的值，返回的 **[AppointmentItem](204a409d-654e-27aa-643a-8344c631b82d.md)** 、 **[MailItem](14197346-05d2-0250-fa4c-4a6b07daf25f.md)** 、 **[MeetingItem](b75730f5-b395-3d66-5acd-b64fd8fcd78f.md)** 或 **[TaskItem](5df8cfa5-5460-a5a1-a130-ba5bca1a0091.md)** 。

仅当 **选定** 对象的 **[GetSelection](c6af6665-d97d-3833-1014-5b43282bafc2.md)** 方法中指定 **olConversationHeaders** ， **[选择](0b06a3ce-0445-db8f-e6e8-bb7bd469c50f.md)** 集合包含 **ConversationHeader** 对象。


## 示例

下面的 Microsoft Visual Basic for Applications (VBA) 示例显示在活动资源管理器中每个选定项目的发件人。它使用 **[Count](ea7a19d2-6261-ce07-97f3-ebe95489a265.md)** 属性并返回 **[Explorer.Selection](11002043-9dab-a5ad-b36e-52ddb04c1859.md)** 属性中， **[选择](0b06a3ce-0445-db8f-e6e8-bb7bd469c50f.md)** 对象的 **[Item](981b107a-14d7-2dd3-6449-2737b2801c3c.md)** 方法显示在活动资源管理器中选定的所有邮件的发件人。


```
Sub GetSelectedItems() 
 Dim myOlExp As Outlook.Explorer 
 Dim myOlSel As Outlook.Selection 
 Dim mySender As Outlook.AddressEntry 
 Dim oMail As Outlook.MailItem 
 Dim oAppt As Outlook.AppointmentItem 
 Dim oPA As Outlook.PropertyAccessor 
 Dim strSenderID As String 
 Const PR_SENT_REPRESENTING_ENTRYID As String = _ 
 "http://schemas.microsoft.com/mapi/proptag/0x00410102" 
 Dim MsgTxt As String 
 Dim x As Long 
 
 MsgTxt = "Senders of selected items:" 
 Set myOlExp = Application.ActiveExplorer 
 Set myOlSel = myOlExp.Selection 
 For x = 1 To myOlSel.Count 
 If myOlSel.Item(x).Class = OlObjectClass.olMail Then 
 ' For mail item, use the SenderName property. 
 Set oMail = myOlSel.Item(x) 
 MsgTxt = MsgTxt &amp; oMail.SenderName &amp; ";" 
 ElseIf myOlSel.Item(x).Class = OlObjectClass.olAppointment Then 
 ' For appointment item, use the Organizer property. 
 Set oAppt = myOlSel.Item(x) 
 MsgTxt = MsgTxt &amp; oAppt.Organizer &amp; ";" 
 Else 
 ' For other items, use the property accessor to get sender ID, 
 ' then get the address entry to display the sender name. 
 Set oPA = myOlSel.Item(x).PropertyAccessor 
 strSenderID = oPA.GetProperty(PR_SENT_REPRESENTING_ENTRYID) 
 Set mySender = Application.Session.GetAddressEntryFromID(strSenderID) 
 MsgTxt = MsgTxt &amp; mySender.Name &amp; ";" 
 End If 
 Next x 
 Debug.Print MsgTxt 
End Sub 

```


## 另请参阅


#### 概念


[选择对象](0b06a3ce-0445-db8f-e6e8-bb7bd469c50f.md)
#### 其他资源


[选择对象的成员](c79922d4-aa76-ff48-f163-8161fa1ae0a8.md)