
# PropertyAccessor.GetProperties 方法 （Outlook）

获取由一维数组  _SchemaNames_ 指定的属性的值。


## 语法

 _表达式_. **GetProperties**( ** _SchemaNames_** )

 _表达式_ 一个代表 **PropertyAccessor** 对象的变量。


### 参数



|**名称**|**必需/可选**|**数据类型**|**说明**|
|:-----|:-----|:-----|:-----|
| _SchemaNames_|必需|**Variant**|一个包含要返回其值的属性的名称的数组。这些属性通过命名空间引用。有关详细信息，请参阅[通过命名空间引用属性](http://msdn.microsoft.com/library/c1c7bfa9-64d7-81d2-84e7-f0a4c57780b3%28Office.15%29.aspx)。|

### 返回值

变量，代表在 _SchemaNames_ 参数中指定的属性的值的数组。返回的数组中的元素个数等于 _SchemaNames_ 数组中的元素数。 如果在获取特定属性时发生错误，则将返回的数组中的对应位置中返回 **Err** 值。


## 说明

 **GetProperties** 返回的数组可以包含属于不同类型，具体取决于请求的属性的类型的元素。由 **GetProperties** 返回的数组元素的类型将是基础属性的类型相同。 **PT_OBJECT** 如某些原始属性类型不受支持，会引发错误。如果您需要转换的原始属性类型，例如，从 **PT_BINARY** 到一个字符串中，或为本地时间， **PT_SYSTIME** 使用[PropertyAccessor.BinaryToString](4a3801af-0a7c-4b8a-7367-600c09047b28.md)和[PropertyAccessor.UTCToLocalTime](a56311ac-60ac-4f51-5255-d6840bf6004d.md)的帮助器方法。

获取使用 **PropertyAccessor** 对象的属性的详细信息，请参阅[获取文件和设置属性的最佳做法](http://msdn.microsoft.com/library/ec087bf8-cfac-9b20-3cb2-3bd308c5c63d%28Office.15%29.aspx)。


## 示例

下面的代码示例演示如何使用 **[PropertyAccessor](2fc91e13-703c-3ec9-9066-ffee7144306c.md)** 对象来获取 MAPI 属性未公开某个 Outlook 项目，即: **PR_SUBJECT** 、 **PR_ATTR_HIDDEN** 、 **PR_ATTR_READONLY** 和 **PR_ATTR_SYSTEM** 。此代码示例使用 **GetProperties** 方法在一个调用中指定的命名空间引用这些属性，到一个数组中检索它们，并获取包含每个属性的原始值的返回的数组。


```
Sub DemoPropertyAccessorGetProperties() 
 
 Dim PropNames() As Variant 
 
 Dim myValues As Variant 
 
 Dim i As Integer 
 
 Dim j As Integer 
 
 Dim oMail As Object 
 
 Dim oPA As Outlook.PropertyAccessor 
 
 
 
 'Get first item in the inbox 
 
 Set oMail = _ 
 
 Application.Session.GetDefaultFolder(olFolderInbox).Items(1) 
 
 'PR_SUBJECT, PR_ATTR_HIDDEN, PR_ATTR_READONLY, PR_ATTR_SYSTEM 
 
 PropNames = _ 
 
 Array("http://schemas.microsoft.com/mapi/proptag/0x0037001E", _ 
 
 "http://schemas.microsoft.com/mapi/proptag/0x10F4000B", _ 
 
 "http://schemas.microsoft.com/mapi/proptag/0x10F6000B", _ 
 
 "http://schemas.microsoft.com/mapi/proptag/0x10F5000B") 
 
 'Obtain an instance of a PropertyAccessor object 
 
 Set oPA = oMail.PropertyAccessor 
 
 'Get myValues array with GetProperties call 
 
 myValues = oPA.GetProperties(PropNames) 
 
 For i = LBound(myValues) To UBound(myValues) 
 
 'Examine the type of the element 
 
 If IsError(myValues(i)) Then 
 
 'CVErr returns a variant of subtype error 
 
 Debug.Print (CVErr(myValues(i))) 
 
 ElseIf IsArray(myValues(i)) Then 
 
 propArray = myValues(i) 
 
 For j = LBound(propArray) To UBound(propArray) 
 
 Debug.Print (propArray(j)) 
 
 Next 
 
 ElseIf IsNull(myValues(i)) Then 
 
 Debug.Print ("Null value") 
 
 ElseIf IsEmpty(myValues(i)) Then 
 
 Debug.Print ("Empty value") 
 
 ElseIf IsDate(myValues(i)) Then 
 
 Debug.Print (oPA.UTCToLocalTime(myValues(i))) 
 
 'VB does not have IsBinary function 
 
 ElseIf VarType(myValues(i)) = vbByte Then 
 
 Debug.Print (oPA.BinaryToString(myValues(i))) 
 
 Else 
 
 Debug.Print (myValues(i)) 
 
 End If 
 
 Next 
 
End Sub
```


## 另请参阅


#### 概念


[PropertyAccessor 对象](2fc91e13-703c-3ec9-9066-ffee7144306c.md)
#### 其他资源


[PropertyAccessor 对象成员](3356e345-8878-0ed7-6783-1e49ddecc066.md)