
# CalendarSharing.SaveAsICal 方法 （Outlook）

将日历信息作为 iCalendar 日历 (.ics) 文件从  **[CalendarSharing](37a8a15e-51c2-b1a0-7db6-cf2a1f4e8405.md)** 对象的父 **[Folder](3cf6cda8-6d70-666e-2643-9d9c5b9cacfc.md)** 导出。


## 语法

 _表达式_. **SaveAsICal**( ** _Path_** )

 _表达式_ 一个返回 **CalendarSharing** 对象的表达式。


### 参数



|**名称**|**必需/可选**|**数据类型**|**说明**|
|:-----|:-----|:-----|:-----|
| _Path_|必需|**String**|iCalendar 文件的路径和文件名。|

## 说明

ICalendar 文件中提供的详细程度取决于在下面的 **CalendarSharing** 属性的值的组合:


-  **[CalendarDetail](f3f0ba8d-23db-505f-58c4-6e3a33a468e7.md)**
    
-  **[IncludeAttachments](504bba9e-009f-986f-070e-ff73ce82ea03.md)**
    
-  **[IncludePrivateDetails](a7c52e33-fe2a-b89a-9102-da2baf937e37.md)**
    
-  **[RestrictToWorkingHours](2d655c66-fd3e-0b82-41b2-798d408f6531.md)**
    
可以将 **[IncludeWholeCalendar](6cb75f0e-afb9-48fc-5b96-9f64a3b2ed6f.md)** 属性设置为 **True** 可导出包含在文件夹中的所有项，或者可以设置 **[开始日期](218654d9-ab4c-9f3b-cb47-3c006eed710d.md)** 和 **[结束日期](89358c71-7805-7acc-5afb-2ba7b592f9f2.md)** 属性，分别结束日期，并限制对指定的开始日期之间的日期范围的导出的项。


## 示例

下面的 Visual Basic for Applications (VBA) 示例创建 **CalendarSharing** 对象的日历文件夹中，然后将整个文件夹 (包括附件和私人性质项目) 中的内容导出到 iCalendar 日历 (.ics) 文件。


```
Public Sub ExportEntireCalendar() 
 
 
 
 Dim oNamespace As NameSpace 
 
 Dim oFolder As Folder 
 
 Dim oCalendarSharing As CalendarSharing 
 
 
 
 On Error GoTo ErrRoutine 
 
 
 
 ' Get a reference to the Calendar default folder 
 
 Set oNamespace = Application.GetNamespace("MAPI") 
 
 Set oFolder = oNamespace.GetDefaultFolder(olFolderCalendar) 
 
 
 
 ' Get a CalendarSharing object for the Calendar default folder. 
 
 Set oCalendarSharing = oFolder.GetCalendarExporter 
 
 
 
 ' Set the CalendarSharing object to export the contents of 
 
 ' the entire Calendar folder, including attachments and 
 
 ' private items, in full detail. 
 
 With oCalendarSharing 
 
 .CalendarDetail = olFullDetails 
 
 .IncludeWholeCalendar = True 
 
 .IncludeAttachments = True 
 
 .IncludePrivateDetails = True 
 
 .RestrictToWorkingHours = False 
 
 End With 
 
 
 
 ' Export calendar to an iCalendar calendar (.ics) file. 
 
 oCalendarSharing.SaveAsICal "C:\SampleCalendar.ics" 
 
 
 
EndRoutine: 
 
 On Error GoTo 0 
 
 Set oCalendarSharing = Nothing 
 
 Set oFolder = Nothing 
 
 Set oNamespace = Nothing 
 
Exit Sub 
 
 
 
ErrRoutine: 
 
 Select Case Err.Number 
 
 Case 287 ' &amp;H0000011F 
 
 ' The user denied access to the Address Book. 
 
 ' This error occurs if the code is run by an 
 
 ' untrusted application, and the user chose not to 
 
 ' allow access. 
 
 MsgBox "Access to Outlook was denied by the user.", _ 
 
 vbOKOnly, _ 
 
 Err.Number &amp; " - " &amp; Err.Source 
 
 Case -2147467259 ' &amp;H80004005 
 
 ' Export failed. 
 
 ' This error typically occurs if the CalendarSharing 
 
 ' method cannot export the calendar information because 
 
 ' of conflicting property settings. 
 
 MsgBox Err.Description, _ 
 
 vbOKOnly, _ 
 
 Err.Number &amp; " - " &amp; Err.Source 
 
 Case -2147221233 ' &amp;H8004010F 
 
 ' Operation failed. 
 
 ' This error typically occurs if the GetCalendarExporter method 
 
 ' is called on a folder that doesn't contain calendar items. 
 
 MsgBox Err.Description, _ 
 
 vbOKOnly, _ 
 
 Err.Number &amp; " - " &amp; Err.Source 
 
 Case Else 
 
 ' Any other error that may occur. 
 
 MsgBox Err.Description, _ 
 
 vbOKOnly, _ 
 
 Err.Number &amp; " - " &amp; Err.Source 
 
 End Select 
 
 
 
 GoTo EndRoutine 
 
End Sub
```


## 另请参阅


#### 概念


[CalendarSharing 对象](37a8a15e-51c2-b1a0-7db6-cf2a1f4e8405.md)
#### 其他资源


[CalendarSharing 对象成员](1b2b6233-9816-e3f2-5924-694ce30cc8ef.md)