
# Exception.OriginalDate 属性 (Outlook)

返回指示原始日期和时间的 **[AppointmentItem](204a409d-654e-27aa-643a-8344c631b82d.md)** ，对其做出更改之前的 **日期** 。即使 **AppointmentItem** 已被删除，则此属性将返回原始日期。但是，如果发生删除操作不会返回原始时间。只读的。


## 语法

 _表达式_. **OriginalDate**

 _表达式_ 一个代表 **Exception** 对象的变量。


## 示例

此 Visual Basic for Applications (VBA) 示例使用 **[CreateItem](e5fbf367-db16-5042-823e-68e6b805e612.md)** 创建一个 **[AppointmentItem](204a409d-654e-27aa-643a-8344c631b82d.md)** 对象。 **[RecurrencePattern](36c098f7-59fb-879a-5173-ed0260d13fa4.md)** 是为此物料使用 **[GetRecurrencePattern](a9f67c5b-a77f-4e34-e654-d12560a6dba0.md)** 方法得到的。通过设置这些属性: **[介于 1](bc9b35b5-ef00-e5cf-09cc-ee8743efddcf.md)** 、 **[PatternStartDate](20c82dbd-a622-91b6-618c-7cbe8bff2ca7.md)** 和 **[PatternEndDate](0f78ea71-3d92-2d38-be10-e05ab7bcf44a.md)** ，约会现在是每天都发生的一年期定期系列。使用 **[GetOccurrence](2a0cd7d2-d16d-7b07-eb5d-43df0bbf022f.md)** 方法来获取此定期约会的一个实例和该实例的属性会被更改时，创建的 **[异常](010552b0-9ba6-c81b-1e3a-fd6a681e5163.md)** 对象。使用 **GetRecurrencePattern** 方法来访问这一系列与关联的 **[异常](fa3b6c2e-33b0-0f04-4e60-af2c582f2caa.md)** 集合中获得约会系列到此异常。消息框显示原始 **[主题](57f0f242-6d04-175f-4ea2-25145787f5bd.md)** 和 **[OriginalDate](0777de75-b32d-fe23-03d8-bb3deb18a69e.md)** 系列的约会和当前日期、 时间和主题对此异常此异常。


```
Public Sub cmdExample() 
 
 Dim myApptItem As Outlook.AppointmentItem 
 
 Dim myRecurrPatt As Outlook.RecurrencePattern 
 
 Dim myNamespace As Outlook.NameSpace 
 
 Dim myFolder As Outlook.Folder 
 
 Dim myItems As Outlook.Items 
 
 Dim myDate As Date 
 
 Dim myOddApptItem As Outlook.AppointmentItem 
 
 Dim saveSubject As String 
 
 Dim newDate As Date 
 
 Dim myException As Outlook.Exception 
 
 
 
 Set myApptItem = Application.CreateItem(olAppointmentItem) 
 
 myApptItem.Start = #2/2/2003 3:00:00 PM# 
 
 myApptItem.End = #2/2/2003 4:00:00 PM# 
 
 myApptItem.Subject = "Meet with Boss" 
 
 
 
 'Get the recurrence pattern for this appointment 
 
 'and set it so that this is a daily appointment 
 
 'that begins on 2/2/03 and ends on 2/2/04 
 
 'and save it. 
 
 Set myRecurrPatt = myApptItem.GetRecurrencePattern 
 
 myRecurrPatt.RecurrenceType = olRecursDaily 
 
 myRecurrPatt.PatternStartDate = #2/2/2003# 
 
 myRecurrPatt.PatternEndDate = #2/2/2004# 
 
 myApptItem.Save 
 
 
 
 'Access the items in the Calendar folder to locate 
 
 'the master AppointmentItem for the new series. 
 
 Set myNamespace = Application.GetNamespace("MAPI") 
 
 Set myFolder = myNamespace.GetDefaultFolder(olFolderCalendar) 
 
 Set myItems = myFolder.Items 
 
 Set myApptItem = myItems("Meet with Boss") 
 
 
 
 'Get the recurrence pattern for this appointment 
 
 'and obtain the occurrence for 3/12/03. 
 
 myDate = #3/12/2003 3:00:00 PM# 
 
 Set myRecurrPatt = myApptItem.GetRecurrencePattern 
 
 Set myOddApptItem = myRecurrPatt.GetOccurrence(myDate) 
 
 
 
 'Save the existing subject. Change the subject and 
 
 'starting time for this particular appointment 
 
 'and save it. 
 
 saveSubject = myOddApptItem.Subject 
 
 myOddApptItem.Subject = "Meet NEW Boss" 
 
 newDate = #3/12/2003 3:30:00 PM# 
 
 myOddApptItem.Start = newDate 
 
 myOddApptItem.Save 
 
 
 
 'Get the recurrence pattern for the master 
 
 'AppointmentItem. Access the collection of 
 
 'exceptions to the regular appointments. 
 
 Set myRecurrPatt = myApptItem.GetRecurrencePattern 
 
 Set myException = myRecurrPatt.Exceptions.item(1) 
 
 
 
 'Display the original date, time, and subject 
 
 'for this exception. 
 
 MsgBox myException.OriginalDate &amp; ": " &amp; saveSubject 
 
 
 
 'Display the current date, time, and subject 
 
 'for this exception. 
 
 MsgBox myException.AppointmentItem.Start &amp; ": " &amp; _ 
 
 myException.AppointmentItem.Subject 
 
End Sub
```


## 另请参阅


#### 概念


[异常对象](010552b0-9ba6-c81b-1e3a-fd6a681e5163.md)
#### 其他资源


[异常对象成员](24ed9b18-34f0-cbe4-73a7-ac585628a990.md)