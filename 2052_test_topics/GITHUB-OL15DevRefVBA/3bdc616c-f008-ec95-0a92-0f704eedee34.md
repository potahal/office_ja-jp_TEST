
# Recipient.Type 属性 (Outlook)

返回或设置一个 **Long** 表示收件人的类型。读/写。


## 语法

 _表达式_. **Type**

 _表达式_ 一个代表 **Recipient** 对象的变量。


## 注解

根据收件人的类型，此属性返回或设置一个 **长** 对应数值相当于下列常量之一:


-  **[JournalItem](6e850295-39f9-47b8-e866-9622e9958c69.md)** 收件人： **[OlJournalRecipientType](d44d02bf-b3c6-64db-e1db-75190ae8f684.md)** 常量 **olAssociatedContact** 。
    
-  **[MailItem](14197346-05d2-0250-fa4c-4a6b07daf25f.md)** 收件人：以下 **[OlMailRecipientType](5f3f584b-c188-5c93-20c3-4dde6f2cfc3b.md)** 常量之一： **olBCC** 、 **olCC** 、 **olOriginator** 或 **olTo** 。
    
-  **[MeetingItem](b75730f5-b395-3d66-5acd-b64fd8fcd78f.md)** 收件人：以下 **[OlMeetingRecipientType](f5636886-f1db-9f64-f36c-4314133a7d12.md)** 常量之一： **olOptional** 、 **olOrganizer** 、 **olRequired** 或 **olResource** 。
    
-  **[TaskItem](5df8cfa5-5460-a5a1-a130-ba5bca1a0091.md)** 收件人：以下 **[OlTaskRecipientType](8f1678ba-f11a-1e70-816f-57b8a348a190.md)** 常量之一： **olFinalStatus** 或 **olUpdate** 。
    


此属性可能不会始终返回会议室的相应收件人类型。例如，会议室可能指定为会议要求中的必选收件人，在此情况下，此属性不会返回该会议室的  **olResource** 。

若要可靠地确定收件人是会议室，使用消息传递 API (MAPI) 属性，  **[PidTagDisplayTypeEx](http://msdn.microsoft.com/library/23074402-6ac1-47f1-8a49-b8909f98a26e%28Office.15%29.aspx)** 的 **[收件人](8cee4d79-ec55-52a4-710b-6456944ca86d.md)** 对象。您可以访问 Outlook 对象模型中使用 **[PropertyAccessor](2fc91e13-703c-3ec9-9066-ffee7144306c.md)** 对象的该属性。 **PidTagDisplayTypeEx** 属性由 MAPI 属性标记命名空间中的"http://schemas.microsoft.com/mapi/proptag/0x39050003"。请注意， **PidTagDisplayTypeEx** 属性在中不可用的 Microsoft Exchange Server 版本早于 Microsoft Exchange Server 2007 年;在 Exchange Server 这种早期版本中，可以使用 **Recipient.Type** 属性，并假定有一个 **olResource** 不同类型的收件人不是会议室。


## 示例

下面的 Visual Basic for Applications (VBA) 的示例显示如何使用 **PidTagDisplayTypeEx** 属性为每个会议请求 **[收件人](774f56b7-4de8-9584-60cd-4fbf361f4c85.md)** 集合中的 **收件人** 对象的 **PropertyAccessor** 。如果该属性的值为 7 (MAPI 常量 **DT_ROOM** MAPI 头文件中定义的值 mapidefs.h)，则该收件人是会议室。本示例假定在检查器中当前有一个会议请求。


```
Sub DemoMeetingRecipients() 
 Dim myAppointment As Outlook.AppointmentItem 
 Dim myPA As Outlook.PropertyAccessor 
 Dim d As Long 
 Dim myInt As Long 
 
 Set myAppointment = Application.ActiveInspector.CurrentItem 
 
 For d = 1 To myAppointment.Recipients.count 
 Debug.Print myAppointment.Recipients.item(d).name 
 Debug.Print myAppointment.Recipients.item(d).Type 
 Set myPA = myAppointment.Recipients.item(d).PropertyAccessor 
 myInt = myPA.GetProperty("http://schemas.microsoft.com/mapi/proptag/0x39050003") 
 Debug.Print myInt 
 Debug.Print "---" 
 Next d 
End Sub 

```

以下 VBA 示例使用 **[CreateItem](e5fbf367-db16-5042-823e-68e6b805e612.md)** 来创建约会，并使用 **[MeetingStatus](cfd970cd-df6c-4537-0a17-b5adab3b667f.md)** 会议将状态设置为"会议"，以将其转换成的必选和可选与会者的会议要求。使用有效的名称以避免错误，应更换收件人的姓名。




```
Sub ScheduleMeeting() 
 Dim myItem as Outlook.AppointmentItem 
 Dim myRequiredAttendee As Outlook.Recipient 
 Dim myOptionalAttendee As Outlook.Recipient 
 Dim myResourceAttendee As Outlook.Recipient 
 
 Set myItem = Application.CreateItem(olAppointmentItem) 
 myItem.MeetingStatus = olMeeting 
 myItem.Subject = "Strategy Meeting" 
 myItem.Location = "Conference Room B" 
 myItem.Start = #9/24/2003 1:30:00 PM# 
 myItem.Duration = 90 
 Set myRequiredAttendee = myItem.Recipients.Add ("Nate Sun") 
 myRequiredAttendee.Type = olRequired 
 Set myOptionalAttendee = myItem.Recipients.Add ("Kevin Kennedy") 
 myOptionalAttendee.Type = olOptional 
 Set myResourceAttendee = myItem.Recipients.Add("Conference Room B") 
 myResourceAttendee.Type = olResource 
 myItem.Display 
End Sub
```


## 另请参阅


#### 概念


[收件人对象](8cee4d79-ec55-52a4-710b-6456944ca86d.md)
#### 其他资源


[收件人对象成员](70e34018-95de-7fcf-1331-9be61a8675a2.md)
[引用的命名空间属性](http://msdn.microsoft.com/library/c1c7bfa9-64d7-81d2-84e7-f0a4c57780b3%28Office.15%29.aspx)