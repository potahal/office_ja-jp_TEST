
# RecurrencePattern.DayOfWeekMask 属性 (Outlook)

返回或设置一个  **[OlDaysOfWeek](49712ccf-4ff7-bfdc-c023-94bcd0d97046.md)** 常量，该常量代表定期约会或任务在星期几发生的掩码。可读/写。


## 语法

 _表达式_. **DayOfWeekMask**

 _表达式_ 一个代表 **RecurrencePattern** 对象的变量。


## 注解

已设置 **[介于 1](bc9b35b5-ef00-e5cf-09cc-ee8743efddcf.md)** 和 **[PatternEndDate](0f78ea71-3d92-2d38-be10-e05ab7bcf44a.md)** 和 **[PatternStartDate](20c82dbd-a622-91b6-618c-7cbe8bff2ca7.md)** 之前设置属性后，则应设置 **年** 。

按月和按年模式才对单独的天有效。每周一次的模式仅为 **或** 的 **年** 有效。


## 示例

此示例的 Visual Basic for Applications 使用 **[GetRecurrencePattern](a9f67c5b-a77f-4e34-e654-d12560a6dba0.md)** 的 **[RecurrencePattern](36c098f7-59fb-879a-5173-ed0260d13fa4.md)** 对象获取新创建的 **[AppointmentItem](204a409d-654e-27aa-643a-8344c631b82d.md)** 。设置属性、 **[介于 1](bc9b35b5-ef00-e5cf-09cc-ee8743efddcf.md)** 、 **年** 、 **PatternStartDate** 、 **PatternEndDate** 、 **[持续时间](91cceed3-fd56-bae3-ee00-16f4b02eb2e3.md)** 、 **[开始时间](557e0f8d-c95d-e1f9-91a2-0734248d8628.md)** 、 **[结束时间](7babda13-9e57-4c80-1ab3-56025753ed9d.md)** 和 **[主题](57f0f242-6d04-175f-4ea2-25145787f5bd.md)** ，约会是保存，则显示与该模式:"发生，每个星期一、 星期三和星期五生效 10/2006 年 7 月至 2006 08/25/从下午 2:00 到下午 3:00。"


```
Sub RecurringAppointmentEveryMondayWednesdayFriday() 
 
 Dim oAppt As AppointmentItem 
 
 Dim oPattern As RecurrencePattern 
 
 Set oAppt = Application.CreateItem(olAppointmentItem) 
 
 Set oPattern = oAppt.GetRecurrencePattern 
 
 With oPattern 
 
 .RecurrenceType = olRecursWeekly 
 
 .DayOfWeekMask = olMonday Or olWednesday Or olFriday 
 
 .PatternStartDate = #7/10/2006# 
 
 .PatternEndDate = #8/25/2006# 
 
 .Duration = 60 
 
 .StartTime = #2:00:00 PM# 
 
 .EndTime = #3:00:00 PM# 
 
 End With 
 
 oAppt.Subject = "Recurring Appointment Monday Wednesday Friday" 
 
 oAppt.Save 
 
 oAppt.Display 
 
End Sub
```

与上一个示例类似，此 Visual Basic for Applications 示例还使用 **GetRecurrencePattern** 来获取新创建的 **AppointmentItem** 的 **RecurrencePattern** 对象。设置属性、 **介于 1** 、 **年** 、 **PatternStartDate** 、 **PatternEndDate** 、 **持续时间** 、 **开始时间** 、 **结束时间** 和 **主题** ，约会是保存，则显示与该模式:"发生，每个星期一、 星期二、 星期三、 星期四和星期五生效 10/2006 年 7 月至 2006 年 8 月 4 日。"




```
Sub RecurringEventEveryWeekday() 
 
 Dim oPattern As Outlook.RecurrencePattern 
 
 Dim oAppt As Outlook.AppointmentItem 
 
 Set oAppt = Application.CreateItem(olAppointmentItem) 
 
 Set oPattern = oAppt.GetRecurrencePattern 
 
 With oPattern 
 
 .RecurrenceType = olRecursWeekly 
 
 .DayOfWeekMask = olMonday Or olTuesday Or olWednesday Or olThursday Or olFriday 
 
 .PatternStartDate = #7/10/2006# 
 
 .PatternEndDate = #8/4/2006# 
 
 .Duration = 1440 'Duration in minutes, for all day event = 24 * 60 
 
 .StartTime = #12:00:00 AM# 
 
 .EndTime = #12:00:00 AM# 
 
 End With 
 
 oAppt.Subject = "Recurring Event Every Weekday" 
 
 oAppt.Save 
 
 oAppt.Display 
 
End Sub 
 

```


## 另请参阅


#### 概念


[RecurrencePattern 对象](36c098f7-59fb-879a-5173-ed0260d13fa4.md)
#### 其他资源


[RecurrencePattern 对象成员](d282fdb2-2b6d-983d-fe5f-698113d35f89.md)