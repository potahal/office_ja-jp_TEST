
# MailItem.Sender 属性 (Outlook)

返回或设置一个 [AddressEntry](d4a0a85e-8bab-bc56-57bc-d70c3c570c8e.md) 对象，该对象对应于从其发送[MailItem](14197346-05d2-0250-fa4c-4a6b07daf25f.md) 的帐户的用户。可读/写。


## 语法

 _表达式_. **Sender**

 _表达式_ 一个代表 **MailItem** 对象的变量。


## 注解

在会话中，如果在配置文件中定义了多个帐户，则可以设置此属性以指定从中发送邮件项目的帐户。将此属性设置为由特定帐户的[CurrentUser](e17ab6a9-344e-b3bf-543c-07590c406a2b.md) 属性表示的用户的 **AddressEntry** 对象。

如果将  **Sender** 属性设置为无权在该帐户上发送邮件的 **AddressEntry** ，则 Microsoft Outlook 将引发错误。


## 示例

Michael Bauer 提供了以下代码示例。Michael 是一位 [Microsoft Most Valuable Professional](https://mvp.microsoft.com/zh-cn/default.aspx
)，拥有 Visual Basic 和 Visual Basic for Applications (VBA) 中的开发 Outlook 解决方案的专业知识。Michael 负责维护一个专业网站，网址为 [VBOffice.net](http://www.vboffice.net/index.mdl?lang=en)。

下面的 VBA 代码示例演示如何显示电子邮件发件人的详细信息。如果发件人对应于用户的 Outlook 联系人通讯簿 (CAB) 中的联系人，则代码示例在检查器中显示关于该联系人的信息。如果发件人不是用户的 CAB 中的联系人，此代码示例在对话框中显示来自用户的地址条目（取自传输提供程序的通讯簿容器）的详细信息。 

若要显示发件人的相关信息，用户应已在浏览器中选择  **MailItem** 。该代码示例还检查是否已发送所选 **MailItem** ，因为 **Sender** 属性仅在已发送 **MailItem** 时定义。然后，该示例访问 **Sender** 属性来获取 **AddressEntry** 对象，该对象对应于该项邮件项目的发件人，并显示联系人信息（如果存在）；否则，该示例显示地址条目详细信息。




```
 
Public Sub DisplaySenderDetails() 
 Dim Explorer As Outlook.Explorer 
 Dim CurrentItem As Object 
 Dim Sender As Outlook.AddressEntry 
 Dim Contact As Outlook.ContactItem 
 
 Set Explorer = Application.ActiveExplorer 
 
 ' Check whether any item is selected in the current folder. 
 If Explorer.Selection.Count Then 
 
 ' Get the first selected item. 
 Set CurrentItem = Explorer.Selection(1) 
 
 ' Check for the type of the selected item as only the 
 ' MailItem object has the Sender property. 
 If CurrentItem.Class = olMail Then 
 Set Sender = CurrentItem.Sender 
 
 ' There is no sender if the item has not been sent yet. 
 If Sender Is Nothing Then 
 MsgBox "There's no sender for the current email", vbInformation 
 Exit Sub 
 End If 
 
 Set Contact = Sender.GetContact 
 
 If Not Contact Is Nothing Then 
 ' The sender is stored in the contacts folder, 
 ' so the contact item can be displayed. 
 Contact.Display 
 
 Else 
 ' If the contact cannot be found, display the 
 ' address entry in the properties dialog box. 
 Sender.Details 0 
 End If 
 End If 
 End If 
End Sub
```


## 另请参阅


#### 概念


[MailItem 对象](14197346-05d2-0250-fa4c-4a6b07daf25f.md)
#### 其他资源


[MailItem 对象成员](1094d7df-ee80-a4b0-5a21-db2979506e6b.md)
[操作方法：基于当前文件夹为特定帐户创建可发送项目](http://msdn.microsoft.com/library/758e2e9c-3633-2e77-b9e0-14bb8078cf0b%28Office.15%29.aspx)