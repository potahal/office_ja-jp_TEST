
# CalendarSharing 对象 （Outlook）

代表用于共享日历信息的一组实用程序。


## 说明

您可以使用表示日历文件夹来创建 **CalendarSharing** 对象的 **[文件夹](3cf6cda8-6d70-666e-2643-9d9c5b9cacfc.md)** 对象的 **[GetCalendarExporter](7c67e208-65dd-8904-4b6f-8ec2df4e530d.md)** 方法。 **GetCalendarExporter** 方法仅可在日历文件夹。如果在代表其他文件夹类型的 **Folder** 对象上使用方法，就会出错。

可以使用  **[SaveAsICal](2314f751-77c5-9b95-05fb-c3075f512508.md)** 方法将日历信息保存在 iCalendar (.ics) 文件中，将日历以 URL 形式共享；也可以使用 **[ForwardAsICal](b796a573-784b-6725-535e-fd156a3f233c.md)** 方法创建 **[MailItem](14197346-05d2-0250-fa4c-4a6b07daf25f.md)** ，将日历以有效负载形式共享。


 **注释**   **CalendarSharing** 对象仅支持导出 iCalendar 格式。


## 示例

下面的 Visual Basic for Applications (VBA) 示例创建 **CalendarSharing** 对象的日历文件夹中，然后将整个文件夹 (包括附件和私人性质项目) 中的内容导出到 iCalendar 日历 (.ics) 文件。


```
Public Sub ExportEntireCalendar() 
 
 
 
 Dim oNamespace As NameSpace 
 
 Dim oFolder As Folder 
 
 Dim oCalendarSharing As CalendarSharing 
 
 
 
 On Error GoTo ErrRoutine 
 
 
 
 ' Get a reference to the Calendar default folder 
 
 Set oNamespace = Application.GetNamespace("MAPI") 
 
 Set oFolder = oNamespace.GetDefaultFolder(olFolderCalendar) 
 
 
 
 ' Get a CalendarSharing object for the Calendar default folder. 
 
 Set oCalendarSharing = oFolder.GetCalendarExporter 
 
 
 
 ' Set the CalendarSharing object to export the contents of 
 
 ' the entire Calendar folder, including attachments and 
 
 ' private items, in full detail. 
 
 With oCalendarSharing 
 
 .CalendarDetail = olFullDetails 
 
 .IncludeAttachments = True 
 
 .IncludePrivateDetails = True 
 
 .IncludeWholeCalendar = True 
 
 End With 
 
 
 
 ' Export calendar to an iCalendar calendar (.ics) file. 
 
 oCalendarSharing.SaveAsICal "C:\SampleCalendar.ics" 
 
 
 
EndRoutine: 
 
 On Error GoTo 0 
 
 Set oCalendarSharing = Nothing 
 
 Set oFolder = Nothing 
 
 Set oNamespace = Nothing 
 
Exit Sub 
 
 
 
ErrRoutine: 
 
 Select Case Err.Number 
 
 Case 287 ' &amp;H0000011F 
 
 ' The user denied access to the Address Book. 
 
 ' This error occurs if the code is run by an 
 
 ' untrusted application, and the user chose not to 
 
 ' allow access. 
 
 MsgBox "Access to Outlook was denied by the user.", _ 
 
 vbOKOnly, _ 
 
 Err.Number &amp; " - " &amp; Err.Source 
 
 Case -2147467259 ' &amp;H80004005 
 
 ' Export failed. 
 
 ' This error typically occurs if the CalendarSharing 
 
 ' method cannot export the calendar information because 
 
 ' of conflicting property settings. 
 
 MsgBox Err.Description, _ 
 
 vbOKOnly, _ 
 
 Err.Number &amp; " - " &amp; Err.Source 
 
 Case -2147221233 ' &amp;H8004010F 
 
 ' Operation failed. 
 
 ' This error typically occurs if the GetCalendarExporter method 
 
 ' is called on a folder that doesn't contain calendar items. 
 
 MsgBox Err.Description, _ 
 
 vbOKOnly, _ 
 
 Err.Number &amp; " - " &amp; Err.Source 
 
 Case Else 
 
 ' Any other error that may occur. 
 
 MsgBox Err.Description, _ 
 
 vbOKOnly, _ 
 
 Err.Number &amp; " - " &amp; Err.Source 
 
 End Select 
 
 
 
 GoTo EndRoutine 
 
End Sub
```


## 另请参阅


#### 其他资源


[Outlook 对象模型引用](http://msdn.microsoft.com/library/73221b13-d8d8-99b8-3394-b95dbbfd5ddc%28Office.15%29.aspx)
[CalendarSharing 对象成员](1b2b6233-9816-e3f2-5924-694ce30cc8ef.md)