
# Rules.Create 方法 （Outlook）

创建一个  **[Rule](ea2ddbcc-fd65-a636-c6da-79950033f385.md)** 对象，该对象的名称由 _Name_ 指定，规则类型由 _RuleType_ 指定。


## 语法

 _表达式_. **Create**( ** _Name_**, ** _RuleType_** )

 _表达式_ 一个代表 **Rules** 对象的变量。


### 参数



|**名称**|**必需/可选**|**数据类型**|**说明**|
|:-----|:-----|:-----|:-----|
| _Name_|必需|**String**|在创建之后将由  **[Rule.Name](6c559ffe-b25c-ff49-31d1-1fd44935a8f3.md)** 代表的规则的字符串标识符。集合中规则的名称不是唯一的。|
| _RuleType_|必需|**[OlRuleType](a9ef16ad-78cf-8c26-0897-39a0bf1a25b1.md)**|确定是否将该规则应用上发送或接收消息的 **OlRuleType** 枚举中的一个常数。|

### 返回值

表示新创建的规则的 **规则** 对象。


## 说明

 _RuleType_ 参数所添加的规则确定有效规则操作和规则条件，可与 **Rule** 对象关联的规则例外条件。

当一个规则添加到集合中时，  **[Rule.ExecutionOrder](070d50ca-4b0b-5629-1609-81ab8a3620d1.md)** 的新规则将为 1。集合中的其他规则 **ExecutionOrder** 就会递增 1。


## 示例

下面的 Visual Basic for Applicatons (VBA) 代码示例使用 Rules 对象模型创建规则。在该代码示例中，除非邮件主题中包含某些词语，否则将使用  **[RuleAction](6451788f-e5ed-239c-a34d-b564b52d8955.md)** 和 **[RuleCondition](e03f91c2-2c08-b036-104a-d6246f28bc2d.md)** 对象来指定将来自特定发件人的邮件转发到特定文件夹的规则。请注意，该代码示例假定"收件箱"下已经存在一个"Dan"文件夹。


```
Sub CreateRule() 
 
 Dim colRules As Outlook.Rules 
 
 Dim oRule As Outlook.Rule 
 
 Dim colRuleActions As Outlook.RuleActions 
 
 Dim oMoveRuleAction As Outlook.MoveOrCopyRuleAction 
 
 Dim oFromCondition As Outlook.ToOrFromRuleCondition 
 
 Dim oExceptSubject As Outlook.TextRuleCondition 
 
 Dim oInbox As Outlook.Folder 
 
 Dim oMoveTarget As Outlook.Folder 
 
 
 
 'Specify target folder for rule move action 
 
 Set oInbox = Application.Session.GetDefaultFolder(olFolderInbox) 
 
 'Assume that target folder already exists 
 
 Set oMoveTarget = oInbox.Folders("Dan") 
 
 
 
 'Get Rules from Session.DefaultStore object 
 
 Set colRules = Application.Session.DefaultStore.GetRules() 
 
 
 
 'Create the rule by adding a Receive Rule to Rules collection 
 
 Set oRule = colRules.Create("Dan's rule", olRuleReceive) 
 
 
 
 'Specify the condition in a ToOrFromRuleCondition object 
 
 'Condition is if the message is sent by "DanWilson" 
 
 Set oFromCondition = oRule.Conditions.From 
 
 With oFromCondition 
 
 .Enabled = True 
 
 .Recipients.Add ("DanWilson") 
 
 .Recipients.ResolveAll 
 
 End With 
 
 
 
 'Specify the action in a MoveOrCopyRuleAction object 
 
 'Action is to move the message to the target folder 
 
 Set oMoveRuleAction = oRule.Actions.MoveToFolder 
 
 With oMoveRuleAction 
 
 .Enabled = True 
 
 .Folder = oMoveTarget 
 
 End With 
 
 
 
 'Specify the exception condition for the subject in a TextRuleCondition object 
 
 'Exception condition is if the subject contains "fun" or "chat" 
 
 Set oExceptSubject = _ 
 
 oRule.Exceptions.Subject 
 
 With oExceptSubject 
 
 .Enabled = True 
 
 .Text = Array("fun", "chat") 
 
 End With 
 
 
 
 'Update the server and display progress dialog 
 
 colRules.Save 
 
End Sub 
 

```


## 另请参阅


#### 概念


[规则对象](dd41b4de-bf5f-5532-46c9-394a5d078bec.md)
#### 其他资源


[规则对象成员](39fb5418-ff5a-1714-d3b5-07cc28893821.md)