
# Méthode DoCmd.TransferText (Access)

La méthode  **TransferText** exécute l'action **TransferText** dans Visual Basic.
 


## Syntaxe

*expression* . **TransferText**( ***TransferType***, ***SpecificationName***, ***TableName***, ***FileName***, ***HasFieldNames***, ***HTMLTableName***, ***CodePage*** )
 

 
*expression* Variable représentant un objet **DoCmd**.
 

 

### Paramètres



|**Nom**|**Obligatoire/Facultatif**|**Type de données**|**Description**|
|:-----|:-----|:-----|:-----|
| _TransferType_|Facultatif|**[AcTextTransferType](56aeb63e-e249-9267-3184-9a370f766fd2.md)**|Type de transfert que vous voulez opérer. Vous pouvez importer, exporter ou lier des données dans des fichiers texte ou HTML délimités ou de longueur fixe HTML. La valeur par défaut est  **acImportDelim**. Seuls les types de transferts **acImportDelim**, **acImportFixed**, **acExportDelim**, **acExportFixed** ou **acExportMerge** sont pris en charge dans un projet Microsoft Access (.adp).|
| _SpecificationName_|Facultatif|**Variant**|Expression chaîne qui correspond au nom du format d'importation ou d'exportation que vous avez créé et enregistré dans la base de données active. Dans le cas d'un fichier texte de longueur fixe, vous devez spécifier un argument ou utiliser un fichier schema.ini, qui doit être stocké dans le même dossier que le fichier texte importé, attaché ou exporté. Vous pouvez créer un fichier schéma à l'aide de l'Assistant d'importation/exportation de texte. Dans le cas des fichiers textes délimités et des fichiers de données de publipostage Microsoft Word, vous pouvez laisser cet argument vide afin de sélectionner les caractéristiques d'importation/exportation par défaut.|
| _TableName_|Facultatif|**Variant**|Expression chaîne qui correspond au nom de la table Microsoft Access dans laquelle vous voulez importer des données, de laquelle vous voulez exporter les données ou à laquelle vous voulez attacher des données. Il peut s'agir également d'une requête Microsoft Access dont vous voulez exporter les résultats dans une feuille de calcul.|
| _FileName_|Facultatif|**Variant**|Expression chaîne qui représente le nom et le chemin du fichier texte dont vous voulez importer les données, dans lequel vous voulez exporter des données ou auquel vous voulez attacher des données.|
| _HasFieldNames_|Facultatif|**Variant**|Utilisez  **True** (?1) pour utiliser les noms de champs de la première ligne du fichier texte lors d'une importation, exportation ou attache. Utilisez **False** (0) pour que Microsoft Access considère la première ligne du fichier texte comme étant des données normales. Si vous laissez cet argument vierge, la valeur par défaut ( **False** ) est choisie. Cet argument est ignoré pour les fichiers de données de publipostage MSWord pour Windows, dans lesquels la première ligne doit toujours contenir les noms des champs.|
| _HTMLTableName_|Facultatif|**Variant**|Expression de chaîne qui correspond au nom de la table ou de la liste dans le fichier HTML que vous voulez importer ou attacher. Cet argument n'est pas pris en compte sauf si l'argument  _TransferType_ a la valeur **acImportHTML** ou **acLinkHTML**. Si vous laissez cet argument vierge, la première table ou liste du fichier HTML est importée ou attachée. Le nom de la table ou de la liste dans le fichier HTML est déterminé par le texte spécifié par la balise **<CAPTION>**, s'il en existe une. S'il n'y a pas de balise  **<CAPTION>**, le nom est déterminé par le texte spécifié par la balise  **<TITLE>**. Si plusieurs tables ou listes portent le même nom, Microsoft Access les distingue en ajoutant un numéro à la fin de chaque nom de table ou de liste ; par exemple, Employees1 et Employees2.|
|CodePage[](https://msdn.microsoft.com/fr-fr/library/windows/desktop/dd317756%28v=vs.85%29.aspx)|Facultatif|**Variant**|Valeur  **Long** indiquant le jeu de caractères de la page de code.|

## Remarques

Vous pouvez utiliser la méthode  **TransferText** pour échanger du texte entre la base de données ou le projet (.adp) Microsoft Access en cours et un fichier texte. Vous pouvez également lier les données d'un fichier texte à la base de données Access en cours. Avec un fichier texte lié, vous pouvez consulter les données de texte avec Access, tout en autorisant un accès complet aux données de votre programme de traitement de texte. Vous pouvez également effectuer les opérations d'importation, d'exportation et de liaison de table ou de liste dans un fichier HTML (*.html).
 

 
Vous pouvez exporter les données figurant dans des requêtes de sélection Access dans des fichiers textes. Access exporte le jeu de résultats de la requête comme s'il s'agissait d'une table.
 

 

## Exemple

Cet exemple exporte les données de la table External Report (État externe) de Microsoft Access dans le fichier texte délimité April.doc en utilisant le format Sortie standard :
 

 

```
DoCmd.TransferText acExportDelim, "Standard Output", _ 
    "External Report", "C:\Txtfiles\April.doc"
```

Le code suivant montre comment créer un document Microsoft Word et effectuer un publipostage avec les données stockées dans la table Clients.
 

 
 **Exemple de code fourni par :**[Référence du programmeur Microsoft Access 2010](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl)
 

 



```
Public Sub DoMailMerge(strFileSavePath As String)

    ' Create new Word App, add a document and set it visible
    Dim wdApp As New Word.Application
    wdApp.Documents.Add
    wdApp.Visible = True

    ' Open the data set from this database
    wdApp.ActiveDocument.MailMerge.OpenDataSource _
        Name:=Application.CurrentProject.FullName, _
        OpenExclusive:=False, _
        LinkToSource:=True, _
        Connection:="TABLE Customers", _
        SQLStatement:="SELECT Customers.* FROM Customers;"
              
    ' Add fields to the mail merge document
    Dim oSel As Object
    Set oSel = wdApp.Selection
    With wdApp.ActiveDocument.MailMerge.Fields
    
        oSel.TypeText vbNewLine &amp; vbNewLine
        .Add oSel.range, "First_Name"
        oSel.TypeText " "
        .Add oSel.range, "Last_Name"
        oSel.TypeText vbNewLine
        .Add oSel.range, "Company"
        oSel.TypeText vbNewLine
        .Add oSel.range, "Address"
        oSel.TypeText vbNewLine
        .Add oSel.range, "City"
        oSel.TypeText ", "
        .Add oSel.range, "State"
        oSel.TypeText " "
        .Add oSel.range, "Zip"
        oSel.TypeText vbNewLine
        oSel.TypeParagraph
        oSel.TypeText "Dear "
        .Add oSel.range, "First_Name"
        oSel.TypeText ","
        oSel.TypeText vbNewLine
        oSel.TypeParagraph
        oSel.TypeText "We have created this mail just for you..."
        oSel.TypeText vbNewLine
        oSel.TypeText vbNewLine
        oSel.TypeText "Sincerely," &amp; vbNewLine &amp; "John Q. Public"
        oSel.TypeText vbFormFeed
        
    End With
    
    ' Execute the mail merge and save the document
    wdApp.ActiveDocument.MailMerge.Execute
    wdApp.ActiveDocument.SaveAs strFileSavePath
        
    ' Close everything and Cleanup Variables
    Set oSel = Nothing
    wdApp.ActiveDocument.Close False
    Set wdApp = Nothing

End Sub
```


## À propos des collaborateurs
<a name="AboutContributors"> </a>

Wrox Press est guidé par la philosophie de programmeur à programmeur. Les livres Wrox sont écrits par des programmeurs pour des programmeurs et la marque Wrox est synonyme de solutions de référence aux problèmes de programmation concrets.
 

 

## Voir aussi
<a name="AboutContributors"> </a>


#### Concepts


 
[Objet DoCmd](3ce44cca-9979-0a1e-9787-079a52ce528f.md)
#### Autres ressources


 
[Membres de l'objet DoCmd](3e7ade9e-86e4-0751-188b-5d31c9101651.md)